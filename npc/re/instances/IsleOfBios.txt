//===== rAthena Script =======================================
//= Isle Of Bios
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Isle Of Bios Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

help_lobby,127,162,5	script	Ceifador Ankou#bios	10092,{
	if (BaseLevel < 255) {
		mes "^ff0000Personagens abaixo do nível 255 não podem entrar nesta masmorra.^000000";
		close;
	}
	if (isbegin_quest(15006) == 0) {
		mes "Você primeiro deve falar com o ^ff0000Velho Homem^000000 para ter acesso a esta masmorra.";
		close;
	}
	.@party_id = getcharid(1);
	if (.@party_id < 1) {
		mes "^ff0000Você primeiro deve formar ou participar de um grupo de pelo menos 1 pessoa para entrar nesta masmorra.^000000";
		close;
	}
	switch( checkquest(15005,PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		if (checkquest(15007,PLAYTIME) == 2) {// 5 mins to re-enter
			mes "^ff0000Este Calabouço Memorial não pode ser acessado por 23 horas após sua última visita.^000000";
			close;
		}
		switch( checkquest(15008,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "^0000ffEste Calabouço Memorial não pode ser acessado por 23 horas após sua última visita.^000000";
			close;
		case 2:
			break;
		}
		break;
	case 2:
		erasequest 15005;// Memorial Dungeon: Isle of Bios
		erasequest 15007;// Memorial Dungeon: Isle of Bios
		if (isbegin_quest(15008) > 0)
			erasequest 15008;// Memorial Dungeon: Isle of Bios
		break;
	}
	if (is_party_leader() == true)
		.@create$ = "Crie a Masmorra Memorial.";
	switch( select( .@create$, "Entre na Ilha de Bios.", "Cancelar" ) ) {
	case 1:
		instance_create("Isle of Bios");
		mes "^ff0000Criou a Masmorra Memorial.^000000";
		mes "^ff0000Por favor, clique e pressione Enter novamente.^000000";
		close;
	case 2:
		switch( instance_enter("Isle of Bios") ) {
		case IE_NOMEMBER:
		case IE_NOINSTANCE:
		case IE_OTHER:
			if (is_party_leader(.@party_id) == false) {
				mes "^ff0000Observe que este Calabouço Memorial não pode ser acessado por 23 horas após sua última visita.^000000";
				next;
			}
			mes "^ff0000O líder do seu grupo ainda não criou o portal para entrar.^000000";
			close;
		case IE_OK:
			mapannounce "help_lobby", "" + strcharinfo(0) + " do grupo, " + getpartyname( getcharid(1) ) + ", está entrando na ilha de Bios.",bc_map,"0x00FF99";
			if (isbegin_quest(15005) == 0) {
				setquest 15005;// Memorial Dungeon: Isle of Bios
				setquest 15007;// Memorial Dungeon: Isle of Bios
			}
			end;
		}
	case 3:
		end;
	}
	end;

OnInit:
	while(true) {
		sleep 500;
		specialeffect EF_ENHANCE;
	}
	end;
}

help_lobby,122,160,5	script	Velho Homem#sara	4_M_EINOLD,{
	if (BaseLevel < 255) {
		mes "[Velho Homem]";
		mes "...";
		mes "Receio que você ainda não esteja preparado para isso.";
		mes "Você deve ter pelo menos nível 255.";
		close;
	}
	if (isbegin_quest(15006) == 0) {
		mes "[Velho Homem]";
		mes "...Onde estou?";
		next;
		mes "[Velho Homem]";
		mes "Não sei quando começou...";
		mes "O mesmo pesadelo está me atormentando há muito tempo.";
		next;
		mes "[Velho Homem]";
		mes "No meu pesadelo, o rei demônio Morocc acorda de seu sono de um século e enche o mundo de escuridão e horror.";
		next;
		mes "[Velho Homem]";
		mes "Não consegui me livrar das imagens vívidas que vi naquele pesadelo. Eu vaguei como um louco e finalmente cheguei aqui.";
		next;
		mes "[Velho Homem]";
		mes "Eu vi esse lugar no meu sonho. É onde Deus Demônio Morocc acorda de seu sono. Meu sonho estava me mostrando um futuro possível.";
		next;
		mes "[Velho Homem]";
		mes "Se meu sonho é realmente uma premonição, as forças do deus demônio Morocc estão dentro dessa semente amarela.";
		next;
		mes "[Velho Homem]";
		mes "Tentei entrar na masmorra, mas sem sucesso: sou velho e fraco demais. Mas talvez você possa.";
		next;
		mes "[Velho Homem]";
		mes "Por favor, investigue a masmorra.";
		mes "É muito perigoso para você ir sozinho. Você deve levar pelo menos um camarada com você.";
		setquest 15006;// Isle of Bios Exploration
		completequest 15006;// Isle of Bios Exploration
		close;
	}
	mes "[Velho Homem]";
	mes "Por favor, investigue a masmorra.";
	mes "É muito perigoso para você ir sozinho. Você deve levar pelo menos um camarada com você.";
	close;
}

// warps
1@dth1,291,102,0	warp2	warp1#bios1	2,2,1@dth2,17,93
1@dth2,291,102,0	warp2	warp1#bios2	2,2,1@dth3,45,68

// zone 1
1@dth1,74,94,3	script	Ceifador Ankou#bios1	3029,{ end; }
1@dth1,71,97,3	duplicate(Ceifador Ankou#bios1)	Zeith#bios1	4_M_GUILLOTINE
1@dth1,67,97,5	duplicate(Ceifador Ankou#bios1)	Vrid#bios1	4_M_KNIGHT_BLACK
// 1@dth1,67,97,5	script	Vrid#bios1	4_M_KNIGHT_BLACK,5,7,{// official

// 1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,{// official
1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,5,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("start1#bios1");
	'zeith$ = instance_npcname("Zeith#bios1");
	'vrid$ = instance_npcname("Vrid#bios1");
	'reaper$ = instance_npcname("Ceifador Ankou#bios1");
	initnpctimer;
	npctalk "Vrid: Ah, alguém veio se juntar a nós.", 'vrid$;
	end;
OnTimer2500:
	npctalk "Vrid: Você também veio parar a ressurreição de Morocc?", 'vrid$;
	end;
OnTimer5000:
	npctalk "Vrid: Eu sou Vrid e este é Zeith, da Prontera. Temos viajado pelo mundo, lutando pela justiça.", 'vrid$;
	end;
OnTimer7500:
	npctalk "Vrid: Ei, Zeith. Diga algo.", 'vrid$;
	end;
OnTimer10000:
	npctalk "Zeith: ...", 'zeith$;
	end;
OnTimer12500:
	npctalk "Zeith: Eu me chamo Zeith.", 'zeith$;
	end;
OnTimer15000:
	npctalk "Zeith: ...", 'zeith$;
	end;
OnTimer17500:
	npctalk "Vrid: Não ligue para ele. Ele não está tão quieto quando somos apenas dois.", 'vrid$;
	end;
OnTimer19500:
	npctalk "Zeith: ...Você fala o suficiente para nós dois.", 'zeith$;
	end;
OnTimer21000:
	npctalk "Vrid: Só viemos porque o velhote lá embaixo continuava divulgando sobre a ressurreição de Morocc.", 'vrid$;
	end;
OnTimer23500:
	npctalk "Vrid: Err, que lugar é esse? Acabei de subir a masmorra e de repente o cenário mudou.", 'vrid$;
	end;
OnTimer26000:
	npctalk "Vrid: Vaca sagrada, estamos voando no céu ?!", 'vrid$;
	end;
OnTimer28500:
	npctalk "???: *Risos* Você quer saber onde está?", 'reaper$;
	end;
OnTimer31000:
	npctalk "Zeith: Quem está aí?!", 'zeith$;
	end;
OnTimer33500:
	enablenpc 'reaper$;
	npctalk "Ceifador Ankou: *Risos* Esta é a ilha de Bios, um lugar que invoquei em minha mente.", 'reaper$;
	end;
OnTimer36000:
	npctalk "Ceifador Ankou: Você entrou na armadilha, fazendo sacrifícios por meu senhor Morocc. Você não tem medo de morrer?", 'reaper$;
	end;
OnTimer38500:
	npctalk "Vrid: Pah! Se eu fosse, não teria vindo de bom grado, teria?", 'vrid$;
	end;
OnTimer41000:
	npctalk "Vrid: Quem você é, nós o matamos antes de você!", 'vrid$;
	end;
OnTimer43500:
	npctalk "Ceifador Ankou: Não me tente, humano.", 'reaper$;
	end;
OnTimer46000:
	npctalk "Ceifador Ankou: Eu quero ver você morrer lenta e dolorosamente. *Risos*", 'reaper$;
	end;
OnTimer48500:
	disablenpc 'reaper$;
	end;
OnTimer51000:
	npctalk "Vrid: Quem é ele? Ele está tentando ressuscitar Morocc? ", 'vrid$;
	end;
OnTimer53500:
	npctalk "Vrid: Vamos persegui-lo! Zeith, depressa!", 'vrid$;
	end;
OnTimer54000:
	disablenpc 'vrid$;
	end;
OnTimer56000:
	npctalk "Zeith: ...Ack.", 'zeith$;
	end;
OnTimer58500:
	npctalk "Zeith: Eu odeio correr.", 'zeith$;
	end;
OnTimer61000:
	disablenpc 'zeith$;
	end;
OnTimer66000:
	stopnpctimer;
	mapannounce 'map_dth1$, "Sistema: Elimine todos os inimigos na ilha e saia pela porta do outro lado para passar para a próxima área.",bc_map,"0xFFFF00";
	'zeith$ = 'vrid$ = 'reaper$ = "";
	end;
}

1@dth1,1,1,0	script	mobs#bios1	HIDDEN_WARP_NPC,{
	end;
OnStart:
	.@label$ = instance_npcname("mobs#bios1") + "::OnMyMobDead";
	for ( .@mob_id = 3010; .@mob_id < 3013; .@mob_id++ ) {
		switch(.@mob_id) {
		case 3010:		// Corrupt Orc Baby
			setarray .@coord[0],
				90,94, 153,76, 121,53, 135,50, 145,107, 132,126, 101,125, 119,144, 124,164, 192,145, 194,116,
				203,131, 214,142, 196,156, 217,129, 227,150, 215,109, 240,130, 252,105, 249,144, 249,155, 232,87,
				316,103, 245,71, 246,71, 210,62, 214,53, 211,88;
			break;
		case 3011:		// Corrupt Baby Desert Wolf
			setarray .@coord[0],
				90,94, 121,69, 153,76, 166,80, 131,101, 145,107, 108,156, 141,143, 165,130, 203,151,
				203,162, 219,151, 203,162, 206,169, 219,171, 227,150, 238,117, 240,140, 274,104, 306,103,
				237,80, 224,62, 233,57;
			break;
		case 3012:		// Corrupt Familiar
			setarray .@coord[0],
				143,49, 156,46, 170,67, 170,67, 131,101, 144,125, 101,125, 108,156, 135,152, 144,164,
				184,130, 204,112, 227,150, 205,98, 225,103, 228,124, 243,106, 225,89, 297,103, 223,72,
				232,69, 225,54, 211,73;
			break;
		}
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
			monster 'map_dth1$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
	}
	end;

OnMyMobDead:
	.@count = mobcount( 'map_dth1$,instance_npcname("mobs#bios1") + "::OnMyMobDead" );
	if (.@count == 10 || .@count == 5)
		mapannounce 'map_dth1$, "Sistema: " + .@count + " monstros permanecem vivos na ilha.",bc_map,"0xFFFF00";
	else if (.@count == 1)
		mapannounce 'map_dth1$, "Sistema: 1 último monstro ainda está vivo na ilha.",bc_map,"0xFFFF00";
	else if (.@count == 0) {
		mapannounce 'map_dth1$, "Sistema: Você eliminou todos os monstros. Use a saída às 3 horas para passar para a próxima área.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("mobs#bios1");
		enablenpc instance_npcname("warp1#bios1");
		enablenpc instance_npcname("start1#bios2");
		donpcevent instance_npcname("mobs#bios2") + "::OnStart";	// spawn at entrance
	}
	end;
}

// zone 2
1@dth2,67,97,0	script	start1#bios2	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname("start1#bios2");
	mapannounce 'map_dth2$, "Sistema: Elimine todos os inimigos na ilha e saia pela porta do outro lado para passar para a próxima área.",bc_map,"0xFFFF00";
	end;
}

1@dth2,1,1,0	script	mobs#bios2	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("mobs#bios2");
	.@label$ = instance_npcname("mobs#bios2") + "::OnMyMobDead";
	for ( .@mob_id = 3013; .@mob_id < 3016; ++.@mob_id ) {
		switch(.@mob_id) {
		case 3013:		// Corrupt Orc Warrior
			setarray .@coord[0],
				90,94, 153,76, 116,66, 133,47, 145,107, 137,134, 101,125, 119,144, 124,164, 194,116,
				203,131, 211,88, 215,109, 214,142, 192,145, 196,156, 217,129, 227,150, 230,89, 210,62,
				214,53, 246,71, 248,81, 252,105, 246,126, 249,144, 249,155, 316,103;
			break;
		case 3014:		// Corrupt Desert Wolf
			setarray .@coord[0],
				90,94, 121,69, 153,76, 165,84, 145,107, 131,101, 108,156, 138,140, 165,130, 203,151,
				203,162, 219,151, 206,169, 203,162, 227,150, 222,164, 238,117, 237,80, 224,62, 233,57,
				236,138, 274,104, 306,103;
			break;
		case 3015:		// Corrupt Phen
			setarray .@coord[0],
				142,54, 156,46, 170,67, 170,67, 131,101, 101,125, 108,156, 135,152, 144,164, 135,131,
				184,130, 204,112, 205,98, 225,103, 225,89, 227,150, 232,69, 223,72, 212,75, 225,54,
				247,110, 232,126, 297,103;
			break;
		}
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
			monster 'map_dth2$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
	}
	end;

OnMyMobDead:
	.@count = mobcount( 'map_dth2$,instance_npcname("mobs#bios2") + "::OnMyMobDead" );
	if (.@count == 10 || .@count == 5)
		mapannounce 'map_dth2$, "System: " + .@count + " monsters remain on the island.",bc_map,"0xFFFF00";
	else if (.@count == 1)
		mapannounce 'map_dth2$, "System: 1 last monster remains on the island.",bc_map,"0xFFFF00";
	else if (.@count == 0) {
		mapannounce 'map_dth2$, "Sistema: Você eliminou todos os monstros. Use a saída às 3 horas para passar para a próxima área.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("mobs#bios2");
		enablenpc instance_npcname("warp1#bios2");
		enablenpc instance_npcname("Zeith#bios3");
		enablenpc instance_npcname("Vrid#bios3");
		enablenpc instance_npcname("event_boss#bios3");
	}
	end;
}

// zone 3
// 1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,5,7,{// official
1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,{
	if ('reward == 1) {
		mes "[Vrid]";
		mes "Eu vim pelo prêmio";
		mes "Eu quero dar a você. Você merece isso.";
		next;
		mes "[Vrid]";
		mes "Vamos sair deste lugar assustador!";
		close2;
		getitem 6684,1;// Token Of Hero
		if (ep14_3_isle == 0)
			ep14_3_isle = 1;
		if (checkquest(15007,PLAYTIME) == 0 || checkquest(15007,PLAYTIME) == 1)
			setquest 15008;// Memorial Dungeon: Isle of Bios
		warp "help_lobby",89,145;
	}
	end;
}

1@dth3,64,74,5	script	event_boss#bios3	HIDDEN_WARP_NPC,5,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("event_boss#bios3");
	'party_id = getcharid(1);
	donpcevent instance_npcname("freeze#bios3") + "::OnStart";
	end;
}

// 1@dth3,69,67,0	script	freeze#bios3	HIDDEN_WARP_NPC,30,30,{// official
1@dth3,1,1,0	script	freeze#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("freeze#bios3");
	'reaper3$ = instance_npcname("Grim Reaper Ankou#bios3");
	'zeith3$ = instance_npcname("Zeith#bios3");
	'vrid3$ = instance_npcname("Vrid#bios3");
	initnpctimer;
	npctalk "Vrid: Você tem sorte de estar aqui. Eu me perdi totalmente no momento em que entrei naquele labirinto.", 'vrid3$;
	end;
OnTimer2500:
	npctalk "Vrid: Se não fosse por Zeith, eu ainda estaria lá, vagando. Obrigado, Zeith.", 'vrid3$;
	end;
OnTimer5000:
	npctalk "Zeith: ...Algo está me incomodando.", 'zeith3$;
	end;
OnTimer7500:
	npctalk "Vrid: E aí?", 'vrid3$;
	end;
OnTimer10000:
	npctalk "Zeith: Eu tenho a sensação de que os monstros estavam ficando mais fortes com o tempo.", 'zeith3$;
	end;
OnTimer12500:
	npctalk "Zeith: Assim como crianças crescendo em adultos.", 'zeith3$;
	end;
OnTimer15000:
	npctalk "Vrid: Acho que você está certo.", 'vrid3$;
	end;
OnTimer17500:
	npctalk "Vrid: Eles são todos pequenos no começo, e então enfrentamos os maiores e mais fortes ao longo do tempo.", 'vrid3$;
	end;
OnTimer20000:
	npctalk "Vrid: Para mim, isso parece natural. Com o que você está preocupado?", 'vrid3$;
	end;
OnTimer22500:
	npctalk "Zeith: Bem, estou curioso sobre o monstro que enfrentaremos agora.", 'zeith3$;
	end;
OnTimer25000:
	npctalk "Zeith: Sejam versões mais fortes de seus eus originais ou algo completamente diferente.", 'zeith3$;
	end;
OnTimer27000:
	enablenpc 'reaper3$;
	end;
OnTimer27500:
	npctalk "Ceifador Ankou: *Risos* Pelo menos um de vocês tem uma boa cabeça nos ombros.", 'reaper3$;
	end;
OnTimer30000:
	npctalk "Vrid: Você Denovo?", 'vrid3$;
	end;
OnTimer32500:
	npctalk "Ceifador Ankou: Você está curioso sobre seus próximos adversários?", 'reaper3$;
	end;
OnTimer35000:
	npctalk "Ceifador Ankou: À sua frente está um exército do imortal.", 'reaper3$;
	end;
OnTimer37500:
	npctalk "Ceifador Ankou: *Risos* Saia, meus soldados!", 'reaper3$;
	end;
OnTimer37700:
	specialeffect EF_READYPORTAL,AREA,'reaper3$;
	end;
OnTimer40000:
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			enablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	end;
OnTimer42000:
	npctalk "Vrid: Cale a boca, seu demônio! Não temos tempo para seus lacaios!", 'vrid3$;
	end;
OnTimer45000:
	npctalk "Vrid: Eu vou me livrar de você mesmo!", 'vrid3$;
	end;
OnTimer47500:
	npctalk "Ceifador Ankou: De volta para você, bigmouth.", 'reaper3$;
	end;
OnTimer50000:
	npctalk "Ceifador Ankou: Vou te ensinar como é aterrorizante ficar desamparado diante do horror!", 'reaper3$;
	end;
OnTimer52500:
	specialeffect EF_STORMGUST,AREA,'reaper3$;
	end;
OnTimer52700:
	specialeffect EF_FREEZED,AREA,'zeith3$;
	specialeffect EF_FREEZED,AREA,'vrid3$;
	specialeffect EF_FIREHIT,AREA,'zeith3$;
	specialeffect EF_FIREHIT,AREA,'vrid3$;
	setnpcdisplay( 'vrid3$, 4_M_FROZEN_KN );
	setnpcdisplay( 'zeith3$, 4_M_FROZEN_GC );

	getpartymember 'party_id, 0, .@player_name$;
	getpartymember 'party_id, 1, .@char_id;
	getpartymember 'party_id, 2, .@account_id;
	for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
		if (isloggedin(.@account_id[.@i],.@char_id[.@i]) == true) {
			getmapxy .@map$,.@x,.@y, BL_PC, .@player_name$[.@i];
			if (.@map$ == 'map_dth3$ && distance(.@x,.@y,69,67) < 30)
				sc_start SC_FREEZE,17500,0,10000, (SCSTART_NOAVOID|SCSTART_NOTICKDEF|SCSTART_NORATEDEF), .@account_id[.@i];
		}
	}
	end;
OnTimer55000:
	npctalk "Vrid: Ugh! O que está acontecendo ?! Não posso me mexer!", 'vrid3$;
	end;
OnTimer57500:
	npctalk "Zeith: N-nem eu! Não consigo mexer os pés!", 'zeith3$;
	end;
OnTimer60000:
	npctalk "Ceifador Ankou: Na Ilha de Bios, cada dia é uma vida.", 'reaper3$;
	end;
OnTimer62500:
	npctalk "Ceifador Ankou: Você deve ter notado que meus soldados cresceram antes do pôr do sol.", 'reaper3$;
	end;
OnTimer65000:
	npctalk "Ceifador Ankou: Após o anoitecer, eles chegaram ao estágio final de seu desenvolvimento: a vida além da morte.", 'reaper3$;
	end;
OnTimer67500:
	npctalk "Ceifador Ankou: Está certo. É hora de você experimentar a morte.", 'reaper3$;
	end;
OnTimer70000:
	npctalk "Ceifador Ankou: Prepare-se para sentir o medo da morte! *Risos*", 'reaper3$;
	// note: unfreeze player
	end;
OnTimer72000:
	disablenpc 'reaper3$;
	end;
OnTimer72500:
	npctalk "Vrid: Droga, os mortos-vivos estão chegando, mas não posso lutar porque não consigo mexer meus pés!", 'vrid3$;
	end;
OnTimer75000:
	npctalk "Vrid: Ack, eu não quero morrer!", 'vrid3$;
	end;
OnTimer77500:
	npctalk "Zeith: ...Ugh...", 'zeith3$;
	end;
OnTimer80000:
	stopnpctimer;
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	.@label$ = instance_npcname("freeze#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Orc Zombie",3016,1, .@label$;// EP14_3_DEATH_C_MOB1
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("freeze#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("freeze#bios3");
		donpcevent instance_npcname("verit#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	verit#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("verit#bios3");
	initnpctimer;
	npctalk "Vrid: Uau, você é incrível! Você matou todos eles?", 'vrid3$;
	end;
OnTimer2500:
	npctalk "Zeith: Ainda não. Mais estão chegando!", 'zeith3$;
	end;
OnTimer5000:
	stopnpctimer;
	.@label$ = instance_npcname("verit#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Verit",3017,1, .@label$;// EP14_3_DEATH_C_MOB2
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("verit#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("verit#bios3");
		donpcevent instance_npcname("megalodon#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	megalodon#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("megalodon#bios3");
	initnpctimer;
	npctalk "Vrid: Espero que realmente tenha sido o último deles.", 'vrid3$;
	end;
OnTimer2500:
	npctalk "Zeith: Não, não temos tanta sorte - mais está a caminho!", 'zeith3$;
	end;
OnTimer5000:
	stopnpctimer;
	.@label$ = instance_npcname("megalodon#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Megalodon",3018,1, .@label$;// EP14_3_DEATH_C_MOB3
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("megalodon#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("megalodon#bios3");
		donpcevent instance_npcname("boss_prelude#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	boss_prelude#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc 'reaper3$;
	initnpctimer;
	end;
OnTimer500:
	npctalk "Ceifador Ankou: Eu subestimei você, ou meus soldados não são tão fortes quanto eu pensava.", 'reaper3$;
	end;
OnTimer3000:
	npctalk "Vrid: Você está trapaceando! Liberte-nos e lute de maneira justa!", 'vrid3$;
	end;
OnTimer5500:
	npctalk "Ceifador Ankou: Você ainda está falando. Eu mesmo vou te matar e acabar logo com isso!", 'reaper3$;
	end;
OnTimer7500:
	disablenpc 'reaper3$;
	end;
OnTimer8000:
	stopnpctimer;
	donpcevent instance_npcname("boss#bios3") + "::OnStart";
	end;
}

1@dth3,1,1,0	script	boss#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("boss#bios3");
	monster 'map_dth3$,70,67,"Ceifador Ankou",3029,1, instance_npcname("boss#bios3") + "::OnMyBossDead";
	'boss_id = $@mobid[0];
	donpcevent instance_npcname("talk_friends#bios3") + "::OnStart";
	donpcevent instance_npcname("talk_boss#bios3") + "::OnStart";
	end;
OnMyBossDead:
	'boss_id = 0;
	donpcevent instance_npcname("talk_friends#bios3") + "::OnStop";
	donpcevent instance_npcname("talk_boss#bios3") + "::OnStop";
	enablenpc 'reaper3$;
	enablenpc 'zeith3$;
	enablenpc 'vrid3$;
	initnpctimer;
	end;
OnTimer3000:
	npctalk "Ceifador Ankou: Você é forte...", 'reaper3$;
	end;
OnTimer6500:
	npctalk "Ceifador Ankou: Mas meu objetivo foi alcançado ... Não pense que isso acabou.", 'reaper3$;
	end;
OnTimer8000:
	npctalk "Ceifador Ankou: Logo Lorde Morocc retornará. *Risaos* O medo da morte o seguirá aonde quer que você vá.", 'reaper3$;
	end;
OnTimer10500:
	disablenpc 'reaper3$;
	specialeffect EF_ICECRASH,AREA,'vrid3$;
	specialeffect EF_ICECRASH,AREA,'zeith3$;
	setnpcdisplay( 'vrid3$, 4_M_KNIGHT_BLACK );
	setnpcdisplay( 'zeith3$, 4_M_GUILLOTINE );
	end;
OnTimer13000:
	npctalk "Vrid: Ah, finalmente eu posso me mover!", 'vrid3$;
	end;
OnTimer16500:
	npctalk "Zeith: Hmpf, tenho vergonha de ser tão fraco ...", 'zeith3$;
	end;
OnTimer18000:
	npctalk "Vrid: Ufa, esse ceifador foi muito mais forte do que eu e Zeith juntos.", 'vrid3$;
	end;
OnTimer20500:
	npctalk "Vrid: Nós poderíamos ter morrido se não fosse por você.", 'vrid3$;
	end;
OnTimer23000:
	stopnpctimer;
	npctalk "Vrid: Eu quero lhe dar algo para salvar nossas vidas. Fale comigo novamente quando estiver pronto para aceitá-lo.", 'vrid3$;
	'reward = 1;
	disablenpc instance_npcname("boss#bios3");
	end;
}

1@dth3,1,1,0	script	talk_friends#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer15000:
	.@r = rand(7);
	if (.@r == 0)
		npctalk "Zeith: Ugh... Eu me sinto tão impotente...", 'zeith3$;
	else if (.@r == 1)
		npctalk "Zeith: Aquele cara Ankou cheira perigoso. É melhor você se mover assim que ele começar a usar uma habilidade.", 'zeith3$;
	else if (.@r == 2)
		npctalk "Vrid: Ack, Sinto muito, não podemos ajudar.", 'vrid3$;
	else if (.@r == 3)
		npctalk "Vrid: Seu trapaceiro, lute de maneira justa!", 'vrid3$;
	else if (.@r == 4)
		npctalk "Vrid: Droga, eu não posso me mover ...", 'vrid3$;
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}

1@dth3,1,1,0	script	talk_boss#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer13000:
	.@r = rand(6);
	if (.@r == 0)
		unittalk 'boss_id, "Ceifador Ankou: *Risos*";
	else if (.@r == 1)
		unittalk 'boss_id, "Ceifador Ankou: Sinta o medo da morte!";
	else if (.@r == 2)
		unittalk 'boss_id, "Ceifador Ankou: Lorde Morocc retornará em breve.";
	else if (.@r == 3)
		unittalk 'boss_id, "Ceifador Ankou: Isso é o melhor que pode fazer? Criaturas tão fracas, vocês humanos.";
	initnpctimer;
	end;;
OnStop:
	stopnpctimer;
	end;

OnInstanceInit:
	'reward = 'boss_id = 0;
	'map_dth1$ = instance_mapname("1@dth1");
	'map_dth2$ = instance_mapname("1@dth2");
	'map_dth3$ = instance_mapname("1@dth3");

	// warps
	disablenpc instance_npcname("warp1#bios1");
	disablenpc instance_npcname("warp1#bios2");

	// zone 1
	disablenpc instance_npcname("Ceifador Ankou#bios1");
	donpcevent instance_npcname("mobs#bios1") + "::OnStart";	// spawn at entrance

	// zone 2
	disablenpc instance_npcname("start1#bios2");
	disablenpc instance_npcname("mobs#bios2");

	// zone 3
	disablenpc instance_npcname("event_boss#bios3");
	disablenpc instance_npcname("freeze#bios3");
	disablenpc instance_npcname("verit#bios3");
	disablenpc instance_npcname("megalodon#bios3");
	disablenpc instance_npcname("boss_prelude#bios3");
	disablenpc instance_npcname("boss#bios3");
	disablenpc instance_npcname("talk_friends#bios3");
	disablenpc instance_npcname("talk_boss#bios3");
	disablenpc instance_npcname("Vrid#bios3");
	disablenpc instance_npcname("Zeith#bios3");
	disablenpc instance_npcname("Grim Reaper Ankou#bios3");
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	end;
}

1@dth3,72,74,3	duplicate(Ceifador Ankou#bios1)	Zeith#bios3	4_M_GUILLOTINE
1@dth3,70,67,3	duplicate(Ceifador Ankou#bios1)	Grim Reaper Ankou#bios3	3029

1@dth3,62,80,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#1bio1	3016
1@dth3,64,80,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#2bio1	3016
1@dth3,66,80,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#3bio1	3016
1@dth3,68,80,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#4bio1	3016
1@dth3,72,80,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#5bio1	3016
1@dth3,74,80,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#6bio1	3016
1@dth3,76,80,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#7bio1	3016
1@dth3,78,80,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#8bio1	3016
1@dth3,62,86,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#1bio2	3016
1@dth3,64,86,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#2bio2	3016
1@dth3,66,86,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#3bio2	3016
1@dth3,68,86,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#4bio2	3016
1@dth3,72,86,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#5bio2	3016
1@dth3,74,86,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#6bio2	3016
1@dth3,76,86,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#7bio2	3016
1@dth3,78,86,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#8bio2	3016
1@dth3,62,92,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#1bio3	3016
1@dth3,64,92,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#2bio3	3016
1@dth3,66,92,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#3bio3	3016
1@dth3,68,92,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#4bio3	3016
1@dth3,72,92,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#5bio3	3016
1@dth3,74,92,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#6bio3	3016
1@dth3,76,92,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#7bio3	3016
1@dth3,78,92,3	duplicate(Ceifador Ankou#bios1)	Corrupt Orc Zombie#8bio3	3016
