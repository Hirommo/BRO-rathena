//===== Alayne Scripts =======================================
//= Temple of Demon God Instance
//===== By: ================================================== 
//= Alayne
//===== Current Version: ===================================== 
//= 1.0
//= 1.1 Bug Fixes and additions by [llchrisll] for rAthena:
//      Fixed Party Bug
//      Moved instance delay check a bit below
//      Moved warp disables into "OnInstanceInit"
//      Fixed wrong Quest ID values
//      Removed instance_id() parameter in most commands, not needed
//      Added missing EXP Rewards at Nydhogg based on iRo Wiki
//      Added getitem reward to the Quest NPCs
//      Added custom function (IGF_SquadPrize) for Expedition Price Box (22567)
//===== Compatible With: ===================================== 
//= rAthena SVN
//= hercules SVN
//===== Description: ========================================= 
//= The official instance Temple of Demon God transcription
//===== Todo Remaining: ======================================
//
//===== Additional Comments: =================================
//
//
//============================================================

help_lobby,58,133,5	script	Comandante Hiva::alatdgcha	4_M_REDSWORD,{
	set .@npcname$, "[Comandante Hiva]";
	cutin "ep13_captin_edq", 2;
	mes .@npcname$;
	//RATHENA
	if(checkquest(.questId) == -1)
	//HERCULES
	//if(questprogress(.questId) == 0)
	{
		mes "Ei! Vocês! Venha aqui.";
		mes "Eu preciso de sua ajuda.";
		mes "Morocc está recuperando seu poder! Precisamos absolutamente desafiá-lo e derrotá-lo antes que ele ressuscite.";
		mes "Mas tenho medo de não sermos fortes o suficiente para lutar com ele...";
		mes "Você pode nos ajudar?";
		if(select("Sim","Não") == 1)
		{
			next;
			mes .@npcname$;
			mes "Boa! Estou contando com você!";
			mes "Vou rezar pela sua vitória!";
			setquest .questId;
		}
	}
	//RATHENA
	else if(checkquest(.questId, HUNTING) < 2)
	//HERCULES
	//else if(questprogress(.questId, HUNTING) < 2)
	{
		//uncomplete quest
		mes "Você ainda não cumpriu a missão, não é?";
	}
	//RATHENA
	else if(checkquest(.questId, HUNTING) == 2)
	//HERCULES
	//else if(questprogress(.questId, HUNTING) == 2)
	{
		//complete quest
		mes "* Apito * Bom trabalho!";
		mes "Como está o Templo do Deus Demônio? Ainda o mesmo?";
		getitem 6684,1; //Marca do Herói
		completequest .questId;
		erasequest .questId;
	}
	else
	{
		mes "Agora que cuidamos do corpo principal, podemos levar as coisas devagar.";
		mes "Por que você não vai descansar por enquanto? Deixe o resto para nós.";
	}
	cutin "", 255;
	close;
	
OnInit:
	set .questId, 62500;
	end;
}	


help_lobby,58,136,3	script	Missão Heróica::alatdga	111,2,2,{
	set .@npcname$, "[Missão]";
	mes .@npcname$;
	//RATHENA
	if(checkquest(.questId) == -1)
	//HERCULES
	//if(questprogress(.questId) == 0)
	{
		mes "Ei! Ouvi dizer que o comandante fez uma missão.";
		mes "Eu também tenho algo a lhe perguntar.";
		mes "Dois monstros protegem Morocc. Brinaranea e Muspelskoll.";
		mes "Se você derrotá-los, isso enfraquecerá esse demônio...";
		mes "Você pode lidar com isso?";
		if(select("Sim","Não") == 1)
		{
			next;
			mes .@npcname$;
			mes "Boa! Estou contando com você!";
			mes "Vou rezar pela sua vitória!";
			setquest .questId;
		}
	}
	//RATHENA
	else if(checkquest(.questId, HUNTING) < 2)
	//HERCULES
	//else if(questprogress(.questId, HUNTING) < 2)
	{
		//uncomplete quest
		mes "Você ainda não cumpriu a missão, não é?";
	}
	//RATHENA
	else if(checkquest(.questId, HUNTING) == 2)
	//HERCULES
	//else if(questprogress(.questId, HUNTING) == 2)
	{
		//complete quest"
		mes "Você fez isso! Eu sabia que você faria!";
		mes "Parabéns!";
		mes "Mas receio que isso ainda não tenha terminado.";
		mes "Eu tenho esse sentimento persistente de apreensão.";
		mes "Eu sinto Muito. Talvez eu esteja pensando irracionalmente.";
		next;
		mes .@npcname$;
		mes "Tome isso como uma recompensa e um agradecimento meu.";
		getitem 6684,1; //Marca do Herói
		completequest .questId;
		erasequest .questId;
	}
	else
	{
		mes "Descanse um pouco agora.";
	}
	close;
	
OnInit:
	set .questId, 62501;
	end;
}	

help_lobby,59,140,4	script	Deus Morroc::alatdgyl	3097,2,2,{
OnStart:
	set .@party_id, getcharid(1);
	set .@tdg_leader, ( getcharid(0) == getpartyleader(.@party_id,2) );

	//RATHENA	
	set .@has_instance, TDG_INSTANCE_ID > 0;
	//HERCULES
	//set .@has_instance, has_instance(.map$[0]);
	
	set .@instance_delay, ( instance_delay_templedg - gettimetick(2) );

	set .@npcname$, "[Deus Morroc]";
	mes .@npcname$;
	mes "Você tocou a árvore e sentiu um fluxo estranho de mana.";
	mes "Talvez o fluxo de mana guie até a profundidade do templo.";
	next;
	switch( select( 
		(( .@party_id && .@tdg_leader && !.@has_instance )?"Criar " + .instance_name$:""),
		(( .@party_id && .@has_instance )?"Entrar " + .instance_name$:""),
		(( .@party_id && .@tdg_leader && .@has_instance )?"Destruir " + .instance_name$:""),
	"Deixa pra lá."
		)){
	case 1:
		if( .@instance_delay > 0 )
		{
			mes "Você tem que esperar : ^FF0000"+.@instance_delay+" Segundos.^000000";
			close;
		}
		getpartymember .@party_id,2;
		if( .min_party_member >= 1 ){
			set .@origin, getcharid(3);
			set .@gettimetick, gettimetick(2);
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set .@member_count, .@member_count + 1;
					if( BaseLevel < .base_level[0] || BaseLevel > .base_level[1] ) set .@fail, .@fail|1;
					if( Zeny < .instance_zeny ) set .@fail, .@fail|2;
					if( instance_delay_templedg > .@gettimetick ) set .@fail, .@fail|4;
					if( .@fail ){
						set .@name$, strcharinfo(0);
						break;
					}
				}
			attachrid( .@origin );
		}
		
		//clean any previously remaining instance
		if(TDG_INSTANCE_ID > 0)
		{			
			getpartymember getcharid(1),2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) && TDG_INSTANCE_ID > 0){
					set TDG_INSTANCE_ID, -1;
				}
			instance_destroy instance_id();
		}
		
		if( .@name$ != "" && .@fail ){
			mes "^FF0000[ Falha ]^000000";
			mes "^0055FF"+.@name$+"^000000, Você não pode participar agora.";
			mes " ";
			mes "^FF0000[ Razão: ]^000000";
			if( .@fail & 1 ) mes "^777777Você deve estar no nível: "+.base_level[0]+" - "+.base_level[1]+"^000000";
			if( .@fail & 2 ) mes "^777777Você precisa de:  "+ .instance_zeny +" Zenys^000000";
			if( .@fail & 4 ) mes "^777777Você deve aguardar o tempo para entrar novamente.^000000";
			break;
		}
		
		getpartymember getcharid(1),0;
		if (instance_check_party(.@party_id, .min_party_member, .base_level[0], .base_level[1]) == 0) {
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "Faça ou participe de um grupo com pelo menos "+.min_party_member+" membros e tente novamente, com nível entre " +.base_level[0] + " e " + .base_level[1] + ".";
		}
		else{
			//RATHENA
			set .@instance, instance_create( .instance_name$ );
			//HERCULES
			//set .@instance, instance_create( .instance_name$, .@party_id, IOT_PARTY);
			if( .@instance < 0 ) {
				npctalk .instance_name$ + " falha na reserva.";
				mes "^0000FF"+ .instance_name$ +" ^000000 - Falha na Reserva!";
				close;
			}
			mes "^0055FF[ "+.instance_name$+" ]^000000";
			mes "^0000FF"+.instance_name$+" ^000000 - Reservado";
			set .@gettimetick, gettimetick(2);
			getpartymember .@party_id,2;
			for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
				if( attachrid( $@partymemberaid[.@i] ) ){
					set instance_delay_templedg, ( .@gettimetick + ( .instance_delay ));
					set Zeny, Zeny - .instance_zeny;
					set TDG_INSTANCE_ID, .@instance;
					dispbottom "["+.instance_name$+"] -"+ .instance_zeny +" Zeny e Tempo de Espera da instância aplicados.";
				}
			attachrid( .@origin );
			npctalk .instance_name$ + " foi gerado para o grupo - " + getpartyname( .@party_id );
			
			//HERCULES
			//for( set .@i, 0; .@i < getarraysize(.map$); set .@i,.@i+1 )
			//{
			//	if( instance_attachmap( .map$[.@i],.@instance ) == "" ){
			//		setmapflag .map$[.@i],mf_zone,"Memorial Dungeon";
			//		npctalk .instance_name$ + " reservation failed due to map attach failure.";
			//		mes "- ^FF0000Reservation Failed!^000000";
			//		instance_destroy( .@instance );
			//		close;
			//	}
			//}
			//instance_set_timeout .instance_delay,300,instance_id();
			//instance_init(instance_id());
 		}
		break;
	case 2:
		if( TDG_INSTANCE_ID <= 0 ){
			mes .instance_name$ + " não existe para o grupo - "+getpartyname( .@party_id );
		}
		else{
			//RATHENA
			getmapxy(.@map$, .@x, .@y, UNITTYPE_PC);
			save .@map$, .@x, .@y;
			switch(instance_enter(.instance_name$)) {
				default:
					mes "Um erro desconhecido ocorreu.";
					close;
				case 2:
					mes "A masmorra memorial " + .instance_name$ + " não existe.";
					mes "O líder do grupo ainda não gerou a masmorra.";
					close;
				case 1:
					mes "Você pode entrar na masmorra depois de fazer o grupo.";
					close;
				case 0:
					mapannounce .map$[0], strcharinfo(0)+" do grupo, " +getpartyname( .@party_id )+", entrando na masmorra, "+.instance_name$+".",bc_map,"0x00ff99",FW_NORMAL,12;
					close;
			}
			//HERCULES
			//if( has_instance( .map$[0] ) == "" ){
			//	mes .instance_name$ + " doesnt exist for Party - "+getpartyname( .@party_id );
			//}
			//else{
			//	warp .map$[0],102,16;
			//	instance_announce instance_id(), getpartyname(getcharid(1))+"  entered in the "+.instance_name$+"!",bc_map,"0x00ff99";
			//}
		}
		break;
	case 3:
		mes .instance_name$ + " Destruído.";
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && TDG_INSTANCE_ID > 0){
				set TDG_INSTANCE_ID, -1;
			}
		instance_destroy instance_id();
		break;
	default: 
		break;
}
close;

OnPCLoginEvent:
	specialeffect EF_ENHANCE;
OnPCLogoutEvent:
	set TDG_INSTANCE_ID, -1;
	end;

OnInit:
	setarray .base_level[0],255, 300;
	setarray .map$[0],"1@eom";
	set .map_size, getarraysize( .map$ );
	set .min_party_member, 1;
	set .instance_zeny, 1000000;
	set .instance_delay, 0;
	set $tdg_timer_delay, 3600; //1 hour to clean the instance
	set .instance_name$, "Templo do Deus Demonio";
	specialeffect EF_ENHANCE;
	end;
}

function	script	IGF_SquadPrize	{
// This is based on kRo and are only the Essences unlike iRo,
// where you also gain other items from it.

// Mini-Config:
// Since I don't know how it is really handled, but based on my tests I mostly got tier 2-3 essences
// So I decided to put harder chances to recieve them!
// 1: Use harder chances
// 0: No harder chances
set .@t_chance,1;
if(.@t_chance) {
	set .@t,rand(1,100);
	if(.@t < 75) // 75% to get Tier 1
		setarray .@item[0],4908, // Darklord Essence Force1
						   4911, // Darklord Essence Intelligence1
						   4914, // Darklord Essence Speed1
						   4917, // Darklord Essence Vitality1
						   4920, // Darklord Essence Concentration1
						   4923; // Darklord Essence Luk1
						   
	else if(.@t >= 75 && .@t < 95) // 20% to get Tier 2		   
		setarray .@item[0],4909, // Darklord Essence Force2
						   4912, // Darklord Essence Intelligence2
						   4915, // Darklord Essence Speed2
						   4918, // Darklord Essence Vitality2
						   4921, // Darklord Essence Concentration2
						   4924; // Darklord Essence Luk2
						   
	else if(.@t >= 95) // 5% to get Tier 3
		setarray .@item[0],4910, // Darklord Essence Force3
						   4913, // Darklord Essence Intelligence3
						   4916, // Darklord Essence Speed3
						   4919, // Darklord Essence Vitality3
						   4922, // Darklord Essence Concentration3
						   4925; // Darklord Essence Luk3

} else if(!.@t_chance)
		setarray .@item[0],4908, // Darklord Essence Force1
						   4909, // Darklord Essence Force2
						   4910, // Darklord Essence Force3
						   4911, // Darklord Essence Intelligence1
						   4912, // Darklord Essence Intelligence2
						   4913, // Darklord Essence Intelligence3
						   4914, // Darklord Essence Speed1
						   4915, // Darklord Essence Speed2
						   4916, // Darklord Essence Speed3
						   4917, // Darklord Essence Vitality1
						   4918, // Darklord Essence Vitality2
						   4919, // Darklord Essence Vitality3
						   4920, // Darklord Essence Concentration1
						   4921, // Darklord Essence Concentration2
						   4922, // Darklord Essence Concentration3
						   4923, // Darklord Essence Luk1
						   4924, // Darklord Essence Luk2
						   4925; // Darklord Essence Luk3

getitem .@item[rand(getarraysize(.@item))],1;
return;
}

1@eom,101,107,4	script	temple_demon_god	HIDDEN_WARP_NPC,2,2,{
function summon_normal;
function summon_guardian;
end;

OnInstanceInit:
	set .@map$, strnpcinfo(4);
	//disable unused npcs for now
	disablenpc instance_npcname( "tdgWarp#1" );
	disablenpc instance_npcname( "tdgWarp#2" );
	disablenpc instance_npcname( "tdgWarp#3" );
	disablenpc instance_npcname( "tdgWarp#4" );
	disablenpc instance_npcname( "tdgWarp#5" );
	
	set 'instance_tdg_level, 0;
	set .max_tdg_level, 6;
	
	set .moroccId, -1;
	
	instance_announce instance_id(), "[Templo do Deus Demônio] Eliminar os demônios apóstolos de Deus, Ahat e Shnaim.",bc_map,0x00ff99;
	sleep 1500;
	instance_announce instance_id(), "[Templo do Deus Demônio] Depois colete suas almas. Essas são as chaves para abrir o templo.",bc_map,0x00ff99;

	set 'ahatSoul, 6713;
	set 'shnaimSoul, 6714;
	set 'tdgMercenaryOn, 0;
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	summon_normal( .@map$,.@npc_name$, 1 );
	
	set .timerCount, 0;
	initnpctimer;
	end;

function	summon_normal	{
	set .@map$, getarg(0);
	set .@npcname$, getarg(1);
	set .@increaseFloor, getarg(2);
	
	if(.@increaseFloor == 1)
	{
		'instance_tdg_level++;
	}
	
	switch( 'instance_tdg_level ){
		case 1:
			//ahat and shnaim
			setarray .@monster,3041,3039;
			setarray .@amount,1,1;
			setarray .@coordinate,89,13,111,49;
			setarray .@label$, "OnAhatDied","OnShnaimDied";
			break;
		case 2:
			//dolomedes
			setarray .@monster,3088;
			setarray .@amount,20;
			setarray .@coordinate,54,97,33,138;
			break;
		case 3:
			//kasa
			setarray .@monster,3089;
			setarray .@amount,20;
			setarray .@coordinate,153,91,173,140;
			break;
		case 4:
			//two satan morocc
			setarray .@monster,3098,3099;
			setarray .@amount,1,1;
			setarray .@coordinate,90,208,116,176;
			break;
		case 6:
			//random pop salamander or dolomedes
			if(rand(0,1) == 1)
			{
				setarray .@monster,3088;
			}
			else
			{
				setarray .@monster,3090;
			}
			setarray .@amount,20;
			setarray .@coordinate,90,208,116,176;
			break;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		set .@target_amount, ( .@amount[.@i] );
		set .@size, 0;
		if(getarraysize(.@label$) == 0)
		{
			set .@clabel$, .@npcname$ + "::OnNormalDied";
		}
		else
		{
			set .@clabel$, .@npcname$ + "::" + .@label$[.@i];
		}	
		areamonster .@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@clabel$,.@size;
	}
	return;
}

function	summon_guardian	{
	set .@map$, getarg(0);
	set .@label$, getarg(1);
			
	switch( 'instance_tdg_level ){
		case 2:
			//brynarea
			setarray .@monster,3091;
			setarray .@coordinate,54,97,33,138;
			break;
		case 3:
			//muspelskoll
			setarray .@monster,3092;
			setarray .@coordinate,153,91,173,140;
			break;
		case 4:
			//morocc boy
			setarray .@monster,3096;
			setarray .@coordinate,90,208,116,176;
			break;
		case 5:
			//morocc boy, 20% left
			setarray .@monster,3096;
			setarray .@coordinate,90,208,116,176;
			break;
		case 6:
			//morocc adult
			setarray .@monster,3097;
			setarray .@coordinate,90,208,116,176;
			break;
		default: 
			mapannounce .@map$,"[ Templo do Deus Demônio] : Limpo!! Parabéns!!",bc_map,0x00FF99;
			return;
	}
	set .@monster_size, getarraysize( .@monster );
	for( set .@i, 0; .@i < .@monster_size; set .@i,.@i+1 ){
		set .@target_amount, 1;
		set .@size, 0;
		if('instance_tdg_level == 4)
		{
			//RATHENA
			areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
			set .moroccId, $@mobid[0];
			//HERCULES
			//set .moroccId, areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
		}
		else if('instance_tdg_level == 5)
		{
			//RATHENA
			areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
			set .moroccId, $@mobid[0];
			getunitdata(.moroccId, .@moroccData);
			setunitdata .moroccId, 2, .@moroccData[UMOB_MAXHP] * 20 / 100;
			//HERCULES
			//set .moroccId, areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
			//set .@mhp, getunitdata($moroccId, UDT_MAXHP);
			//setunitdata $moroccId, UDT_HP, .@mhp * 20 / 100;
		}
		else
		{
			areamonster(.@map$,.@coordinate[0],.@coordinate[1],.@coordinate[2],.@coordinate[3],"--ja--",.@monster[.@i],.@target_amount,.@label$,.@size);
		}
	}
	return;
}

OnAhatDied:
	//get item
	getitem 'ahatSoul, 1;
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnShnaimDied" );
	if(.@mob_dead_num == 0)
	{
		instance_announce instance_id(), "[Templo do Deus Demônio] Ative o Globo de Almas para abrir o templo.",bc_map,0x00ff99;
		enablenpc instance_npcname( "alatdgor" );
	}
	end;

OnShnaimDied:
	//get item
	getitem 'shnaimSoul, 1;
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnAhatDied" );
	if(.@mob_dead_num == 0)
	{
		instance_announce instance_id(), "[Templo do Deus Demônio] Ative o Globo de Almas para abrir o templo.",bc_map,0x00ff99;
		enablenpc instance_npcname( "alatdgor" );
	}
	end;

OnNormalDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnNormalDied" );
	if( .@mob_dead_num == 0 && .@mob_dead_num2 == 0 ){
		if('instance_tdg_level == 4)
		{
			'instance_tdg_level++;
			//summon morocc boy again
			summon_guardian( .@map$,.@npc_name$+"::OnGuardianDied" );
		}	
	}
	end;
	
OnGuardianDied:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnGuardianDied" );
	set .@party_id, getcharid(1);
	
	if( !.@mob_dead_num ){
	
		if('instance_tdg_level == 2)
		{
			killmonster strnpcinfo(4), "All";
			//nydhogg pop
			enablenpc instance_npcname( "alatdgny2" );
			donpcevent instance_npcname( "alatdgny2" ) + "::OnDialog2";
		}
		else if('instance_tdg_level == 3)
		{
			killmonster strnpcinfo(4), "All";
			//nydhogg pop
			enablenpc instance_npcname( "alatdgny3" );	
			donpcevent instance_npcname( "alatdgny3" ) + "::OnDialog3";	
		}
		else if('instance_tdg_level == 5)
		{
			'instance_tdg_level++;
			//morocc boy death
			instance_announce instance_id(), "[Morocc] Isso está ficando mais interessante, humanos!",bc_map,0xFF0000;
			sleep 1500;
			instance_announce instance_id(), "[Morocc] Você não acha que isso já acabou?",bc_map,0xFF0000;
			sleep 1500;
			set .randomPop, 1;
			summon_guardian( .@map$, .@npc_name$ + "::OnGuardianDied" );
		}
		else if ('instance_tdg_level == .max_tdg_level)
		{			
			killmonster strnpcinfo(4), "All";
			stopnpctimer;
			instance_announce instance_id(), "Não ... Não pense ... Acabou ... Você! Você já está ... morto !!",bc_map,0xFF0000;
			sleep 1500;
			
			//enable portal
			enablenpc instance_npcname( "alatdgny4" );
			
			end;
		}
	}
	end;
	
OnTimer10000:
	//each 10s, increase timer
	set .timerCount, .timerCount + 10;
	if(.timerCount == $tdg_timer_delay)
	{
		instance_announce instance_id(), "[ Templo do Deus Demônio ] A dimensão está em colapso! Eu tenho que tirar você de lá!", bc_map, 0xFF0000;
		sleep 5000;
		getpartymember getcharid(1),2;
		for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
			if( attachrid( $@partymemberaid[.@i] ) && TDG_INSTANCE_ID > 0){
				set TDG_INSTANCE_ID, -1;
			}
		instance_destroy instance_id();
		stopnpctimer;
	}
	if(.randomPop == 1)
	{
		set .@map$, strnpcinfo(4);
		set .@npc_name$, instance_npcname( strnpcinfo(0) );
		set .@mob_dead_num, mobcount(.@map$,.@npc_name$+"::OnNormalDied" );
		if(.@mob_dead_num == 0)
		{
			summon_normal( .@map$,.@npc_name$, 0 );
		}
	}
	if(.checkHP == 1)
	{
		callsub OnCheckMoroccHP;
	}
	setnpctimer 0;
	end;
	
OnCheckMoroccHP:	
	//RATHENA
	getunitdata(.moroccId, .@moroccData);
	if(.@moroccData[UMOB_HP] <= .@moroccData[UMOB_MAXHP] / 2)
	//HERCULES
	//set .@hp, getunitdata($stefanId, UDT_HP);
	//set .@mhp, getunitdata($stefanId, UDT_MAXHP);
	//if(.@hp <= .@mhp / 2)
	{
		set .@map$, strnpcinfo(4);
		set .@npc_name$, strnpcinfo(3);
		killmonster strnpcinfo(4), "All";
		set .checkHP, 0;
		//stop, kill monster and pop 2 morocc
		summon_normal( .@map$, strnpcinfo(3), 0 );
	}	
	return;
	
OnFirstFloorOn:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	//pop furious incarnation of morroc	
	summon_normal( .@map$, .@npc_name$, 1 );
	end;
	
OnSecondFloorOn:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	//pop dolomedes
	summon_normal( .@map$, .@npc_name$, 1 );
	//pop brynarea	
	summon_guardian( .@map$, .@npc_name$ + "::OnGuardianDied" );
	enablenpc instance_npcname( "tdgWarp#1" );
	end;
	
OnThirdFloorOn:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	//pop kasa
	summon_normal( .@map$, .@npc_name$, 1 );
	//pop muspelskoll	
	summon_guardian( .@map$, .@npc_name$ + "::OnGuardianDied" );
	enablenpc instance_npcname( "tdgWarp#3" );
	end;
	
OnFourthFloorOn:
	set .@map$, strnpcinfo(4);
	set .@npc_name$, instance_npcname( strnpcinfo(0) );
	'instance_tdg_level++;
	//pop morocc boy	
	summon_guardian( .@map$, .@npc_name$ + "::OnGuardianDied");
	enablenpc instance_npcname( "tdgWarp#5" );
	set .checkHP, 1;
	end;
}

1@eom,101,62,3	script	Temple Entrance::alatdgwe	HIDDEN_WARP_NPC,2,2,{
	end;
	
OnTouch:
	warp strnpcinfo(4), 98, 115;
	end;
	
OnInit:
OnInstanceInit:
	disablenpc strnpcinfo(3);
	end;
}

1@eom,98,56,3	script	Globo de Almas::alatdgor	HIDDEN_WARP_NPC,2,2,{
	set .@npcname$, "[Globo de Almas]";
	mes .@npcname$;
	mes "Deseja inserir as almas?";
	if(select("Sim","Não") == 1)
	{
		if(countitem('ahatSoul) > 0)
		{
			delitem 'ahatSoul, 1;
			set .ahat, 1;
		}
		if(countitem('shnaimSoul) > 0)
		{
			delitem 'shnaimSoul, 1;
			set .shnaim, 1;
		}
		if(.ahat == 1 && .shnaim == 1)
		{
			set .warpingOn, 1;
			npctalk "[Globo de Almas] A porta está aberta.";
			instance_announce instance_id(), "[Globo de Almas] A porta está aberta.", bc_map, 0x00FF99;
			//enable warp
			enablenpc instance_npcname( "alatdgwe" );
			enablenpc instance_npcname( "alatdge" );
			enablenpc instance_npcname( "alatdgmo" );
			enablenpc instance_npcname( "alatdglo" );
			enablenpc instance_npcname( "alatdgny" );
		}
		else
		{
			next;
			mes "[Globo de Almas]";
			mes "Insira as almas Ahat e Shnaim para abrir a porta.";
		}
	}
	close;
	
OnInstanceInit:
OnInit:
	enablenpc strnpcinfo(3);
	set .ahat, 0;
	set .shnaim, 0;
	end;
}

1@eom,98,115,3	script	entrance::alatdge	HIDDEN_WARP_NPC,2,2,{
	end;
	
OnTouch:
	if('instance_tdg_level == 1)
	{
		sleep 5000;
		donpcevent instance_npcname( "alatdgmo" ) + "::OnDialog1_1";
	}
	end;

OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@eom,102,124,3	script	Morocc::alatdgmo	4_F_MOCBOY,2,2,{
	end;
	
OnDialog1_1:
	if(.moTalking == 0)
	{
		set .moTalking, 1;
		npctalk "Vou criar um novo mundo e me tornar seu criador.";
		instance_announce instance_id(), "[Morocc] Vou criar um novo mundo e me tornar seu criador.",  bc_map, 0xFF0000;
		sleep 1500;
		npctalk "Deixe-me perguntar, as primeiras criaturas que pisaram na minha terra recém-nascida.";
		instance_announce instance_id(), "[Morocc] Deixe-me perguntar, as primeiras criaturas que pisaram na minha terra recém-nascida.", bc_map, 0xFF0000;
		sleep 1500;
		npctalk "Como você gostaria de me seguir e assistir ao nascimento de um novo mundo?";
		instance_announce instance_id(), "[Morocc] Como você gostaria de me seguir e assistir ao nascimento de um novo mundo?", bc_map, 0xFF0000;
		sleep 1500;
		donpcevent instance_npcname( "alatdglo" ) + "::OnDialog1_2";
	}
	end;
	
OnDialog1_4:
	npctalk "Deixe-me perguntar, as primeiras criaturas que pisaram na minha terra recém-nascida.";
	instance_announce instance_id(), "[Morocc] Deixe-me perguntar, as primeiras criaturas que pisaram na minha terra recém-nascida.",bc_map, 0xFF0000;
	sleep 1500;
	npctalk "Como você gostaria de me seguir e assistir ao nascimento de um novo mundo?";
	instance_announce instance_id(), "[Morocc] Como você gostaria de me seguir e assistir ao nascimento de um novo mundo?",bc_map, 0xFF0000;
	sleep 1500;
	donpcevent instance_npcname( "alatdglo" ) + "::OnDialog1_5";
	end;

OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	set .moTalking, 0;
	end;
}

1@eom,95,125,5	script	Loki::alatdglo	4_M_BLACKMAN,2,2,{
	end;
	
OnDialog1_2:
	npctalk "Chega de seu absurdo. Apenas diga que você gosta de destruir coisas!";
	instance_announce instance_id(), "[Loki] Chega de seu absurdo. Apenas diga que você gosta de destruir coisas!", bc_map, 0x00FF99;
	sleep 1500;
	npctalk "Criar um novo mundo e se tornar seu criador? Se este é o novo mundo que você está falando, estou desapontado!";
	instance_announce instance_id(), "[Loki] Criar um novo mundo e se tornar seu criador? Se este é o novo mundo que você está falando, estou desapontado!", bc_map, 0x00FF99;
	sleep 1500;
	npctalk "Mesmo se você queimar seu corpo, seu poder não é tão potente quanto o de Ymir do Gênesis!";
	instance_announce instance_id(), "[Loki] Mesmo se você queimar seu corpo, seu poder não é tão potente quanto o de Ymir do Gênesis!",bc_map, 0x00FF99;
	sleep 1500;
	npctalk "Só o fato de você ter roubado o Yggdrasil mostra falta de criatividade!";
	instance_announce instance_id(), "[Loki] Só o fato de você ter roubado o Yggdrasil mostra falta de criatividade!",bc_map, 0x00FF99;
	sleep 1500;
	npctalk "Dê uma olhada em si mesmo. Você é ilusório se pensa que parece um criador de qualquer tipo!";
	instance_announce instance_id(), "[Loki] Dê uma olhada em si mesmo. Você é ilusório se pensa que parece um criador de qualquer tipo!",bc_map, 0x00FF99;
	sleep 1500;
	
	donpcevent instance_npcname( "alatdgny" ) + "::OnDialog1_3";
	end;
	
OnDialog1_5:
	disablenpc instance_npcname( "alatdgmo" );
	npctalk "Você vai correr?!";
	instance_announce instance_id(), "[Loki] Você vai correr?!",bc_map, 0x00FF99;
	
	disablenpc strnpcinfo(3);
	sleep 1500;
	set 'tdgMercenaryOn, 1;
		
	donpcevent instance_npcname( "temple_demon_god" ) + "::OnSecondFloorOn";
	end;

OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@eom,93,122,5	script	Nydhogg::alatdgny	4_F_NYDHOG,{
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[Nydhogg]";
		mes "Ouça com atenção.";
		next;
		mes "[Nydhogg]";
		mes "Morocc logo irá dominar todos nós. Precisamos derrotá-lo antes que ele chegue.";
		next;
		mes "[Nydhogg]";
		mes "Precisamos de seus poderes para derrotá-lo. Não somos fortes o suficiente para derrotá-lo sozinhos.";
		next;
		mes "[Nydhogg]";
		mes "Oferecerei minha força em troca de você para me ajudar.";
		close2;
		summon "Nydhogg",3087,99999999;
		disablenpc strnpcinfo(3);
		end;
	}
	mes "[Nydhogg]";
	mes "Estou falando com seu líder.";
	close;
	
OnDialog1_3:
	npctalk "Eu concordo. Você é desnecessário para este mundo. Você não pertence aqui!";
	instance_announce instance_id(), "[Nydhogg] Eu concordo. Você é desnecessário para este mundo. Você não pertence aqui!",bc_map, 0x0000FF;
	sleep 1500;
	npctalk "Até Yggdrasil nega sua existência!";
	instance_announce instance_id(), "[Nydhogg] Até Yggdrasil nega sua existência!",bc_map, 0x0000FF;
	sleep 1500;
	npctalk "Como guardião de Yggdrasil, tenho que apagar sua existência deste mundo.";
	instance_announce instance_id(), "[Nydhogg] Como guardião de Yggdrasil, tenho que apagar sua existência deste mundo.",bc_map, 0x0000FF;
	sleep 1500;

	donpcevent instance_npcname( "alatdgmo" ) + "::OnDialog1_4";
	end;

OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	set .endTalking, 0;
	end;
}

1@eom,102,124,3	script	Morocc::alatdgmoa	3097,2,2,{
	end;
	
OnTouch:
	if(.moTalking == 0)
	{
		set .moTalking, 1;
		npctalk "Você excedeu minhas expectativas.";
		instance_announce instance_id(), "[Morocc] Você excedeu minhas expectativas..",bc_map, 0xFF0000;
		sleep 1500;
		npctalk "Eu aceito o seu desafio.";
		instance_announce instance_id(), "[Morocc] Eu aceito o seu desafio.",bc_map, 0xFF0000;
		sleep 1500;
		npctalk "Encharcarei a terra com o seu sangue que flui e a nutrirei com a sua carne rasgada.";
		instance_announce instance_id(), "[Morocc] Encharcarei a terra com o seu sangue que flui e a nutrirei com a sua carne rasgada.",bc_map, 0xFF0000;
		sleep 1500;
		npctalk "Eu vou te mostrar a verdadeira profundidade do desespero.";
		instance_announce instance_id(), "[Morocc] Eu vou te mostrar a verdadeira profundidade do desespero.",bc_map, 0xFF0000;
		sleep 1500;
		disablenpc strnpcinfo(3);
		donpcevent instance_npcname( "temple_demon_god" ) + "::OnFourthFloorOn";
	}
	end;

OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	set .moTalking, 0;
	end;
}

1@eom,61,149,3	script	Nydhogg::alatdgny2	4_F_NYDHOG,{
	set .@npcname$, "[Nydhogg]";
	mes .@npcname$;
	mes "Essa foi uma batalha difícil.";
	mes "Isso pode não ser bom o suficiente, mas espero que ajude seu crescimento.";
	if(NYDBRYN == 0)
	{
		// Halfed EXP Values taken from iRo Wiki
		getexp 5000000,2500000;
		next;
		mes "Nydhogg o recompensou com uma grande quantidade de EXP por derrotar Brinnaranea.";
		set NYDBRYN, 1;
	}
	close;
	
OnDialog2:
	instance_announce instance_id(), "[Nyddhog] A influência de Brinnaranea se dissipou e um novo portão de dobra apareceu.",bc_map, 0x0000FF;
	enablenpc instance_npcname( "tdgWarp#2" );
	donpcevent instance_npcname( "temple_demon_god" ) + "::OnThirdFloorOn";
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@eom,154,155,3	script	Nydhogg::alatdgny3	4_F_NYDHOG,{
	set .@npcname$, "[Nydhogg]";
	mes .@npcname$;
	mes "Bom trabalho novamente.";
	mes "Isso pode não ser bom o suficiente, mas espero que ajude seu crescimento.";
	if(NYDMUS == 0)
	{
		// Halfed EXP Values taken from iRo Wiki
		getexp 500000,250000;
		next;
		mes "Nydhogg recompensou você com uma grande quantidade de EXP por derrotar Muspelskoll.";
		set NYDMUS, 1;
	}
	close;
	
OnDialog3:
	instance_announce instance_id(), "[Nyddhog] A influência de Muspelskoll se dissipou e um novo portão de dobra apareceu.",bc_map, 0x0000FF;
	enablenpc instance_npcname( "tdgWarp#4" );
	enablenpc instance_npcname( "alatdgmoa" );
	end;
	
OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@eom,106,197,3	script	Nydhogg::alatdgny4	4_F_NYDHOG,{
	set .@npcname$, "[Nydhogg]";
	cutin "ep14_nyd01",1;
	mes .@npcname$;
	mes "Eu não sei o que dizer...";
	mes "Paramos Deus esperando.";
	mes "Nós cumprimos nosso objetivo.";
	mes "Mas eu ainda estou nervoso...";
	next;
	mes .@npcname$;
	mes "Este mundo é criado na mente de Morocc.";
	mes "Ele viverá para sempre neste jardim que não pôde terminar.";
	next;
	mes .@npcname$;
	mes "Ele provavelmente não sabe que seu corpo é destruído para sempre.";
	mes "Ele era ambicioso demais, até para ele.";
	mes "Ele queria criar um novo mundo e se tornar seu criador.";
	next;
	mes .@npcname$;
	mes "Mas ele não conseguiu criar nada sozinho.";
	mes "Ele abandonou seu corpo demoníaco, apenas para se tornar um mero fac-símile de um deus existente.";
	next;
	mes .@npcname$;
	mes "Ele parece o snape de um homem.";
	mes "Ele não era criativo o suficiente para reinventar o conceito de um Deus.";
	mes "Ele era algum tipo de paradoxo.";
	next;
	mes .@npcname$;
	mes "Agora precisamos sair dessa armadilha a tempo e voltar à realidade.";
	mes "Este mundo existe apenas na mente do demônio, e esse dia se repetirá sem parar.";
	next;
	mes .@npcname$;
	mes "Todos que entrarem neste mundo serão arrastados nesse loop.";
	mes "Nesse sentido, ele conseguiu criar seu próprio mundo.";
	next;
	mes .@npcname$;
	mes "Eu falei muito.";
	mes "O comandante deve estar esperando. Deixe-me validar o seu feito para que você possa se reportar a ele.";
	mes "Você fez um ótimo trabalho hoje.";
	cutin "", 255;
	next;
	getitem 6684,1;// Token Of Hero
	getpartymember getcharid(1),2;
	for( set .@i, 0; .@i < $@partymembercount; set .@i, .@i+1 )
		if( attachrid( $@partymemberaid[.@i] ) && TDG_INSTANCE_ID > 0){
			set TDG_INSTANCE_ID, -1;
		}
	instance_destroy instance_id();
	close;

OnInstanceInit:
OnInit:
	disablenpc strnpcinfo(3);
	end;
}

1@eom,91,120	warp	tdgWarp#1	2,2,1@eom,61,88
1@eom,69,149	warp	tdgWarp#2	2,2,1@eom,98, 115
1@eom,105,120	warp	tdgWarp#3	2,2,1@eom,133,93
1@eom,147,156	warp	tdgWarp#4	2,2,1@eom,98, 115
1@eom,98,127	warp	tdgWarp#5	2,2,1@eom,101,176