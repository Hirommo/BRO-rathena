prt_in,136,29,4	script	Ferreiro Mestre#prt	738,{
	disable_items;
	mes "[ ^a60536Ferreiro Mestre^000000 ]";
	mes "Tenho ampla experiência em refinamento, posso refinar equipamentos do ^2596C2 nível +4 até +10^000000.";
	mes "Quando o refine falhar, o nível de refinamento cai ^ff00001^000000 ponto, mas não perde o item!";
	mes "^777777Diga-me então o que deseja refinar:^000000";
	next;
	setarray .@position$[1], "Cabeça Topo", "Corpo", "Mão Esquerda", "Mão Direita", "Capa", "Sapatos", "Acessório 1", "Acessório 2", "Cabeça Meio", "Cabeça Baixo";
	set .@menu$, "";
	for( set .@i, 1; .@i <= 10; set .@i, .@i + 1 )
		set .@menu$, .@menu$ + (( getequipisequiped( .@i )) ? "^2596C2"+getequipname( .@i )+"^000000": .@position$[.@i] +" - ^777777[Desequipado]^000000") +":";
	set .@part, select( .@menu$ );
	if( !getequipisequiped( .@part )){
		mes "[ ^a60536Ferreiro Mestre^000000 ]";
		mes "Traga o item para que eu possa refiná-lo!";
		close;
	}
	if( !getequipisenableref( .@part )){
		mes "[ ^a60536Ferreiro Mestre^000000 ]";
		mes "Este item não pode ser refinado.";
		close;
	}
	if( getequiprefinerycnt( .@part ) < 4 || getequiprefinerycnt(.@part) > 10 ){
		mes "[ ^a60536Ferreiro Mestre^000000 ]";
		mes "Eu só sei refinar itens com níveis de refino de ^2596C2+4 até +10^000000.";
		close;
	}
	if( getequiprefinerycnt( .@part ) == 10 ){
		mes "[ ^a60536Ferreiro Mestre^000000 ]";
		mes "Você já atingiu o nível máximo de refine do equipamento.";
		close;
	}
	switch( getequipweaponlv( .@part )){
	default:
	case 0:
		set .@price, 2000000;
		set .@material, 7619; //HD_Elunium
		break;
	case 1:
	case 2:
	case 3:
	case 4:
		set .@price, 2000000;
		set .@material, 7620; //HD_Oridecon
		break;
	}
	mes "[ ^a60536Ferreiro Mestre^000000 ]";
	mes "Para realizar o refinamento comigo você precisa do seguinte:";
	mes "^2596C2- "+ getitemname( .@material ) +".^000000";
	mes "^2596C2- 2.000,00 Zeny.^000000";
	mes " ";
	mes "Deseja prosseguir com o refine?";
	next;
	if( select("[^ff0000»^000000] Sim.:[^ff0000»^000000] Não.") == 2 ) close;
			if( countitem( .@material ) == 0 || Zeny < .@price ){
				mes "[ ^a60536Ferreiro Mestre^000000 ]";
				mes "Está lhe faltando a matéria prima e o zeny necessário!";
				close;
			}
			delitem .@material, 1;
			set Zeny, Zeny - .@price;
			mes "[ ^a60536Ferreiro Mestre^000000 ]";
			mes "Tac! Tac! Tac!";
			if( getequippercentrefinery( .@part ) > rand( 100 ) || getequippercentrefinery( .@part ) > rand( 100 )){
				successrefitem .@part;
				next;
				emotion e_no1;
				mes "[ ^a60536Ferreiro Mestre^000000 ]";
				mes "O refinamento foi realizado com perfeição!";
				close;
			}
			downrefitem .@part;
			next;
			emotion e_omg;
			mes "[ ^a60536Ferreiro Mestre^000000 ]";
			mes "Tivemos uma falha, infelizmente o refinamento falhou.";
			close;
}


