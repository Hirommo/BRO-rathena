// ==============================================================================
// BattleGround System - KvM 80~99
// ==============================================================================

// Registration NPC's
// *********************************************************************

prontera,124,168,5	script	Registration::KvM01R_Guillaume	418,{ // KvM Guillaume
	end;

OnInit:
	waitingroom "Krieger von Midgard [PTxPT]",6,"KvM01_BG::OnGuillaumeJoin",1;
	end;
	
OnEnterBG:
	set $@KvM01BG_id1, waitingroom2bg("kvm",53,128,0,"KvM01_BG::OnGuillaumeQuit","KvM01_BG::OnGuillaumeDie");
	end;
}

prontera,135,168,4	script	Registration::KvM01R_Croix	414,{ // KvM Croix
	end;

OnInit:
	waitingroom "Krieger von Midgard [PTxPT]",6,"KvM01_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM01BG_id2, waitingroom2bg("kvm",146,55,1,"KvM01_BG::OnCroixQuit","KvM01_BG::OnCroixDie");
	end;
}

// Light Effects
// *********************************************************************

kvm,54,123,0	script	#kvma	111,{
	end;

OnKvM01LightA: misceffect 54; end;
OnKvM01LightB: misceffect 55; end;
OnKvM01LightC: misceffect 56; end;
}

kvm,56,125,0	duplicate(#kvma)	#kvmb	111
kvm,58,127,0	duplicate(#kvma)	#kvmc	111
kvm,141,56,0	duplicate(#kvma)	#kvmd	111
kvm,143,58,0	duplicate(#kvma)	#kvme	111
kvm,145,60,0	duplicate(#kvma)	#kvmf	111

// Battleground Engine
// *********************************************************************

-	script	KvM01_BG	-1,{
	end;

OnInit:
	setwall "kvm",54,122,6,7,0,"batc01wall_a";
	setwall "kvm",55,122,5,7,0,"batc01wall_b";
	setwall "kvm",140,56,6,7,0,"batc01wall_c";
	setwall "kvm",140,57,5,7,0,"batc01wall_d";
	disablenpc "VintenarKvM01a";
	disablenpc "VintenarKvM01b";
	end;

OnGuillaumeJoin:
	if( $@KvM01BG == 1 && bg_get_data($@KvM01BG_id1, 0) < 5 )
		waitingroom2bg_single $@KvM01BG_id1,"kvm",53,128,"KvM01R_Guillaume";
	else
		donpcevent "KvM01_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	setquest 8506;
	if( $@KvM01BG == 1 && bg_get_data($@KvM01BG_id1, 0) < 5 )
		waitingroom2bg_single $@KvM01BG_id1,"kvm",53,128,"KvM01R_Guillaume";
OnGuillaumeDie:
	if( $@KvM01BG == 2 )
	{
		getmapxy .@m$, .@x, .@y, 0;
		if( .@m$ != "kvm" || (.@x >= 50 && .@x <= 58 && .@y >= 123 && .@y <= 131) )
			end; // Killed/Logout on Cementery or outside the map?

		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "kvm",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ) donpcevent "KvM01_BG::OnCroixWin";
	}
	end;

OnCroixJoin:
	if( $@KvM01BG == 1 && bg_get_data($@KvM01BG_id2, 0) < 5 )
		waitingroom2bg_single $@KvM01BG_id2,"kvm",146,55,"KvM01R_Croix";
	else
		donpcevent "KvM01_BG::OnReadyCheck";
	end;

OnCroixQuit:
	setquest 8506;
	if( $@KvM01BG == 1 && bg_get_data($@KvM01BG_id2, 0) < 5 )
		waitingroom2bg_single $@KvM01BG_id2,"kvm",146,55,"KvM01R_Croix";
OnCroixDie:
	if( $@KvM01BG == 2 )
	{
		getmapxy .@m$, .@x, .@y, 0;
		if( .@m$ != "kvm" || (.@x >= 141 && .@x <= 149 && .@y >= 52 && .@y <= 60) )
			end; // Killed/Logout on Cementery or outside the map?

		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "kvm",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ) donpcevent "KvM01_BG::OnGuillaumeWin";
	}
	end;

OnReadyCheck:
	if( $@KvM01BG || $@Global_BG_Block )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM01R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM01R_Croix");

	if( .@Guillaume < 6 || .@Croix < 6 )
	{
		if( .@Guillaume > 3 && .@Croix > 3 && !agitcheck() && $@KvM_Flood < gettimetick(2) )
		{
			announce "Batalha Campal - [ Kreiger Von Midgard ] - G: " + .@Guillaume + "/6, C: " + .@Croix + "/6",0,0x00BFFF;
			set $@KvM_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "prontera","Batalha Campal - [ Kreiger Von Midgard ] - G: " + .@Guillaume + "/6, C: " + .@Croix + "/6",1,0x00BFFF;
		end;
	}

	set $@KvM01BG, 1; // Starting
	donpcevent "KvM01R_Croix::OnEnterBG";
	donpcevent "KvM01R_Guillaume::OnEnterBG";
	donpcevent "KvM01_BG::OnStart";
	end;

OnStart:
	announce "Batalha Campal - [ Kreiger Von Midgard ] - começou!",0,0x00BFFF;
	enablenpc "TherapistKvM01a";
	enablenpc "TherapistKvM01b";
	disablenpc "VintenarKvM01a";
	disablenpc "VintenarKvM01b";
	set $@KvM01BG_Victory, 0;
	sleep 2000;
	// Warp Teams
	bg_warp $@KvM01BG_id1,"kvm",53,128;
	bg_warp $@KvM01BG_id2,"kvm",146,55;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer5000:
	areapercentheal "kvm",50,123,58,131,100,100;
	areapercentheal "kvm",141,52,149,60,100,100;
	mapannounce "kvm","A batalha vai começar em 25 segundos!!",1,0x00BFFF;
	end;

OnTimer26000:
	mapannounce "kvm","A batalha vai começar em 4 segundos!!",1,0x00BFFF;
	end;

OnTimer27000:
	donpcevent "::OnKvM01LightA";
	end;

OnTimer28000:
	donpcevent "::OnKvM01LightB";
	end;
	
OnTimer29000:
	donpcevent "::OnKvM01LightC";
	end;

OnTimer30000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM01BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM01BG_id2, 0);
	if( .Guillaume_Count < 6 || .Croix_Count < 6 )
	{
		set $@KvM01BG_Victory, 3;
		set $@KvM01BG, 3;
		mapannounce "kvm","Não há jogadores suficientes para começar a batalha.",1,0x00BFFF;
		stopnpctimer;
		sleep 2000;
		donpcevent "KvM01_BG::OnStop";
		end;
	}

	set $@KvM01BG, 2; // Playing
	areapercentheal "kvm",50,123,58,131,100,100;
	bg_warp $@KvM01BG_id1,"kvm",62,119;
	areapercentheal "kvm",141,52,149,60,100,100;
	bg_warp $@KvM01BG_id2,"kvm",137,64;
	end;

OnTimer32000:
	mapannounce "kvm","A batalha de Kreiger Von Midgard começou!!",1,0x00BFFF;
	end;

OnTimer300000:
	mapannounce "kvm","A batalha vai terminar em 30 segundos!!",1,0x00BFFF;
	end;

OnTimer330000:
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM01_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM01_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM01BG, 3;
		set $@KvM01BG_Victory, 3;
		stopnpctimer;
		sleep 2000;
		mapannounce "kvm","O tempo terminou! Esta batalha foi um empate...",1,0x00BFFF;
		donpcevent "KvM01_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 1;
	stopnpctimer;
	sleep 2000;
	mapannounce "kvm","O exército de Guillaume ganhou a batalha de Kreiger Von Midgard",1,0x0000FF;
	donpcevent "KvM01_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 2;
	stopnpctimer;
	sleep 2000;
	mapannounce "kvm","O exército de Croix ganhou a batalha de Battle of Kreiger Von Midgard",1,0xFF0000;
	donpcevent "KvM01_BG::OnStop";
	end;

OnStop:
	disablenpc "TherapistKvM01a";
	disablenpc "TherapistKvM01b";
	enablenpc "VintenarKvM01a";
	enablenpc "VintenarKvM01b";
	// Warp Teams
	bg_warp $@KvM01BG_id1,"kvm",53,128;
	bg_warp $@KvM01BG_id2,"kvm",146,55;
	donpcevent "KvM01_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "KvM01_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM01BG_Victory, 0;
	if( $@KvM01BG_id1 ) { bg_destroy $@KvM01BG_id1; set $@KvM01BG_id1, 0; }
	if( $@KvM01BG_id2 ) { bg_destroy $@KvM01BG_id2; set $@KvM01BG_id2, 0; }
	disablenpc "TherapistKvM01a";
	disablenpc "TherapistKvM01b";
	disablenpc "VintenarKvM01a";
	disablenpc "VintenarKvM01b";
	sleep 1000;
	mapwarp "kvm","bg",148,99;
	sleep 2000;
	maprespawnguildid "kvm",0,3; // Just in case someone else
	bg_updatescore "kvm",0,0;
	sleep 2000;
	set $@KvM01BG, 0;
	donpcevent "KvM01_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM01_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "kvm","A batalha de Kreiger Von Midgard vai fechar em 1 minuto!",1,0x00BFFF;
	end;
OnTimer30000:
	mapannounce "kvm","A batalha de Kreiger Von Midgard vai fechar em 30 segundos!",1,0x00BFFF;
	end;
OnTimer50000:
	mapannounce "kvm","A batalha de Kreiger Von Midgard vai fechar em 10 segundos!",1,0x00BFFF;
	end;
OnTimer60000:
	donpcevent "KvM01_BG::OnReset";
	end;
}

// Battleground Therapist
// *********************************************************************

//kvm,51,130,5	script	Therapist in battle::TherapistKvM01a	95,{
//	mes "[Therapist in battle]";
//	mes "Just close your eyes, and take a deep breathe.";
//	mes "You can be free from pain.";
//	specialeffect2 312;
//	close;
//}

//kvm,148,53,1	script	Therapist in battle::TherapistKvM01b	95,{
//	mes "[Therapist in battle]";
//	mes "Just close your eyes, and take a deep breathe.";
//	mes "You can be free from pain.";
//	specialeffect2 312;
//	close;
//}

// Battleground rewards
// *********************************************************************

kvm,51,130,5	script	Guillaume Vintenar::VintenarKvM01a	419,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == 3 )
		{
			bg_leave;
			warp "prontera",130,165;
			end;
		}
		else if( $@KvM01BG_Victory == 1 )
		{ // Victory
			set .@Reward, 150;
			getitem 21046,1;
			set #CASHPOINTS, #CASHPOINTS+3;
			mes "[Swandery]";
			mes "Abençoado Guillaume!!";
			mes "Vamos aproveitar nossa gloriosa vitória!";
			mes "" + strcharinfo(0) + ", é um sinal refletindo vitória";
			close2;
		}
		else
		{ // 
			set .@Reward, 10;
			mes "[Swandery]";
			mes "Você perdeu, mas você deu tudo de si nesta batalha.";
			mes "Isto daqui é uma recompensa pela sua grande dedicação ao Guillaume Marollo!";
			mes "Lide com esta derrota como uma lição, e depois você irá ficar melhor.";
			close2;
		}

		getitem 7773, .@Reward;
		bg_leave;
		warp "bg",148,99;
		end;
	}
	end;
}

kvm,148,53,1	script	Croix Vintenar::VintenarKvM01b	415,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == 3 )
		{
			bg_leave;
			warp "prontera",130,165;
			end;
		}
		else if( $@KvM01BG_Victory == 2 )
		{ // Victory
			set .@Reward, 90;
			mes "[Swandery]";
			mes "Abençoado Croax!!";
			mes "Vamos aproveitar nossa gloriosa vitória!";
			mes "" + strcharinfo(0) + ", é um sinal refletindo vitória";
			close2;
		}
		else
		{ // 
			set .@Reward, 15;
			mes "[Swandery]";
			mes "Ah, " + strcharinfo(0) + ". Não fique triste.";
			mes "Mesmo que não tenhamos ganhado, nós demos o nosso melhor.";
			mes "Está é uma premiação de Croix, e por favor esqueça está batalha. Nós podemos ganhar o próximo.";
			close2;
		}

		getitem 7773, .@Reward;
		bg_leave;
		warp "prontera",130,165;
		end;
	}
	end;
}

// MapFlags
// *********************************************************************

kvm	mapflag	battleground
kvm	mapflag	nomemo
kvm	mapflag	nosave	SavePoint
kvm	mapflag	noteleport
kvm	mapflag	nowarp
kvm	mapflag	nowarpto
kvm	mapflag	noreturn
kvm	mapflag	nobranch
kvm	mapflag	nopenalty
kvm	mapflag	noemergencycall