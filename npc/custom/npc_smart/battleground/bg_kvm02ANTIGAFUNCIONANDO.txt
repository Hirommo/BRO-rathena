//=====================================================================
// Registration NPC's
// *********************************************************************

bg,140,128,4	script	Registration::KvM02R_Guillaume	418,{ // KvM Guillaume
	end;

OnInit:
	waitingroom "Krieger von Midgard [PTxPT]",3,"KvM02_BG::OnGuillaumeJoin",1;
	end;
	
OnEnterBG:
	set $@KvM02BG_id1, waitingroom2bg("kvm2",53,128,0,"KvM02_BG::OnGuillaumeQuit","KvM02_BG::OnGuillaumeDie");
	end;
}

bg,155,128,4	script	Registration::KvM02R_Croix	414,{ // KvM Croix
	end;

OnInit:
	waitingroom "Krieger von Midgard [PTxPT]",3,"KvM02_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM02BG_id2, waitingroom2bg("kvm2",146,55,1,"KvM02_BG::OnCroixQuit","KvM02_BG::OnCroixDie");
	end;
}

// Light Effects
// *********************************************************************

kvm2,54,123,0	script	#kvm2a	111,{
	end;

OnKvM02LightA: misceffect 54; end;
OnKvM02LightB: misceffect 55; end;
OnKvM02LightC: misceffect 56; end;
}

kvm2,56,125,0	duplicate(#kvm2a)	#kvm2b	111
kvm2,58,127,0	duplicate(#kvm2a)	#kvm2c	111
kvm2,141,56,0	duplicate(#kvm2a)	#kvm2d	111
kvm2,143,58,0	duplicate(#kvm2a)	#kvm2e	111
kvm2,145,60,0	duplicate(#kvm2a)	#kvm2f	111

// Battleground Engine
// *********************************************************************

-	script	KvM02_BG	-1,{
	end;

OnInit:
	setwall "kvm2",54,122,6,7,0,"batc02wall_a";
	setwall "kvm2",55,122,5,7,0,"batc02wall_b";
	setwall "kvm2",140,56,6,7,0,"batc02wall_c";
	setwall "kvm2",140,57,5,7,0,"batc02wall_d";
	disablenpc "VintenarKvM02a";
	disablenpc "VintenarKvM02b";
	end;

OnGuillaumeJoin:
	if( $@KvM02BG == 1 && bg_get_data($@KvM02BG_id1, 0) < 5 )
		waitingroom2bg_single $@KvM02BG_id1,"kvm2",53,128,"KvM02R_Guillaume";
	else
		donpcevent "KvM02_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	setquest 8506;
	if( $@KvM02BG == 1 && bg_get_data($@KvM02BG_id1, 0) < 5 )
		waitingroom2bg_single $@KvM02BG_id1,"kvm2",53,128,"KvM02R_Guillaume";
OnGuillaumeDie:
	if( $@KvM02BG == 2 )
	{
		getmapxy .@m$, .@x, .@y, 0;
		if( .@m$ != "kvm2" || (.@x >= 50 && .@x <= 58 && .@y >= 123 && .@y <= 131) )
			end; // Killed/Logout on Cementery or outside the map?

		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "kvm2",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ) donpcevent "KvM02_BG::OnCroixWin";
	}
	end;

OnCroixJoin:
	if( $@KvM02BG == 1 && bg_get_data($@KvM02BG_id2, 0) < 5 )
		waitingroom2bg_single $@KvM02BG_id2,"kvm2",146,55,"KvM02R_Croix";
	else
		donpcevent "KvM02_BG::OnReadyCheck";
	end;

OnCroixQuit:
	setquest 8506;
	if( $@KvM02BG == 1 && bg_get_data($@KvM02BG_id2, 0) < 5 )
		waitingroom2bg_single $@KvM02BG_id2,"kvm2",146,55,"KvM02R_Croix";
OnCroixDie:
	if( $@KvM02BG == 2 )
	{
		getmapxy .@m$, .@x, .@y, 0;
		if( .@m$ != "kvm2" || (.@x >= 141 && .@x <= 149 && .@y >= 52 && .@y <= 60) )
			end; // Killed/Logout on Cementery or outside the map?

		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "kvm2",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ) donpcevent "KvM02_BG::OnGuillaumeWin";
	}
	end;

OnReadyCheck:
	if( $@KvM02BG || $@Global_BG_Block )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM02R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM02R_Croix");

	if( .@Guillaume < 3 || .@Croix < 3 )
	{
		if( .@Guillaume > 3 && .@Croix > 3 && !agitcheck() && $@KvM_Flood < gettimetick(2) )
		{
			announce "Batalha Campal - [ Kreiger Von Midgard ] - G: " + .@Guillaume + "/3, C: " + .@Croix + "/3",0,0x00BFFF;
			set $@KvM_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "bg","Batalha Campal - [ Kreiger Von Midgard ] - G: " + .@Guillaume + "/3, C: " + .@Croix + "/3",1,0x00BFFF;
		end;
	}

	set $@KvM02BG, 1; // Starting
	donpcevent "KvM02R_Croix::OnEnterBG";
	donpcevent "KvM02R_Guillaume::OnEnterBG";
	donpcevent "KvM02_BG::OnStart";
	end;

OnStart:
	announce "Batalha Campal - [ Kreiger Von Midgard ] - começou!",0,0x00BFFF;
	enablenpc "TherapistKvM02a";
	enablenpc "TherapistKvM02b";
	disablenpc "VintenarKvM02a";
	disablenpc "VintenarKvM02b";
	set $@KvM02BG_Victory, 0;
	sleep 2000;
	// Warp Teams
	bg_warp $@KvM02BG_id1,"kvm2",53,128;
	bg_warp $@KvM02BG_id2,"kvm2",146,55;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer5000:
	areapercentheal "kvm2",50,123,58,131,100,100;
	areapercentheal "kvm2",141,52,149,60,100,100;
	mapannounce "kvm2","A batalha vai começar em 25 segundos!!",1,0x00BFFF;
	end;

OnTimer26000:
	mapannounce "kvm2","A batalha vai começar em 4 segundos!!",1,0x00BFFF;
	end;

OnTimer27000:
	donpcevent "::OnKvM02LightA";
	end;

OnTimer28000:
	donpcevent "::OnKvM02LightB";
	end;
	
OnTimer29000:
	donpcevent "::OnKvM02LightC";
	end;

OnTimer30000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM02BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM02BG_id2, 0);
	if( .Guillaume_Count < 3 || .Croix_Count < 3 )
	{
		set $@KvM02BG_Victory, 3;
		set $@KvM02BG, 3;
		mapannounce "kvm2","Não há jogadores suficientes para começar a batalha.",1,0x00BFFF;
		stopnpctimer;
		sleep 2000;
		donpcevent "KvM02_BG::OnStop";
		end;
	}

	set $@KvM02BG, 2; // Playing
	areapercentheal "kvm2",50,123,58,131,100,100;
	bg_warp $@KvM02BG_id1,"kvm2",62,119;
	areapercentheal "kvm2",141,52,149,60,100,100;
	bg_warp $@KvM02BG_id2,"kvm2",137,64;
	end;

OnTimer32000:
	mapannounce "kvm2","A batalha de Kreiger Von Midgard começou!!",1,0x00BFFF;
	end;

OnTimer300000:
	mapannounce "kvm2","A batalha vai terminar em 30 segundos!!",1,0x00BFFF;
	end;

OnTimer330000:
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM02_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM02_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM02BG, 3;
		set $@KvM02BG_Victory, 3;
		stopnpctimer;
		sleep 2000;
		mapannounce "kvm2","O tempo terminou! Esta batalha foi um empate...",1,0x00BFFF;
		donpcevent "KvM02_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 1;
	stopnpctimer;
	sleep 2000;
	mapannounce "kvm2","O exército de Guillaume ganhou a batalha de Kreiger Von Midgard",1,0x0000FF;
	donpcevent "KvM02_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 2;
	stopnpctimer;
	sleep 2000;
	mapannounce "kvm2","O exército de Croix ganhou a batalha de Battle of Kreiger Von Midgard",1,0xFF0000;
	donpcevent "KvM02_BG::OnStop";
	end;

OnStop:
	disablenpc "TherapistKvM02a";
	disablenpc "TherapistKvM02b";
	enablenpc "VintenarKvM02a";
	enablenpc "VintenarKvM02b";
	// Warp Teams
	bg_warp $@KvM02BG_id1,"kvm2",53,128;
	bg_warp $@KvM02BG_id2,"kvm2",146,55;
	donpcevent "KvM02_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "KvM02_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM02BG_Victory, 0;
	if( $@KvM02BG_id1 ) { bg_destroy $@KvM02BG_id1; set $@KvM02BG_id1, 0; }
	if( $@KvM02BG_id2 ) { bg_destroy $@KvM02BG_id2; set $@KvM02BG_id2, 0; }
	disablenpc "TherapistKvM02a";
	disablenpc "TherapistKvM02b";
	disablenpc "VintenarKvM02a";
	disablenpc "VintenarKvM02b";
	sleep 1000;
	mapwarp "kvm2","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "kvm2",0,3; // Just in case someone else
	bg_updatescore "kvm2",0,0;
	sleep 2000;
	set $@KvM02BG, 0;
	donpcevent "KvM02_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM02_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "kvm2","A batalha de Kreiger Von Midgard vai fechar em 1 minuto!",1,0x00BFFF;
	end;
OnTimer30000:
	mapannounce "kvm2","A batalha de Kreiger Von Midgard vai fechar em 30 segundos!",1,0x00BFFF;
	end;
OnTimer50000:
	mapannounce "kvm2","A batalha de Kreiger Von Midgard vai fechar em 10 segundos!",1,0x00BFFF;
	end;
OnTimer60000:
	donpcevent "KvM02_BG::OnReset";
	end;
}

// Battleground Therapist
// *********************************************************************

//kvm,51,130,5	script	Therapist in battle::TherapistKvM02a	95,{
//	mes "[Therapist in battle]";
//	mes "Just close your eyes, and take a deep breathe.";
//	mes "You can be free from pain.";
//	specialeffect2 312;
//	close;
//}

//kvm,148,53,1	script	Therapist in battle::TherapistKvM02b	95,{
//	mes "[Therapist in battle]";
//	mes "Just close your eyes, and take a deep breathe.";
//	mes "You can be free from pain.";
//	specialeffect2 312;
//	close;
//}

// Battleground rewards
// *********************************************************************

kvm2,51,130,5	script	Guillaume Vintenar::VintenarKvM02a	419,{
	if( $@KvM02BG_Victory )
	{
		if( $@KvM02BG_Victory == 3 )
		{
			bg_leave;
			warp "bg",148,99;
			end;
		}
		else if( $@KvM02BG_Victory == 1 )
		{ // Victory
			set .@Reward, 30;
			getitem 21046,1;
			mes "[Swandery]";
			mes "Abençoado Guillaume!!";
			mes "Vamos aproveitar nossa gloriosa vitória!";
			mes "" + strcharinfo(0) + ", é um sinal refletindo vitória";
			close2;
		}
		else
		{ // 
			set .@Reward, 10;
			mes "[Swandery]";
			mes "Você perdeu, mas você deu tudo de si nesta batalha.";
			mes "Isto daqui é uma recompensa pela sua grande dedicação ao Guillaume Marollo!";
			mes "Lide com esta derrota como uma lição, e depois você irá ficar melhor.";
			close2;
		}

		getitem 7829, .@Reward;
		getitem 7828, .@Reward;
		getitem 7773, .@Reward;
		bg_leave;
		warp "bg",148,99;
		end;
	}
	end;
}

kvm2,148,53,1	script	Croix Vintenar::VintenarKvM02b	415,{
	if( $@KvM02BG_Victory )
	{
		if( $@KvM02BG_Victory == 3 )
		{
			bg_leave;
			warp "bg",148,99;
			end;
		}
		else if( $@KvM02BG_Victory == 2 )
		{ // Victory
			set .@Reward, 30;
			getitem 21046,1;
			mes "[Swandery]";
			mes "Abençoado Croax!!";
			mes "Vamos aproveitar nossa gloriosa vitória!";
			mes "" + strcharinfo(0) + ", é um sinal refletindo vitória";
			close2;
		}
		else
		{ // 
			set .@Reward, 10;
			mes "[Swandery]";
			mes "Ah, " + strcharinfo(0) + ". Não fique triste.";
			mes "Mesmo que não tenhamos ganhado, nós demos o nosso melhor.";
			mes "Está é uma premiação de Croix, e por favor esqueça está batalha. Nós podemos ganhar o próximo.";
			close2;
		}

		getitem 7829, .@Reward;
		getitem 7828, .@Reward;
		getitem 7773, .@Reward;
		bg_leave;
		warp "bg",148,99;
		end;
	}
	end;
}

// MapFlags
// *********************************************************************

kvm2	mapflag	battleground
kvm2	mapflag	nomemo
kvm2	mapflag	nosave	SavePoint
kvm2	mapflag	noteleport
kvm2	mapflag	nowarp
kvm2	mapflag	nowarpto
kvm2	mapflag	noreturn
kvm2	mapflag	nobranch
kvm2	mapflag	nopenalty
kvm2	mapflag	noemergencycall