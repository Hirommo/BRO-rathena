
-	shop	VOTESHOP	-1,512:-1	//Duplicar essa linha para quantidade de lojas que precisar
prt_smart,167,196,4	script	Garota dos Votos	726,{
//	function getTimeFromVote;
	set .npc$, "[ ^a60536Garota dos Votos^000000 ]"; //Nome do NPC
	set .@minVotes, 0;	//Minimo de votos para ver a loja
	set .@voteTime, 24;	//Tempo minimo em horas entre os votos
	setarray .@topNames$[0], "TOPRAG.COM.BR", "TOPRAG.ORG";	//Array com os tops para a lista
	setarray .@lastVotes[0], 0, 0;
	setarray .storeNames$[0], "Itens Diversos";	//Array com os nomes das lojas
	setarray .VOTEStore1[0], 21059, 8, 21039, 6, 21015, 6, 21016, 4, 21061, 4, 21075, 2, 21000, 2;	//Itens que serão vendidos no padrão: ID, Quantidade[, ID, Quatidade[, ...]] - Duplicar essa linha e mudar o nome para mais lojas

	//Recebe quantidade de pontos no início:
	query_sql("SELECT `points` FROM `cp_v4p_voters` WHERE `account_id`='" + getcharid(3) + "'", .@votePoints);
	
	//Recebe o tempo desde o ultimo voto do jogador:
	query_sql("SELECT `last_vote1` FROM `vote_point` WHERE `account_id`='" + getcharid(3) + "'", .@lastVotes[0]);
	query_sql("SELECT `last_vote2` FROM `vote_point` WHERE `account_id`='" + getcharid(3) + "'", .@lastVotes[1]);

	mes .npc$;
	dispbottom "[ Digite @vote para ver seus Pontos de Voto ]."; 
	mes "Toda vez que você vota no ^ff0000SmartRO^000000, seus votos são convertidos em Pontos, ou seja, você nos ajuda e a gente te ajuda!";
	mes "^0000ff- Você tem um total de " + .@votePoints + " Pontos de Voto.^000000";
//	for (set .@i, 0;.@i < getarraysize(.@topNames$); set .@i, .@i + 1) {
//		mes "";
//		mes "" + .@topNames$[.@i] + ":";
//		getTimeFromVote(.@lastVotes[.@i], .@voteTime);
//		mes "";
//	}
	
	if (.@votePoints < .@minVotes) {
		mes "^ff0000É preciso ter pelo menos " + .@minVotes + " Pontos de Voto para trocá-los por items!^000000";
		close;
	}
	next;
	set .@menuOptions$, "";
	for (set .@i, 0; .@i < getarraysize(.storeNames$); set .@i, .@i + 1) {
		set .@menuOptions$, .@menuOptions$ + "[^ff0000»^000000] " + .storeNames$[.@i];
			if ((.@i + 1) < getarraysize(.storeNames$)) {
				set .@menuOptions$, .@menuOptions$ + ":";
			}
	}
	set .s, select(.@menuOptions$);
	npcshopitem "VOTESHOP", 512, -1;
	npcshopdelitem "VOTESHOP", 512;
	for(set .@i, 0; .@i < getarraysize(getd(".VOTEStore" + .s)); set .@i, .@i + 2) {
		npcshopadditem "VOTESHOP", getd(".VOTEStore" + .s + "[" + .@i + "]"), getd(".VOTEStore" + .s + "[" + (.@i + 1) + "]");
	}
	message strcharinfo(0), "[ Esta loja só aceita Pontos de Voto ].";
	dispbottom "[ Você tem: " + .@votePoints + " Pontos de Voto ].";
	close2;
	callshop "VOTESHOP", 1;
	npcshopattach "VOTESHOP";
	end;

	OnBuyItem:
		query_sql("SELECT `points` FROM `cp_v4p_voters` WHERE `account_id`='" + getcharid(3) + "'", .@votePoints);
		mes .npc$;
		if (.@votePoints < 1 ){
			message strcharinfo(0), "[ Você não possui Pontos de Voto ].";
			close;
		} else {
			for (set .@i, 0; .@i < getarraysize(@bought_nameid); set .@i, .@i + 1) {
				for (set .@j, 0; .@j < getarraysize(getd(".VOTEStore" + .s)); set .@j, .@j + 2 ){
					if( getd(".VOTEStore" + .s + "[" + .@j + "]") == @bought_nameid[.@i] ){
						set .@itemcost, (getd(".VOTEStore" + .s + "[" + (.@j + 1) + "]") * @bought_quantity[.@i]);
						set .@totalcost, .@totalcost + .@itemcost;
						break;
					}
				}
			}
			if (.@totalcost > .@votePoints) {
				message strcharinfo(0), "[ Você não tem Pontos de Voto o suficiente ].";
			} else {
				set .@newPoints, .@votePoints - .@totalcost;
				query_sql("UPDATE `cp_v4p_voters` SET `points`='" + .@newPoints + "' WHERE `account_id`='" + getcharid(3) + "'");
				for (set .@i, 0; .@i < getarraysize(@bought_nameid); set .@i, .@i + 1) {
					getitem @bought_nameid[.@i], @bought_quantity[.@i];
					dispbottom "Você comprou " + @bought_quantity[.@i] + "x " + getitemname(@bought_nameid[.@i]) + ".";
				}
				mes "Seu total de Pontos de Voto agora é " + .@newPoints + ".";
			}
			set .@totalcost, 0;
			deletearray @bought_nameid[0], 128;
			deletearray @bought_quantity[0], 128;
			close;
		}
		end;
	OnVote:
		query_sql("SELECT `points` FROM `cp_v4p_voters` WHERE `account_id`='" + getcharid(3) + "'", .@votePoints);
		dispbottom "[ Você tem " + .@votePoints + " Pontos de Voto ].";
		end;
	OnInit:
		bindatcmd("vote", strnpcinfo(0) +"::OnVote");
		end;
	
function getTimeFromVote {
		set .@seconds, getarg(0);
		set .@minutes, .@seconds / 60;
		set .@hours, .@minutes / 60;
		
		if (.@hours > getarg(1)) {	//arg(1) é o voteTime
			mes "^00FF00Você já pode votar.^000000";	//Já pode votar
		} else {
			if( .@hours > 1 ){
				mes "^FF0000Você votou a " + .@hours + " horas atrás.^000000";	//Votou a X horas atrás
			} else {
				if( .@minutes > 1 ){
					mes "^FF0000Você votou a " + .@minutes + " minutos atrás.^000000";	//Votou a X minutos atrás
				} else {
					if( .@seconds > 1 ){
						mes "^FF0000Você votou a " + .@seconds + " segundos atrás.^000000";	//Votou a X segundos atrás
					} else {
						mes "^FFA500Você não votou.^000000";	//Não votou
						}
					}
				}
			}
			return;
		}
}