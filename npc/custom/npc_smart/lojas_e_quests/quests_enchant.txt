prontera,138,182,7	script	Quests Enchant	681,{
function Add; function Chk; function Slot; function A_An;

if( #portugues ) {
		mes "[ ^a60536Quests SmartRO^000000 ]";
		mes "Olá, ^FF0000"+strcharinfo(0)+"^000000.";
		mes "Aqui no SmartRO temos um novo sistema de quests que funciona como uma loja que pede itens.";
		next;
		mes "[ ^a60536Quests SmartRO^000000 ]";
		mes "Então, qual tipo de enchant você quer fazer?";
		next;
}else{
		mes "[ ^a60536Quests SmartRO^000000 ]";
		mes "Hello, ^FF0000"+strcharinfo(0)+"^000000.";
		mes "Here at SmartRO, we have a new quest system that works as a store.";
		next;
		mes "[ ^a60536Quests SmartRO^000000 ]";
		mes "So what type of item you want to do?";
		next;
}

	if(.Shops2$ != "") set .@i,1;
	else {
		set .@menu$,"";
		for(set .@i,1; .@i<=getarraysize(.Shops2$); set .@i,.@i+1)
			set .@menu$, .@menu$+.Shops2$[.@i]+":";
		set .@i, select(.@menu$); }
	dispbottom "Selecione um item de cada vez.";
	callshop "q2shop"+.@i,1;
	npcshopattach "q2shop"+.@i;
	end;

function Add {
	if (getitemname(getarg(1))=="null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped)."; return; }
	for(set .@n,5; .@n<127; set .@n,.@n+2) {
		if (!getarg(.@n,0)) break;
		if (getitemname(getarg(.@n))=="null") {
			debugmes "Quest requirement #"+getarg(.@n)+" invalid (skipped)."; return; } }
	for(set .@i,2; .@i<.@n; set .@i,.@i+1)
		set getd(".q_"+getarg(1)+"["+(.@i-2)+"]"), getarg(.@i);
	npcshopadditem "q2shop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return; }
function Chk {
	if (getarg(0)<getarg(1)) { set @qe0,1; return "^FF0000"; }
	else return "^00FF00"; }
function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot){
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),11)>0) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$; } }
function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0); }

OnBuyItem:
	set .@q[0],@bought_nameid;
	copyarray .@q[1],getd(".q_"+@bought_nameid+"[0]"),getarraysize(getd(".q_"+@bought_nameid+"[0]"));
	if (!.@q[1]) { message strcharinfo(0),"An error has occurred."; end; }
	mes "[ ^a60536Quests SmartRO^000000 ]";
	mes "Recompensa: ^0055FF"+((.@q[1]>1)?.@q[1]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requerimentos:";
	if (.@q[2]) mes " > "+Chk(Zeny,.@q[2])+.@q[2]+" Zeny^000000";
	if (.@q[3]) mes " > "+Chk(getd(.Points$[0]),.@q[3])+.@q[3]+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+.@q[3]+")^000000";
	if (.@q[4]) for(set .@i,4; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1])+((.DisplayID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+.@q[.@i+1]+")^000000";
	next;
	set @qe1, getiteminfo(.@q[0],5); set @qe2, getiteminfo(.@q[0],11);
	addtimer 1000, strnpcinfo(1)+"::OnEnd";
	while(1){
		switch(select("[^ff0000»^000000] Fazer ^0055FF"+getitemname(.@q[0])+"^000000:"+((((@qe1&1) || (@qe1&256) || (@qe1&512)) && @qe2>0 && !@qe6)?"[^ff0000»^000000] Experimentar Item.":"")+": ^777777Cancel.^000000")) {
			case 1:
				if (@qe0) { 
					mes "[ ^a60536Quests SmartRO^000000 ]";
					mes "Você está esquecendo um ou mais requisitos da quest.";
					close; }
				if (!checkweight(.@q[0],.@q[1])) {
					mes "[ ^a60536Quests SmartRO^000000 ]";
					mes "^FF0000You need "+(((.@q[1]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
					close; }
				if (.@q[2]) set Zeny, Zeny-.@q[2];
				if (.@q[3]) setd .Points$[0], getd(.Points$[0])-.@q[3];
				if (.@q[4]) for(set .@i,4; .@i<getarraysize(.@q); set .@i,.@i+2)
					delitem .@q[.@i],.@q[.@i+1];
				getitem .@q[0],.@q[1];
				if (.Announce) announce "[Quest SmartRO]: » "+strcharinfo(0)+" « acabou de criar seu item: "+(getitemname(.@q[0]))+"!", bc_all, 0xd8ff00;
				specialeffect2 699;
				close;
			case 2:
				set @qe3, getlook(3); set @qe4, getlook(4); set @qe5, getlook(5);
				if (@qe1&1) atcommand "@changelook 3 "+@qe2;
				if (@qe1&256) atcommand "@changelook 1 "+@qe2;
				if (@qe1&512) atcommand "@changelook 2 "+@qe2;
				set @qe6,1;
				break;
			case 3:
				close; } }

OnEnd:
	if (@qe6) { atcommand "@changelook 3 "+@qe3; atcommand "@changelook 1 "+@qe4; atcommand "@changelook 2 "+@qe5; }
	for(set .@i,0; .@i<7; set .@i,.@i+1) setd "@qe"+.@i,0;
	end;

OnPCLoadMapEvent:
	if( strcharinfo(3) == strnpcinfo(4) )
		showevent 1,0;
	end;

OnInit:
	freeloop(1);
	waitingroom "Quests Enchant",0;

// --------------------- Config ---------------------
// Custom points, if needed: "<variable>","<name to display>"
	setarray .Points$[0],"#CASHPOINTS","Cash Points";

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,2;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .DisplayID,1;	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)

// Shop categories, if needed: "<Shop 1>","<Shop 2>"{,...};
// Duplicate dummy data for any additional Shops2 (bottom of script).
// If no categories, use the second line instead (remove //).

	setarray .Shops2$[1],"[^ff0000»^000000] DemiHuman +2% Elemental.","[^ff0000»^000000] ATQM +2% Elemental.","[^ff0000»^000000] HP/SP +4% Elemental. ","[^ff0000»^000000] DemiHuman -2% Elemental.";

	// set .Shops2$,"n/a";

// Add(<shop number>,<reward ID>,<reward amount>,<Zeny cost>,<point cost>,
//     <required item ID>,<required item amount>{,...});
// Shop number corresponds with order above (default is 1).
// Note: Do NOT use a reward item more than once!

	Add(1,4880,1,50000000,0,7428,4,978,20,4850,1);
	Add(1,4881,1,50000000,0,7426,4,975,20,4850,1);
	Add(1,4882,1,50000000,0,7429,4,979,20,4850,1);
	Add(1,4883,1,50000000,0,7427,4,976,20,4850,1);
	
	Add(2,4888,1,50000000,0,7428,4,978,20,4851,1);
	Add(2,4889,1,50000000,0,7426,4,975,20,4851,1);
	Add(2,4890,1,50000000,0,7429,4,979,20,4851,1);
	Add(2,4891,1,50000000,0,7427,4,976,20,4851,1);

	Add(3,4884,1,50000000,0,7428,4,978,20,4852,1);
	Add(3,4885,1,50000000,0,7426,4,975,20,4852,1);
	Add(3,4886,1,50000000,0,7429,4,979,20,4852,1);
	Add(3,4887,1,50000000,0,7427,4,976,20,4852,1);

	Add(4,4876,1,50000000,0,7428,4,978,20,4874,1);
	Add(4,4877,1,50000000,0,7426,4,975,20,4874,1);
	Add(4,4878,1,50000000,0,7429,4,979,20,4874,1);
	Add(4,4879,1,50000000,0,7427,4,976,20,4874,1);


// --------------------------------------------------
	freeloop(0);
	for(set .@i,1; .@i<=getarraysize(.Shops2$); set .@i,.@i+1)
		npcshopdelitem "q2shop"+.@i,909;
	end;
}

// -------- Dummy data (duplicate as needed) --------
-	shop	q2shop1	-1,909:-1
-	shop	q2shop2	-1,909:-1
-	shop	q2shop3	-1,909:-1
-	shop	q2shop4	-1,909:-1