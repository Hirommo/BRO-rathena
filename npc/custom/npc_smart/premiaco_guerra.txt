/*
CREATE TABLE IF NOT EXISTS `woe_eco_reward` (
  `account_id` INT(11) UNSIGNED NOT NULL DEFAULT '0',
  `ip` VARCHAR(100) NOT NULL DEFAULT '',
  `mac` VARCHAR(18) NOT NULL DEFAULT '',
  PRIMARY KEY (`account_id`, `ip`, `mac`)
) ENGINE=MyISAM;
*/

prontera, 128, 165,5	script	Rei dos Castelos#woe	56,{
	//Configurações:
	set .@npcName$, "[ ^a60536Rei dos castelos^000000 ]";
	setarray .@itemRewards[0], 21035, 21036, 21037, 21038;
	
	//Daqui pra baixo NPC.
	if (!getcharid(2)) {
		mes .@npcName$;
		mes "Você não é membro de nenhum clã.";
		close;
	}
	
	set .@lastIP$, callsub(sub_Querys, 2, getcharid(3));
	set .@lastMac$, callsub(sub_Querys, 3, getcharid(3));
	set .@check, callsub(sub_Querys, 4, .@lastIP$, .@lastMac$, getcharid(3));
	if (.@check) {
		mes .@npcName$;
		mes "Você já retirou sua recompensa hoje, volte amanhã!";
		close;
	}
	
	set .@castleId, callsub(sub_Querys, 0, getcharid(2));
	if (.@castleId < 0) {
		mes .@npcName$;
		mes "Seu clã não é dono de nenhum castelo, boa sorte na próxima guerra ^FF0000:)^000000";
		close;
	}
	
	if (getarraysize(getd("$rewardMembersCastle" + .@castleId)) < 2) {
		mes .@npcName$;
		mes "Não existem membros que dominaram o castelo salvos.";
		close;
	}
	
	if (getd("$rewardMembersCastle" + .@castleId + "[0]") != getcharid(2)) {
		mes .@npcName$;
		mes "O seu clã não foi o clã que dominou o castelo.";
		close;
	}
	
	set .@isMember, 0;
	for (set .@i, 1; .@i < getd("$rewardMembersCastle" + .@castleId); set .@i, .@i + 1) {
		if (getd("$rewardMembersCastle" + .@castleId + "[" + .@i + "]") == getcharid(0)) {
			set .@isMember, 1;
		}
	}
	
	if (.@isMember == 0) {
		mes .@npcName$;
		mes "Você não fazia parte desse clã quando ele dominou o castelo.";
		close;
	}
	
	set .@economy, callsub(sub_Querys, 1, .@castleId);
	if (!.@economy) {
		mes .@npcName$;
		mes "Seu clã está com a economia muito baixa, invista em seu castelo e ganhe sempre mais prêmios";
		close;
	}
	
	if (.@economy < 20) {
		set .@rewardAmount, 1;
	} else if (.@economy < 40 && .@economy > 19) {
		set .@rewardAmount, 2;
	} else if (.@economy < 60 && .@economy > 39) {
		set .@rewardAmount, 3;
	} else if (.@economy < 80 && .@economy > 59) {
		set .@rewardAmount, 4;
	} else if (.@economy < 100 && .@economy > 79) {
		set .@rewardAmount, 5;
	} else if(.@economy == 100) {
		set .@rewardAmount, 6;
	}

	set .@castleName$, callsub(sub_CastleName, .@castleId);
	
	mes .@npcName$;
	mes "Seu clã está investindo bem no castelo ^0000FF" + .@castleName$ + "^000000.";
	mes "Vocês tem uma economia de ^0000FF" + .@economy +"^000000/100 pontos!";
	mes "A cada 20 pontos de economia, você ganha 1 caixa a mais!";
	next;
	
	mes .@npcName$;
	mes "Boa sorte em suas lutas!";
	mes "Receba seu prêmio: ^0000ff"+ .@rewardAmount +"x ^000000Caixa dos Deuses Aleatória.";
	for (set .@i, 0; .@i < .@rewardAmount; set .@i, .@i + 1) {
		getitem .@itemRewards[rand(getarraysize(.@itemRewards) - 1)], 1;
	}
	query_sql("INSERT INTO `woe_eco_reward` VALUES (" + getcharid(3) + ", '" + .@lastIP$ + "', '" + .@lastMac$ + "')");
	close;
	
	sub_Querys:
		switch(getarg(0)) {
			case 0:
				query_sql("SELECT `castle_id` FROM `guild_castle` WHERE `guild_id`='" + getarg(1) + "' ORDER BY `economy` DESC", .@castleId);
				return getarraysize(.@castleId) ? .@castleId[0] : -1;
			case 1:
				query_sql("SELECT `economy` FROM `guild_castle` WHERE `castle_id`='" + getarg(1) + "'", .@economy);
				return getarraysize(.@economy) ? .@economy[0] : 0;
			case 2:
				query_sql("SELECT `last_ip` FROM `login` WHERE `account_id`='" + getarg(1) + "'", .@lastIP$);
				return getarraysize(.@lastIP$) ? .@lastIP$[0] : "";
			case 3:
				query_sql("SELECT `last_mac` FROM `login` WHERE `account_id`='" + getarg(1) + "'", .@lastMac$);
				return getarraysize(.@lastMac$) ? .@lastMac$[0] : "";
			case 4:
				query_sql("SELECT `account_id` FROM `woe_eco_reward` WHERE `account_id`='" + getarg(3) + "' OR `ip`='" + getarg(1) + "' OR `mac`='" + getarg(2) + "'", .@aid);
				return getarraysize(.@aid) ? 1 : 0;
			default:
				break;
		}
	return 0;
	
	sub_CastleName:
		setarray .@names$[0], "Neuschwanstein", "Hohenschwangau", "Nuernberg", "Wuerzburg", "Rothenburg", "Repherion", "Eeyolbriggar", "Yesnelph", "Bergel", "Mersetzdeitz",
							  "Bright Arbor", "Scarlet Palace", "Holy Shadow", "Sacred Altar", "Bamboo Grove Hill", "Kriemhild", "Swanhild", "Fadhgridh", "Skoegul", "Gondul",
							  "Earth", "Air", "Water", "Fire", "Himinn", "Andlangr", "Viblainn", "Hljod", "Skidbladnir", "Mardol", "Cyr", "Horn", "Gefn", "Bandis";
	return getarg(0) > getarraysize(.@names$) ? "Desconhecido" : .@names$[getarg(0)];

	OnClock0001:
		query_sql("DELETE FROM `woe_eco_reward`");
	end;
}