/*.-----------------------------------------------------------------.
  .    ____                          __                             .
  .   /\  _`\                       /\ \__  __                      .
  .   \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __      .
  .    \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\    .
  .     \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/    .
  .      \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\   .
  .       \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/   .
  .                                                                 .
  .          2014~2017 © Creative Services and Development          .
  .                      www.creativesd.com.br                      .
  .-----------------------------------------------------------------.
  . Script:                                                         .
  .    Battleground Warfare (1º Arena de Flavius)                   .
  .-----------------------------------------------------------------.
  . Autor: Romulo SM (sbk_)                          Versão: 1.0    .
  .                                                                 .
  . Compatibilidade:                                                .
  .    eAthena, rAthena, Hercules e derivações.                     .
  .-----------------------------------------------------------------.
  . Descrição:                                                      .
  .    Batalha no qual os jogadores devem destruir o cristal do     .
  .    adversário.                                                  .
  *-----------------------------------------------------------------*/
  
// Engine das Batalhas Campais da Arena de Flavius
// -----------------------------------------------------------------
bat_b01,15,15,3	script	start#bat_b01	CLEAR_NPC,{
OnInit:
	// Removendo Jogadores da Arena
	mapwarp "bat_b01","bat_room",154,150;
	
	// Criando Fila de Espera e Times especifico para Arena
	queue_create(3, "1ª Arena de Flavius", 0, 0, "start#bat_b01::OnReadyCheck");
	set $@FlaviusBG1_id1, bg_create_team(5, "bat_b01", 10, 290, 87, 75, 0, "start#bat_b01::OnGuillaumeQuit", "", "start#bat_b01::OnGuillWO");
	set $@FlaviusBG1_id2, bg_create_team(6, "bat_b01", 390, 10, 311, 224, 1, "start#bat_b01::OnCroixQuit", "", "start#bat_b01::OnCroixWO");
	
	// Expulsando Jogadores
	bg_kick_team($@FlaviusBG1_id1, 0);
	bg_kick_team($@FlaviusBG1_id2, 0);
	
	// Íniciando Eventos
	set .global_announce, 0;
	set .BlockStatus, 0;
	donpcevent "start#bat_b01::OnReadyCheck";
	
	// Seta NPC's de Bandeiras com o Simbolo do Exército.
	donpcevent "::OnBatB01Emblem";
	end;

OnReadyCheck:
	if( .BlockStatus )
	{
		sleep 5000;
		donpcevent "start#bat_b01::OnReadyCheck";
	}
		
	set .BlockStatus, 1;

	set .@min_player, callfunc("bg_getarenainfo", 3, "min_player");
	set .@max_player, callfunc("bg_getarenainfo", 3, "max_player");
	set .@a_name$, callfunc("bg_getarenainfo", 3, "arena_name");
	set .@min_player, (.@min_player*2);
	
	if( .@max_player > MAX_BG_MEMBERS )
	{
		bg_console "(BG_ID: 3) Maximo de jogadores em uma equipe ultrapassa o limite estabelecido, definindo o maximo de jogadores para " + MAX_BG_MEMBERS + ".";
		set .@max_player, MAX_BG_MEMBERS;
	}
	
	queue_get_players 3;
	set .@playlist_team1, bg_team_size($@FlaviusBG1_id1);
	set .@playlist_team2, bg_team_size($@FlaviusBG1_id2);
	if( !.@queue_count )
	{
		set .BlockStatus, 0;
		end;
	}
	else if( $@FlaviusBG1 && $@FlaviusBG1 < 2 && .@queue_count && (.@playlist_team1 < .@max_player || .@playlist_team2 < .@max_player) )
	{
		set .@guill, 0;
		set .@croix, 0;
		set .@count_g, 0;
		set .@count_c, 0;
		
		// ---------------------------------------------------
		// -             Igualando os Exércitos              -
		// ---------------------------------------------------
		if( .@playlist_team1 < .@playlist_team2 )
		{
			set .@guill, .@playlist_team2-.@playlist_team1;
			for( set .@i,0; .@i < .@guill; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_g, .@count_g + 1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 3; // Reset
			set .@playlist_team1, bg_team_size($@FlaviusBG1_id1);
		}
		else if( .@playlist_team2 < .@playlist_team1 )
		{
			set .@croix, .@playlist_team1-.@playlist_team2;
			for( set .@i,0; .@i < .@croix; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG2_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_c, .@count_c + 1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Croix em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 3; // Reset
			set .@playlist_team2, bg_team_size($@FlaviusBG1_id2);
		}
		// ---------------------------------------------------
		// -            Expandindo os Exércitos              -
		// ---------------------------------------------------
		if( .@queue_count && .@playlist_team1 == .@playlist_team2 && .@playlist_team1 < .@max_player && .@playlist_team2 < .@max_player )
		{
			set .@guill, .@max_player-.@playlist_team1;
			set .@croix, .@max_player-.@playlist_team2;
			
			if( .@queue_count > (.@guill+.@croix) )
				set .@queue_count, (.@guill+.@croix);
				
			if( .@queue_count%2 )
				set .@queue_count, .@queue_count-1;
			
			set .@size, .@queue_count/2;
			for( set .@i, 0; .@i < .@size; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_g$[.@count_g], .@queue_player$[.@i];
				set .@count_g, .@count_g + 1;
			}
			
			for( set .@i,.@size; .@i < .@queue_count; set .@i, .@i + 1 )
			{
				bg_join_team $@FlaviusBG1_id2, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_c$[.@count_c], .@queue_player$[.@i];
				set .@count_c, .@count_c + 1;
			}
		}
		
		if( .@count_c || .@count_g )
		{
			announce "[Batalhas Campais]: " + (.@count_c+.@count_g) + " jogadores estão sendo enviados para uma batalha que está acontecendo em " + .@a_name$ + ", os Exércitos foram equilibrados e expandidos.", bc_all, "0xFFCE00";
			sleep 3000;
		}
		
		deletearray .@queue_cid;
		deletearray .@queue_aid;
		deletearray .@queue_player$;
		set .@queue_count, 0;
		queue_get_players 3; // Reset
		
		set .@queue_size, queue_size(3);
		set .@team1_size, bg_team_size($@FlaviusBG1_id1);
		set .@team2_size, bg_team_size($@FlaviusBG1_id2);
		
		if( .@team1_size < .@max_player || .@team2_size < .@max_player )
		{
			set .@size, (2*.@max_player)-(.@team1_size+.@team2_size)-.@queue_size;
			announce "[Batalhas Campais]: Ainda há " + (.@size) + " vagas para uma batalha que está acontecendo em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	else if( $@FlaviusBG1 && !.global_announce && .@queue_count >= .@min_player )
	{
		announce "[Batalhas Campais]: A batalha está completa, mas há uma batalha acontecendo em " + .@a_name$ + ", todos jogadores que estão na fila de espera devem aguardar.", bc_all, "0xFFCE00";
		set .global_announce, 1;
	}
	else if( .@queue_count < .@min_player )
		announce "[Batalhas Campais]: Falta " + (.@min_player-.@queue_count) + " jogadores na batalha em " + .@a_name$ + "!", bc_all, "0xFFCE00";
	else
	{
		// ---------------------------------------------------
		// -                  Balanceamento                  -
		// ---------------------------------------------------
		if( .@queue_count > (.@max_player*2) )
			set .@queue_count, (.@max_player*2);
			
		if( .@queue_count%2 )
			set .@queue_count, .@queue_count-1;
			
		if( .@queue_count >= .@min_player )
		{
			for( set .@i,0; .@i < .@queue_count; set .@i, .@i + 2 )
			{
				// Ingressando no Exército de Guillaume
				bg_join_team $@FlaviusBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume, será teleportado em 10 segundos.";
				
				// Ingressando no Exército de Croix
				bg_join_team $@FlaviusBG1_id2, 1, .@queue_aid[.@i+1];
				queue_leave 0, .@queue_aid[.@i+1];
				message .@queue_player$[.@i+1], "Você ingressou no Exército de Croix, será teleportado em 10 segundos.";
			}
		}
		
		bg_refresh_patent($@FlaviusBG1_id1);
		bg_refresh_patent($@FlaviusBG1_id2);
		set .global_announce, 0;
		set $@FlaviusBG1, 1;
		announce "[Batalhas Campais]: Ambos os Exércitos, estão completo boa sorte nas batalhas em " + .@a_name$ + ".", bc_all, "0xFFCE00";
		set $@FlaviusBG1_CroixScore, 0;
		set $@FlaviusBG1_GuillScore, 0;
		bg_updatescore "bat_b01",0,0;
		donpcevent "start#bat_b01::OnEnable";
		donpcevent "BattleGround#queue_main::OnStopTimer";
		sleep 3000;
		initnpctimer;
		
		if( .@queue_count < (2*.@max_player) )
		{
			sleep 3000;
			set .@count, (2*.@max_player)-.@queue_count;
			announce "[Batalhas Campais]: Ainda há " + .@queue_count + " vagas nas batalhas em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	set .BlockStatus, 0;
	end;

OnEnable:
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Tenente#b01_a";
	disablenpc "Tenente#b01_b";
	donpcevent "countdown#bat_b01::OnEnable";
	donpcevent "time_event#bat_b01::OnEnable";
	initnpctimer;
	end;

OnReset:
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Tenente#b01_a";
	disablenpc "Tenente#b01_b";
	bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
	bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
	end;

OnGuillaumeQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 3, 0, 0, 1, 0, $@FlaviusBG1_CroixScore;
	donpcevent "start#bat_b01::OnReadyCheck";
	end;
	
OnCroixQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 3, 0, 0, 1, $@FlaviusBG1_GuillScore, 0;
	donpcevent "start#bat_b01::OnReadyCheck";
	end;
	
OnGuillWO:
	if( queue_size(3) )
		end;
		
	// Sinaliza a Vitória
	set $@FlaviusBG1, 2;
	set $@FlaviusBG1_Victory, 2;
	
	// Computa Ranking e faz entrega de recompensas
	callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
	
	// Inicia Eventos de Finalização
	donpcevent "Terapeuta da Batalha#b01_a::OnStop";
	donpcevent "Terapeuta da Batalha#b01_b::OnStop";
	mapannounce "bat_b01", "Tenente Swandery: Os covardes de Guillaume fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	enablenpc "Tenente#b01_a";
	enablenpc "Tenente#b01_b";
	donpcevent "countdown#bat_b01::OnStop";
	donpcevent "time_event#bat_b01::OnStop";
	donpcevent "start#bat_b01_Out::OnBegin";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	end;
	
OnCroixWO:
	if( queue_size(3) )
		end;
		
	// Sinaliza a Vitória
	set $@FlaviusBG1, 2;
	set $@FlaviusBG1_Victory, 1;
	
	// Computa Ranking e faz entrega de recompensas
	callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
	
	// Inicia Eventos de Finalização
	donpcevent "Terapeuta da Batalha#b01_a::OnStop";
	donpcevent "Terapeuta da Batalha#b01_b::OnStop";
	mapannounce "bat_b01", "Tenente Axl Rose: Os covardes de Croix fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	enablenpc "Tenente#b01_a";
	enablenpc "Tenente#b01_b";
	donpcevent "countdown#bat_b01::OnStop";
	donpcevent "time_event#bat_b01::OnStop";
	donpcevent "start#bat_b01_Out::OnBegin";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	end;

// Checagem de Jogadores na Arena.
// -------------------------------------------------------
OnTimer60000:
	stopnpctimer;
	if( !getmapusers("bat_b01") )
	{
		set $@FlaviusBG1, 0;
		bg_kick_team $@FlaviusBG1_id1;
		bg_kick_team $@FlaviusBG1_id2;
		donpcevent "start#bat_b01_Out::OnStop";
		
		if( $@BG_MODE )
			donpcevent "BattleGround#queue_main::OnReload";
		else
			donpcevent "start#bat_b01::OnReadyCheck";
	}
	else
		initnpctimer;
	end;
}

bat_b01,1,1,3	script	OBJ#bat_b01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",61,150,"Cristal Vermelho",1915,"","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_a::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "O Cristal de Guillaume foi destruído.",bc_map,"0xFFCE00";
		set $@FlaviusBG1_CroixScore, $@FlaviusBG1_CroixScore+1;
		set .@score_round, callfunc("bg_getarenainfo",3,"score_round");
		bg_updatescore "bat_b01",$@FlaviusBG1_GuillScore,$@FlaviusBG1_CroixScore;
		
		if( $@FlaviusBG1_CroixScore >= .@score_round )
		{
			set $@FlaviusBG1, 2;
			set $@FlaviusBG1_Victory, 2;
			enablenpc "Tenente#b01_a";
			enablenpc "Tenente#b01_b";
			donpcevent "time#bat_b01::OnStop";
			donpcevent "countdown#bat_b01::OnStop";
			donpcevent "time_event#bat_b01::OnStop";
			donpcevent "start#bat_b01_Out::OnBegin";
			mapannounce "bat_b01", "Tenente Swandery: Uau! Nós destruímos o Cristal do Exército de Guillaume. Nós vencemos!",bc_map,"0xFFCE00";
			
			// Computa Ranking e faz entrega de recompensas
			callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
			
			bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
			bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		}
		else {
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
			bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
			bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
			sleep 1000;
			mapannounce "bat_b01", "Tenente Swandery: Uau! Nós destruímos o Cristal do Exército de Guillaume. Nós vencemos esta rodada!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_b01,1,2,3	script	OBJ#bat_b01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",328,150,"Cristal Azul",1914,"","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_b::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "O Cristal de Croix foi destruído.",bc_map,"0xFFCE00";
		set $@FlaviusBG1_GuillScore, $@FlaviusBG1_GuillScore+1;
		set .@score_round, callfunc("bg_getarenainfo",3,"score_round");
		bg_updatescore "bat_b01",$@FlaviusBG1_GuillScore,$@FlaviusBG1_CroixScore;
		
		if( $@FlaviusBG1_GuillScore >= .@score_round )
		{
			set $@FlaviusBG1, 2;
			set $@FlaviusBG1_Victory, 1;
			enablenpc "Tenente#b01_a";
			enablenpc "Tenente#b01_b";
			donpcevent "time#bat_b01::OnStop";
			donpcevent "countdown#bat_b01::OnStop";
			donpcevent "time_event#bat_b01::OnStop";
			donpcevent "start#bat_b01_Out::OnBegin";
			mapannounce "bat_b01", "Tenente Axl Rose: Uau! Nós destruímos o Cristal do Exército de Guillaume. Nós vencemos!",bc_map,"0xFFCE00";
			
			// Computa Ranking e faz entrega de recompensas
			callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
			
			bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
			bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		}
		else {
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
			bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
			bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
			sleep 1000;
			mapannounce "bat_b01", "Tenente Axl Rose: Uau! Nós destruímos o Cristal do Exército de Guillaume. Nós vencemos esta rodada!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,159,"Guardião de Guillaume",1949,"Guardião","guardian#bat_b01_a::OnMyMobDead";
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,141,"Guardião de Guillaume",1949,"Guardião","guardian#bat_b01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_a::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_a::OnGreen";
		mapannounce "bat_b01", "O Guardião que protegia o Cristal de Guillaume foi destruído.",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,160,"Guardião de Croix",1949,"Guardião","guardian#bat_b01_b::OnMyMobDead";
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,138,"Guardião de Croix",1949,"Guardião","guardian#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_b::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_b::OnGreen";
		mapannounce "bat_b01", "O Guardião que protegia o Cristal de Croix foi destruído.",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,4,3	script	cell#bat_b01_a	CLEAR_NPC,{
OnRed:
	setwall "bat_b01",62,151,3,2,0,"bat_b01_a01";
	setwall "bat_b01",62,150,3,2,0,"bat_b01_a02";
	setwall "bat_b01",62,149,3,2,0,"bat_b01_a03";
	end;

OnGreen:
	delwall "bat_b01_a01";
	delwall "bat_b01_a02";
	delwall "bat_b01_a03";
	end;
}

bat_b01,1,5,3	script	cell#bat_b01_b	CLEAR_NPC,{
OnRed:
	setwall "bat_b01",329,151,3,2,0,"bat_b01_b01";
	setwall "bat_b01",329,150,3,2,0,"bat_b01_b02";
	setwall "bat_b01",329,149,3,2,0,"bat_b01_b03";
	end;

OnGreen:
	delwall "bat_b01_b01";
	delwall "bat_b01_b02";
	delwall "bat_b01_b03";
	end;
}

bat_b01,1,6,1	script	time#bat_b01	CLEAR_NPC,{
OnEnable:
	donpcevent "Terapeuta da Batalha#b01_a::OnEnable";
	donpcevent "Terapeuta da Batalha#b01_b::OnEnable";
	end;

OnStop:
	donpcevent "Terapeuta da Batalha#b01_a::OnStop";
	donpcevent "Terapeuta da Batalha#b01_b::OnStop";
	end;
}

bat_b01,10,294,3	script	Terapeuta da Batalha#b01_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;
	
OnTimer26500:
	stopnpctimer;
	donpcevent "Terapeuta da Batalha#b01_a::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Terapeuta da Batalha#b01_a";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#b01_a";
	stopnpctimer;
	end;
}

bat_b01,389,14,3	script	Terapeuta da Batalha#b01_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Terapeuta da Batalha#b01_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Terapeuta da Batalha#b01_b";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#b01_b";
	stopnpctimer;
	end;
}

bat_b01,1,5,3	script	countdown#bat_b01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_b01", "Tenente Axl Rose : Vamos atacar e destruir o Cristal!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_b01", "Tenente Swandery : Mesmo que Guillaume esteja lutando contra nossa vitória, ela já é nossa! Vamos mostrar a eles o nosso poder.",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_b01", "Marollo VII : Guillaume Marollo, Croix Marollo! E seus seguidores!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_b01", "Marollo VII : Ambos Exércitos são muito competitivos, por isso é difícil julgar qual grupamento é superior.",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_b01", "Marollo VII : Essa batalha Flavius é um desperdício de tempo. Logo decidirei a vitória e a derrota pelo seu progresso.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_b01", "Marollo VII : Se vocês não aceitarem esse resultado, tentem a sorte em outro vale de batalha!",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_b01", "Axl Rose, Swandery : Sim, senhor.",bc_map,"0x99CC00";
	stopnpctimer;
	end;
}

bat_b01,1,8,3	script	time_event#bat_b01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	set .Flavius_Timer, 0;
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;
	
OnTimer60000:
	set .Flavius_Timer, .Flavius_Timer+1;
	set .@event_timer, callfunc("bg_getarenainfo", 3, "event_timer");
	if( .Flavius_Timer >= .@event_timer )
	{
		set $@FlaviusBG1, 2;
		if( $@FlaviusBG1_CroixScore > $@FlaviusBG1_GuillScore )
		{
			mapannounce "bat_b01", "Marollo VII: O Exército de Croix é o vitorioso por seu desempenho nas Batalhas!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 2;
			setarray .@result[0], 1, 1, 0, 0;
		}
		else if( $@FlaviusBG1_GuillScore > $@FlaviusBG1_CroixScore )
		{
			mapannounce "bat_b01", "Marollo VII: O Exército de Guillaume é o vitorioso por seu desempenho nas Batalhas!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 1;
			setarray .@result[0], 0, 0, 1, 1;
		}
		else
		{
			mapannounce "bat_b01", "Marollo VII: Ambos Exércitos não cumpriram o objetivo, isto é sinal de um empate!",bc_map,"0x99CC00";
			set $@FlaviusBG1_Victory, 3;
			setarray .@result[0], 2, 2, 2, 2;
		}
		
		// Computa Ranking e faz entrega de recompensas
		callfunc "bg_finish", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, $@FlaviusBG1_Victory, $@FlaviusBG1_GuillScore, $@FlaviusBG1_CroixScore;
		
		donpcevent "time#bat_b01::OnStop";
		donpcevent "countdown#bat_b01::OnStop";
		donpcevent "start#bat_b01_Out::OnBegin";
		enablenpc "Tenente#b01_a";
		enablenpc "Tenente#b01_b";
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		stopnpctimer;
	}
	else
	{
		stopnpctimer;
		initnpctimer;
	}
	end;
}

-	script	start#bat_b01_Out	FAKE_NPC,{
	end;

OnBegin:
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_b01", "Por favor fale com o Tenente para sair do campo de batalha.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_b01", "Em 30 segundos, o Tenente irá dispersar o grupamento.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_b01", "Fale com o Tenente para sair do campo de batalha imediatamente.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_b01", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set $@FlaviusBG1_GuillScore,0;
	set $@FlaviusBG1_CroixScore,0;
	set $@FlaviusBG1_Victory, 0;
	bg_kick_team $@FlaviusBG1_id1;
	bg_kick_team $@FlaviusBG1_id2;
	disablenpc "Tenente#b01_a";
	disablenpc "Tenente#b01_b";
	mapwarp "bat_b01","bat_room",154,150;
	maprespawnguildid "bat_b01",0,3; // Just in case someone else
	end;
}

// Bandeiras de Guillaume
bat_b01,81,83,4	script	Acampamento Guillaume#flag21	GUILD_FLAG,{
	end;
	
OnBatB01Emblem:
	bg_set_npc $@FlaviusBG1_id1;
	end;
}

bat_b01,94,83,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag22	GUILD_FLAG
bat_b01,81,66,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag23	GUILD_FLAG
bat_b01,94,66,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag24	GUILD_FLAG
bat_b01,139,142,6	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag25	GUILD_FLAG
bat_b01,139,158,6	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag26	GUILD_FLAG
bat_b01,110,161,6	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag27	GUILD_FLAG
bat_b01,110,137,6	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag28	GUILD_FLAG
bat_b01,63,135,6	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag29	GUILD_FLAG
bat_b01,63,165,6	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag30	GUILD_FLAG
bat_b01,10,296,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag31	GUILD_FLAG


// Bandeiras de Croix
bat_b01,306,233,4	script	Acampamento Croix#flag21	GUILD_FLAG,{
	end;
	
OnBatB01Emblem:
	bg_set_npc $@FlaviusBG1_id2;
	end;
}

bat_b01,317,233,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag22	GUILD_FLAG
bat_b01,306,216,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag23	GUILD_FLAG
bat_b01,317,216,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag24	GUILD_FLAG
bat_b01,257,158,2	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag25	GUILD_FLAG
bat_b01,257,141,2	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag26	GUILD_FLAG
bat_b01,297,164,2	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag27	GUILD_FLAG
bat_b01,297,136,2	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag28	GUILD_FLAG
bat_b01,336,161,2	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag29	GUILD_FLAG
bat_b01,336,139,2	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag30	GUILD_FLAG
bat_b01,389,16,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag31	GUILD_FLAG

bat_b01,1,10,3	script	Liberar Todos#b01	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Precisa de minha ajuda?";
		next;
		switch(select("Liberar todos.:Cancelar.")) {
		case 1:
			mes "Tchau.";
			close2;
			mapwarp "bat_b01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}

// Tenentes
// -----------------------------------------------------------------
bat_b01,10,294,3	script	Tenente#b01_a	4_M_KY_HEAD,{
	mes "[Axl Rose]";
	if( getcharid(4) == $@FlaviusBG1_id2 )
	{
		mes "O que?? Uns dos soldados do Exército de Croix está fazendo no acampamento de Guillaume!!";
		mes "Um soldado irá te conduzir ao seu grupamento.";
		close2;
		warp "bat_b01", 389, 14;
		end;
	}
	else if( getcharid(4) == $@FlaviusBG1_id1 )
	{
		if( $@FlaviusBG1_Victory == 0 )
		{
			mes "A Batalha ainda não foi definida soldado, vá agora mesmo ao seu posto, o Exército de Guillaume precisa de você!";
			close;
		}
		else if( $@FlaviusBG1_Victory == 1 )
		{
			mes "Abençoado o Exército de Guillaume!";
			mes strcharinfo(0) + ", vamos aproveitar nossa gloriosa vitória!";
		}
		else if( $@FlaviusBG1_Victory == 2 ) {
			mes "Você perdeu, mas se dedicou a esta batalha por Guillaume Marollo!";
			mes "Considere esta derrota como uma lição, e na próxima vez você sairemos vitoriosos.";
		}
		else {
			mes "Esta de fato foi uma grande Batalha!";
			mes "Nem todos podem sair vitoriosos, considere este empate como uma lição e na próxima vez sairemos vitoriosos.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "Um Cívil em nosso campo de batalha?";
		mes "Iremos te conduzir a um local seguro!";
		close2;
	}
	warp "prontera", 156, 176;
	end;

OnInit:
	disablenpc "Tenente#b01_a";
	end;
}

bat_b01,389,14,3	script	Tenente#b01_b	4_M_CRU_HEAD,{
	mes "[Swandery]";
	if( getcharid(4) == $@FlaviusBG1_id1 )
	{
		mes "O que?? Uns dos soldados do Exército de Guillaume está fazendo no acampamento de Croix!!";
		mes "Um soldado irá te conduzir ao seu grupamento.";
		close2;
		warp "bat_b01", 10, 294;
		end;
	}
	else if( getcharid(4) == $@FlaviusBG1_id2 )
	{
		if( $@FlaviusBG1_Victory == 0 )
		{
			mes "A Batalha ainda não foi definida soldado, vá agora mesmo ao seu posto, o Exército de Croix precisa de você!";
			close;
		}
		else if( $@FlaviusBG1_Victory == 1 ) {
			mes "Ah " + strcharinfo(0) + ", não fique triste.";
			mes "Mesmo que não ganhamos, fizemos o nosso melhor.";
			mes "Não esqueça desta batalha, ganharemos a próxima.";
		}
		else if( $@FlaviusBG1_Victory == 2 )
		{
			mes "Abençoado o Exército de Croix!";
			mes strcharinfo(0) + ", vamos aproveitar nossa gloriosa vitória!";
		}
		else {
			mes "Esta de fato foi uma grande Batalha!";
			mes "Nem todos podem sair vitoriosos, não se esqueça desta batalha!";
			mes "Ganharemos a próxima.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "Um Cívil em nosso campo de batalha?";
		mes "Iremos te conduzir a um local seguro!";
		close2;
	}
	warp "prontera", 156, 176;
	end;

OnInit:
	disablenpc "Tenente#b01_b";
	end;
}

// MapFlags
// -----------------------------------------------------------------
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	nomemo
bat_b01	mapflag	noteleport
bat_b01	mapflag	nosave
bat_b01	mapflag	noreturn
bat_b01	mapflag	nobranch
bat_b01	mapflag	nopenalty
bat_b01	mapflag	battleground	2