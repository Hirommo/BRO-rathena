/*.-----------------------------------------------------------------.
  .    ____                          __                             .
  .   /\  _`\                       /\ \__  __                      .
  .   \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __      .
  .    \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\    .
  .     \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/    .
  .      \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\   .
  .       \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/   .
  .                                                                 .
  .          2014~2017 © Creative Services and Development          .
  .                      www.creativesd.com.br                      .
  .-----------------------------------------------------------------.
  . Script:                                                         .
  .    Battleground Warfare (1º Desfiladeiro de Tierra)             .
  .-----------------------------------------------------------------.
  . Autor: Romulo SM (sbk_)                          Versão: 1.0    .
  .                                                                 .
  . Compatibilidade:                                                .
  .    eAthena, rAthena, Hercules e derivações.                     .
  .-----------------------------------------------------------------.
  . Descrição:                                                      .
  .    Batalha no qual os jogadores devem destruír o suprimento do  .
  .    adversário.                                                  .
  *-----------------------------------------------------------------*/
  
// Engine das Batalhas Campais do Desfiladeiro de Tierra
// -----------------------------------------------------------------
bat_a01,15,15,3	script	start#bat_a01	CLEAR_NPC,{
OnInit:
	// Removendo Jogadores da Arena
	mapwarp "bat_a01","bat_room",154,150;
	
	// Criando Fila de Espera e Times especifico para Arena
	queue_create(1, "1º Desfiladeiro de Tierra", 0, 0, "start#bat_a01::OnReadyCheck");
	set $@TierraBG1_id1, bg_create_team(1, "bat_a01", 50, 374, 352, 342, 0, "start#bat_a01::OnGuillaumeQuit", "", "start#bat_a01::OnGuillWO");
	set $@TierraBG1_id2, bg_create_team(2, "bat_a01", 42, 16, 353, 52, 1, "start#bat_a01::OnCroixQuit", "", "start#bat_a01::OnCroixWO");
	
	// Expulsando Jogadores
	bg_kick_team($@TierraBG1_id1, 0);
	bg_kick_team($@TierraBG1_id2, 0);
	
	// Íniciando Eventos
	set .global_announce, 0;
	set .BlockStatus, 0;
	donpcevent "start#bat_a01::OnReadyCheck";
	
	// Seta NPC's de Bandeiras com o Simbolo do Exército.
	donpcevent "::OnBatA01Emblem";
	end;

OnReadyCheck:
	if( .BlockStatus )
	{
		sleep 5000;
		donpcevent "start#bat_a01::OnReadyCheck";
	}
	
	set .BlockStatus, 1;

	set .@min_player, callfunc("bg_getarenainfo", 1, "min_player");
	set .@max_player, callfunc("bg_getarenainfo", 1, "max_player");
	set .@a_name$, callfunc("bg_getarenainfo", 1, "arena_name");
	set .@min_player, (.@min_player*2);
	
	if( .@max_player > MAX_BG_MEMBERS )
	{
		bg_console "(BG_ID: 1) Maximo de jogadores em uma equipe ultrapassa o limite estabelecido, definindo o maximo de jogadores para " + MAX_BG_MEMBERS + ".";
		set .@max_player, MAX_BG_MEMBERS;
	}
	
	queue_get_players 1;
	set .@playlist_team1, bg_team_size($@TierraBG1_id1);
	set .@playlist_team2, bg_team_size($@TierraBG1_id2);
	
	if( !.@queue_count )
	{
		set .BlockStatus, 0;
		end;
	}
	else if( $@TierraBG1 && $@TierraBG1 < 2 && .@queue_count && (.@playlist_team1 < .@max_player || .@playlist_team2 < .@max_player) )
	{
		set .@guill, 0;
		set .@croix, 0;
		set .@count_g, 0;
		set .@count_c, 0;
		
		// ---------------------------------------------------
		// -             Igualando os Exércitos              -
		// ---------------------------------------------------
		if( .@playlist_team1 < .@playlist_team2 )
		{
			set .@guill, .@playlist_team2-.@playlist_team1;
			for( set .@i,0; .@i < .@guill; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_g, .@count_g + 1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 1; // Reset
			set .@playlist_team1, bg_team_size($@TierraBG1_id1);
		}
		else if( .@playlist_team2 < .@playlist_team1 )
		{
			set .@croix, .@playlist_team1-.@playlist_team2;
			for( set .@i,0; .@i < .@croix; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_c, .@count_c + 1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Croix em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 1; // Reset
			set .@playlist_team2, bg_team_size($@TierraBG1_id2);
		}
		// ---------------------------------------------------
		// -            Expandindo os Exércitos              -
		// ---------------------------------------------------
		if( .@queue_count && .@playlist_team1 == .@playlist_team2 && .@playlist_team1 < .@max_player && .@playlist_team2 < .@max_player )
		{
			set .@guill, .@max_player-.@playlist_team1;
			set .@croix, .@max_player-.@playlist_team2;
			
			if( .@queue_count > (.@guill+.@croix) )
				set .@queue_count, (.@guill+.@croix);
				
			if( .@queue_count%2 )
				set .@queue_count, .@queue_count-1;
			
			set .@size, .@queue_count/2;
			for( set .@i, 0; .@i < .@size; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_g$[.@count_g], .@queue_player$[.@i];
				set .@count_g, .@count_g + 1;
			}
			
			for( set .@i,.@size; .@i < .@queue_count; set .@i, .@i + 1 )
			{
				bg_join_team $@TierraBG1_id2, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_c$[.@count_c], .@queue_player$[.@i];
				set .@count_c, .@count_c + 1;
			}
		}
		
		if( .@count_c || .@count_g )
		{
			announce "[Batalhas Campais]: " + (.@count_c+.@count_g) + " jogadores estão sendo enviados para uma batalha que está acontecendo em " + .@a_name$ + ", os Exércitos foram equilibrados e expandidos.", bc_all, "0xFFCE00";
			sleep 3000;
		}
		
		deletearray .@queue_cid;
		deletearray .@queue_aid;
		deletearray .@queue_player$;
		set .@queue_count, 0;
		queue_get_players 1; // Reset
		
		set .@queue_size, queue_size(1);
		set .@team1_size, bg_team_size($@TierraBG1_id1);
		set .@team2_size, bg_team_size($@TierraBG1_id2);
		
		if( .@team1_size < .@max_player || .@team2_size < .@max_player )
		{
			set .@size, (2*.@max_player)-(.@team1_size+.@team2_size)-.@queue_size;
			announce "[Batalhas Campais]: Ainda há " + (.@size) + " vagas para uma batalha que está acontecendo em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	else if( $@TierraBG1 && !.global_announce && .@queue_count >= .@min_player )
	{
		announce "[Batalhas Campais]: A batalha está completa, mas há uma batalha acontecendo em " + .@a_name$ + ", todos jogadores que estão na fila de espera devem aguardar.", bc_all, "0xFFCE00";
		set .global_announce, 1;
	}
	else if( .@queue_count < .@min_player )
		announce "[Batalhas Campais]: Falta " + (.@min_player-.@queue_count) + " jogadores na batalha em " + .@a_name$ + "!", bc_all, "0xFFCE00";
	else
	{
		// ---------------------------------------------------
		// -                  Balanceamento                  -
		// ---------------------------------------------------
		if( .@queue_count > (.@max_player*2) )
			set .@queue_count, (.@max_player*2);
			
		if( .@queue_count%2 )
			set .@queue_count, .@queue_count-1;
			
		if( .@queue_count >= .@min_player )
		{
			for( set .@i,0; .@i < .@queue_count; set .@i, .@i + 2 )
			{
				// Ingressando no Exército de Guillaume
				bg_join_team $@TierraBG1_id1, 1, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume, será teleportado em 10 segundos.";
				
				// Ingressando no Exército de Croix
				bg_join_team $@TierraBG1_id2, 1, .@queue_aid[.@i+1];
				queue_leave 0, .@queue_aid[.@i+1];
				message .@queue_player$[.@i+1], "Você ingressou no Exército de Croix, será teleportado em 10 segundos.";
			}
		}
		
		bg_refresh_patent $@TierraBG1_id1;
		bg_refresh_patent $@TierraBG1_id2;
		set .global_announce, 0;
		set $@TierraBG1, 1;
		announce "[Batalhas Campais]: Ambos os Exércitos, estão completo boa sorte nas batalhas em " + .@a_name$ + ".", bc_all, "0xFFCE00";
		set $@TierraBG1_CroixScore, 0;
		set $@TierraBG1_GuillScore, 0;
		bg_updatescore "bat_a01",0,0;
		donpcevent "start#bat_a01::OnEnable";
		donpcevent "BattleGround#queue_main::OnStopTimer";
		sleep 3000;
		initnpctimer;
		
		if( .@queue_count < (2*.@max_player) )
		{
			sleep 3000;
			set .@count, (2*.@max_player)-.@queue_count;
			announce "[Batalhas Campais]: Ainda há " + .@queue_count + " vagas nas batalhas em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	set .BlockStatus, 0;
	end;
	
OnEnable:
	bg_set_respawn $@TierraBG1_id1, 352, 342;
	bg_set_respawn $@TierraBG1_id2, 353, 52;
	donpcevent "OBJ#bat_a01_a::OnKill";
	donpcevent "OBJ#bat_a01_a::OnEnable";
	donpcevent "OBJ#bat_a01_b::OnKill";
	donpcevent "OBJ#bat_a01_b::OnEnable";
	donpcevent "barricade#bat_a01_a::OnKill";
	donpcevent "barricade#bat_a01_a::OnEnable";
	donpcevent "barricade#bat_a01_b::OnKill";
	donpcevent "barricade#bat_a01_b::OnEnable";
	donpcevent "OBJ#bat_a01_n::OnKill";
	donpcevent "OBJ#bat_a01_n::OnEnable";
	donpcevent "NOBJ_mob#bat_a01_a::OnKill";
	donpcevent "NOBJ_mob#bat_a01_b::OnKill";
	donpcevent "Terapeuta da Batalha#a01_a::OnEnable";
	donpcevent "Terapeuta da Batalha#a01_b::OnEnable";
	donpcevent "countdown#bat_a01::OnEnable";
	donpcevent "time_event#bat_a01::OnEnable";
	disablenpc "Ferreiro de Guillaume#a01";
	disablenpc "Ferreiro de Croix#bat_a01";
	disablenpc "Tenente#a01_a";
	disablenpc "Tenente#a01_b";
	end;

OnGuillaumeQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 1, 0, 0, 1, 0, $@TierraBG1_CroixScore;
	donpcevent "start#bat_a01::OnReadyCheck";
	end;
	
OnCroixQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 1, 0, 1, 1, $@TierraBG1_GuillScore, 0;
	donpcevent "start#bat_a01::OnReadyCheck";
	end;

OnGuillWO:
	if( queue_size(1) )
		end;
	
	// Sinaliza a Vitória
	set $@TierraBG1, 2;
	set $@TierraBG1_Victory, 2;
	
	// Computa Ranking e faz entrega de recompensas
	callfunc "bg_finish", 1, $@TierraBG1_id1, $@TierraBG1_id2, $@TierraBG1_Victory, $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
	
	// Inicia eventos de Finalização.
	donpcevent "Terapeuta da Batalha#a01_a::OnStop";
	donpcevent "Terapeuta da Batalha#a01_b::OnStop";
	donpcevent "countdown#bat_a01::OnStop";
	donpcevent "time_event#bat_a01::OnStop";
	donpcevent "start#bat_a01_Out::OnBegin";
	mapannounce "bat_a01", "Tenente Swandery: Os covardes de Guillaume fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	enablenpc "Tenente#a01_a";
	enablenpc "Tenente#a01_b";
	sleep 2000;
	bg_warp $@TierraBG1_id1,"bat_a01",50,374;
	bg_warp $@TierraBG1_id2,"bat_a01",42,16;
	end;
	
OnCroixWO:
	if( queue_size(1) )
		end;
	
	// Sinaliza a Vitória
	set $@TierraBG1, 2;
	set $@TierraBG1_Victory, 1;
	
	// Computa Ranking e faz entrega de recompensas
	callfunc "bg_finish", 1, $@TierraBG1_id1, $@TierraBG1_id2, $@TierraBG1_Victory, $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
	
	// Inicia eventos de Finalização.
	donpcevent "Terapeuta da Batalha#a01_a::OnStop";
	donpcevent "Terapeuta da Batalha#a01_b::OnStop";
	donpcevent "countdown#bat_a01::OnStop";
	donpcevent "time_event#bat_a01::OnStop";
	donpcevent "start#bat_a01_Out::OnBegin";
	mapannounce "bat_a01", "Tenente Axl Rose: Os covardes de Croix fugiram. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
	enablenpc "Tenente#a01_a";
	enablenpc "Tenente#a01_b";
	sleep 2000;
	bg_warp $@TierraBG1_id1,"bat_a01",50,374;
	bg_warp $@TierraBG1_id2,"bat_a01",42,16;
	end;

// Checagem de Jogadores na Arena.
// -----------------------------------------------------------------
OnTimer60000:
	stopnpctimer;
	if( !getmapusers("bat_a01") )
	{
		set $@TierraBG1, 0;
		bg_kick_team $@TierraBG1_id1;
		bg_kick_team $@TierraBG1_id2;
		donpcevent "start#bat_a01_Out::OnStop";
		
		if( $@BG_MODE )
			donpcevent "BattleGround#queue_main::OnReload";
		else
			donpcevent "start#bat_a01::OnReadyCheck";
	}
	else
		initnpctimer;
	end;
}

bat_a01,15,16,3	script	OBJ#bat_a01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG1_id1,"bat_a01",176,345,"Suprimentos",1909,"","OBJ#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_a::OnMyMobDead") < 1) {
		set $@TierraBG1_CroixScore, $@TierraBG1_CroixScore+1;
		set .@score_round, callfunc("bg_getarenainfo",1,"score_round");
		bg_updatescore "bat_a01", $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
		
		if( $@TierraBG1_CroixScore >= .@score_round )
		{
			donpcevent "Terapeuta da Batalha#a01_a::OnStop";
			donpcevent "Terapeuta da Batalha#a01_b::OnStop";
			donpcevent "countdown#bat_a01::OnStop";
			donpcevent "time_event#bat_a01::OnStop";
			set $@TierraBG1, 2;
			set $@TierraBG1_Victory, 2;
			enablenpc "Tenente#a01_a";
			enablenpc "Tenente#a01_b";
			donpcevent "start#bat_a01_Out::OnBegin";
			mapannounce "bat_a01", "Tenente Swandery: Uau! Nós destruímos os Suprimentos do Exército de Guillaume. Nós vencemos!",bc_map,"0xFFCE00";
			
			// Computa Ranking e faz entrega de recompensas
			callfunc "bg_finish", 1, $@TierraBG1_id1, $@TierraBG1_id2, $@TierraBG1_Victory, $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
			
			bg_warp $@TierraBG1_id1,"bat_a01",50,374;
			bg_warp $@TierraBG1_id2,"bat_a01",42,16;
		}
		else {
			donpcevent "start#bat_a01::OnEnable";
			bg_warp $@TierraBG1_id1,"bat_a01",50,374;
			bg_warp $@TierraBG1_id2,"bat_a01",42,16;
			sleep 1000;
			mapannounce "bat_a01", "Tenente Swandery: Uau! Nós destruímos os Suprimentos do Exército de Guillaume. Nós vencemos esta rodada!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a01,15,17,3	script	OBJ#bat_a01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG1_id2,"bat_a01",167,50,"Suprimentos",1910,"","OBJ#bat_a01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_b::OnMyMobDead") < 1) {
		set $@TierraBG1_GuillScore, $@TierraBG1_GuillScore+1;
		set .@score_round, callfunc("bg_getarenainfo",1,"score_round");
		bg_updatescore "bat_a01", $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
		if( $@TierraBG1_GuillScore >= .@score_round )
		{
			donpcevent "Terapeuta da Batalha#a01_a::OnStop";
			donpcevent "Terapeuta da Batalha#a01_b::OnStop";
			donpcevent "countdown#bat_a01::OnStop";
			donpcevent "time_event#bat_a01::OnStop";
			set $@TierraBG1, 2;
			set $@TierraBG1_Victory, 1;
			enablenpc "Tenente#a01_a";
			enablenpc "Tenente#a01_b";
			donpcevent "start#bat_a01_Out::OnBegin";
			mapannounce "bat_a01", "Tenente Axl Rose : Uau! Nós destruímos os Suprimentos do Exército de Croix. Nós vencemos!",bc_map,"0xFFCE00";
			
			// Computa Ranking e faz entrega de recompensas
			callfunc "bg_finish", 1, $@TierraBG1_id1, $@TierraBG1_id2, $@TierraBG1_Victory, $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
			
			bg_warp $@TierraBG1_id1,"bat_a01",50,374;
			bg_warp $@TierraBG1_id2,"bat_a01",42,16;
		}
		else {
			donpcevent "start#bat_a01::OnEnable";
			bg_warp $@TierraBG1_id1,"bat_a01",50,374;
			bg_warp $@TierraBG1_id2,"bat_a01",42,16;
			sleep 1000;
			mapannounce "bat_a01", "Tenente Swandery: Uau! Nós destruímos os Suprimentos do Exército de Guillaume. Nós vencemos esta rodada!",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a01,15,18,3	script	barricade#bat_a01_a	CLEAR_NPC,{
OnEnable:
	for ( set.@i,185; .@i < 202; set .@i,.@i+1 )
		bg_monster $@TierraBG1_id1,"bat_a01",.@i,266,"Barricada",1906,"","barricade#bat_a01_a::OnMyMobDead";
	setwall "bat_a01",186,266,16,6,1,"bat_a01_c1";
	end;

OnKill:
	killmonster "bat_a01","barricade#bat_a01_a::OnMyMobDead";
	delwall "bat_a01_c1";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","barricade#bat_a01_a::OnMyMobDead") < 17) {
		killmonster "bat_a01","barricade#bat_a01_a::OnMyMobDead";
		delwall "bat_a01_c1";
		enablenpc "Ferreiro de Guillaume#a01";
		mapannounce "bat_a01", "Tenente Axl Rose : A Barrica do desfiladeiro foi destruída! Aonde está nosso Ferreiro? Precisamos reconstruir a barricada.",bc_map,"0xFFCE00";
	}
	end;
}

bat_a01,15,19,3	script	barricade#bat_a01_b	CLEAR_NPC,{
OnEnable:
	for ( set .@i,169; .@i < 186; set .@i,.@i+1 )
		bg_monster $@TierraBG1_id2,"bat_a01",.@i,129,"Barricada",1906,"","barricade#bat_a01_b::OnMyMobDead";
	setwall "bat_a01",170,129,16,6,1,"bat_a01_g1";
	end;

OnKill:
	killmonster "bat_a01","barricade#bat_a01_b::OnMyMobDead";
	delwall "bat_a01_g1";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","barricade#bat_a01_b::OnMyMobDead") < 17) {
		killmonster "bat_a01","barricade#bat_a01_b::OnMyMobDead";
		delwall "bat_a01_g1";
		enablenpc "Ferreiro de Croix#bat_a01";
		mapannounce "bat_a01", "Tenente Swandery : A Barricada do desfiladeiro foi destruída! Aonde está nosso Ferreiro? Precisamos reconstruir a barricada.",bc_map,"0xFFCE00";
	}
	end;
}

bat_a01,15,20,3	script	OBJ#bat_a01_n	CLEAR_NPC,{
OnEnable:
	monster "bat_a01",273,203,"Bandeira Neutra",1911,1,"OBJ#bat_a01_n::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_n::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_n::OnMyMobDead") < 17) {
		bg_set_respawn getcharid(4),301,209;
		if (getcharid(4) == $@TierraBG1_id1) {
			donpcevent "NOBJ_mob#bat_a01_a::OnEnable";
			mapannounce "bat_a01", "O Exército de Guillaume obteve um Ponto Estratégico, portanto está com vantagem.",bc_map,"0xFFCE00";
		}
		else {
			donpcevent "NOBJ_mob#bat_a01_b::OnEnable";
			mapannounce "bat_a01", "O Exército de Croix obteve um Ponto Estratégico, portanto está com vantagem.",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a01,15,21,3	script	NOBJ_mob#bat_a01_a	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a01_b::OnKill";
	bg_monster $@TierraBG1_id1,"bat_a01",272,204,"Guardião de Guillaume",1949,"Guardião","NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id1,"bat_a01",272,213,"Guardião de Guillaume",1949,"Guardião","NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id1,"bat_a01",273,197,"Guardião de Guillaume",1950,"Guardião","NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a01,15,22,3	script	NOBJ_mob#bat_a01_b	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a01_a::OnKill";
	bg_monster $@TierraBG1_id2,"bat_a01",272,204,"Guardião de Croix",1949,"Guardião","NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id2,"bat_a01",272,213,"Guardião de Croix",1949,"Guardião","NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id2,"bat_a01",273,197,"Guardião de Croix",1950,"Guardião","NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","NOBJ_mob#bat_a01_b::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a01,185,270,1	script	Ferreiro de Guillaume#a01	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG1_id1) {
		mes "[Ferreiro de Guillaume]";
		mes "Estamos em urgência! A Barricada foi destruída!";
		mes "Podemos recuperar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "Temos tudo isso exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro de Guillaume]";
				mes "Você trouxe pedras suficientes! Vamos reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal, e trançando as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro de Guillaume]";
				mes "Uau! Está feito.";
				mes "Agora estamos mais aliviados.";
				delitem 7049,50; //Pedras
				set .@score, bg_get_score(31);
				bg_set_score 31, (.@score+16); // Set Score
				donpcevent "barricade#bat_a01_a::OnEnable";
				close2;
				disablenpc "Ferreiro de Guillaume#a01";
				end;
			}
			else {
				mes "[Ferreiro de Guillaume]";
				mes "Você não tem Pedras suficientes!";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "^3131FFPrecisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, então por favor, se apresse.";
				close;
			}
		case 2:
			mes "[Ferreiro de Guillaume]";
			mes "Os inimigos estão chegando! Vamos evacuar e partir daqui!";
			close;
		}
	}
	else {
		mes "[Ferreiro de Guillaume]";
		mes "Lá, o inimigo está chegando";
		close;
	}

OnInit:
	disablenpc "Ferreiro de Guillaume#a01";
	end;
}

bat_a01,170,121,5	script	Ferreiro de Croix#bat_a01	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG1_id2) {
		mes "[Ferreiro de Croix]";
		mes "Estamos em urgência! A Barricada foi destruída!";
		mes "Podemos recuperar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "Temos tudo isso exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro de Croix]";
				mes "Você trouxe pedras suficientes! Vamos reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal, e trançando as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro de Croix]";
				mes "Uau! Está feito.";
				mes "Agora estamos mais aliviados.";
				delitem 7049,50; //Pedras
				set .@score, bg_get_score(30);
				bg_set_score 30, (.@score+16); // Set Score
				donpcevent "barricade#bat_a01_b::OnEnable";
				close2;
				disablenpc "Ferreiro de Croix#bat_a01";
				end;
			}
			else {
				mes "[Ferreiro de Croix]";
				mes "Você não tem Pedras suficientes!";
				next;
				mes "[Ferreiro de Croix]";
				mes "^3131FFPrecisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, então por favor, se apresse.";
				close;
			}
		case 2:
			mes "[Ferreiro de Croix]";
			mes "There are enemies coming! Let's evacuate from here!";
			close;
		}
	}
	else {
		mes "[Ferreiro de Croix]";
		mes "Os inimigos estão chegando! Vamos evacuar e partir daqui!";
		close;
	}

OnInit:
	disablenpc "Ferreiro de Croix#bat_a01";
	end;
}

bat_a01,53,377,3	script	Terapeuta da Batalha#a01_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta da Batalha#a01_a";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#a01_a";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	end;

OnTimer26500:
	donpcevent "Terapeuta da Batalha#a01_a::OnEnable";
	end;
}

bat_a01,45,19,3	script	Terapeuta da Batalha#a01_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta da Batalha#a01_b";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#a01_b";
	stopnpctimer;
	end;

OnTimer26500:
	donpcevent "Terapeuta da Batalha#a01_b::OnEnable";
	end;
}

bat_a01,60,216,3	script	Fantasma#bat_a01_n	4_GHOSTRING,{
	specialeffect2 EF_HEAL;
	mes "[Fantasma]";
	mes "Boo...Boo...";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_n_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_n_warp";
	end;

OnTimer26500:
	donpcevent "Fantasma#bat_a01_n::OnEnable";
	end;
}

bat_a01,55,213,0	script	bat_a01_rp1_n_warp	FAKE_NPC,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_n_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a01",301,209;
	end;
}

bat_a01,194,267,0	script	barri_warp_up#bat_a01_a	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id1)
		warp "bat_a01",194,261;
	end;
}

bat_a01,194,265,0	script	barri_warp_down#bat_a01a	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id1)
		warp "bat_a01",194,270;
	end;
}

bat_a01,177,130,0	script	barri_warp_up#bat_a01_b	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id2)
		warp "bat_a01",178,125;
	end;
}

bat_a01,177,128,0	script	barri_warp_down#bat_a01b	FAKE_NPC,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id2)
		warp "bat_a01",178,134;
	end;
}

bat_a01,169,227,0	script	underladd#bat_a01_1	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a01",178,228;
	end;
}

bat_a01,208,164,0	script	underladd#bat_a01_2	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a01",200,171;
	end;
}

// Bandeiras de Guillaume
bat_a01,171,309,4	script	Acampamento Guillaume#flag1	GUILD_FLAG,{
	end;

OnBatA01Emblem:
	bg_set_npc $@TierraBG1_id1;
	end;
}

bat_a01,149,310,4	duplicate(Acampamento Guillaume#flag1)	Acampamento Guillaume#flag2	GUILD_FLAG
bat_a01,119,336,2	duplicate(Acampamento Guillaume#flag1)	Acampamento Guillaume#flag3	GUILD_FLAG
bat_a01,118,357,2	duplicate(Acampamento Guillaume#flag1)	Acampamento Guillaume#flag4	GUILD_FLAG
bat_a01,150,380,0	duplicate(Acampamento Guillaume#flag1)	Acampamento Guillaume#flag5	GUILD_FLAG
bat_a01,173,380,0	duplicate(Acampamento Guillaume#flag1)	Acampamento Guillaume#flag6	GUILD_FLAG
bat_a01,210,344,6	duplicate(Acampamento Guillaume#flag1)	Acampamento Guillaume#flag7	GUILD_FLAG
bat_a01,350,325,4	duplicate(Acampamento Guillaume#flag1)	Acampamento Guillaume#flag8	GUILD_FLAG
bat_a01,358,325,4	duplicate(Acampamento Guillaume#flag1)	Acampamento Guillaume#flag9	GUILD_FLAG

// Bandeiras de Croix
bat_a01,138,12,4	script	Acampamento Croix#flag1	GUILD_FLAG,{
	end;

OnBatA01Emblem:
	bg_set_npc $@TierraBG1_id2;
	end;
}

bat_a01,108,36,2	duplicate(Acampamento Croix#flag1)	Acampamento Croix#flag2	GUILD_FLAG
bat_a01,108,63,2	duplicate(Acampamento Croix#flag1)	Acampamento Croix#flag3	GUILD_FLAG
bat_a01,136,87,0	duplicate(Acampamento Croix#flag1)	Acampamento Croix#flag4	GUILD_FLAG
bat_a01,167,86,0	duplicate(Acampamento Croix#flag1)	Acampamento Croix#flag5	GUILD_FLAG
bat_a01,199,49,5	duplicate(Acampamento Croix#flag1)	Acampamento Croix#flag6	GUILD_FLAG
bat_a01,168,16,4	duplicate(Acampamento Croix#flag1)	Acampamento Croix#flag7	GUILD_FLAG
bat_a01,357,74,0	duplicate(Acampamento Croix#flag1)	Acampamento Croix#flag8	GUILD_FLAG
bat_a01,348,74,0	duplicate(Acampamento Croix#flag1)	Acampamento Croix#flag9	GUILD_FLAG

bat_a01,1,5,3	script	countdown#bat_a01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_a01", "Tenente Axl Rose : Vamos atacar e queirmar todo o Depósito de Suprimentos de Croix!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_a01", "Tenente Swandery : Mestre do Valhalla! Presenteia-nos com coragem e uma fé infalível!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_a01", "Marollo VII : Guillaume Marollo, Croix Marollo! E todos os seus seguidores!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_a01", "Marollo VII : Ambos Exércitos são muito competitivos, de forma que nenhum acampamento será facilmente destruído. Isso significa que os Reinos de Morollo jamais serão derrotados!",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_a01", "Marollo VII : Acho que seria melhor encerrar a batalha, e chamarmos isso de um empate.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_a01", "Marollo VII : Mantenham fé e lealdade por um momento, e vamos resolver a batalha pelo Desfiladeiro de Tierra.",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_a01", "Axl Rose, Swandery : Sim senhor.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	stopnpctimer;
	end;
}

bat_a01,1,8,3	script	time_event#bat_a01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	set .Tierra_Timer, 0;
	stopnpctimer;
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;
	
OnTimer60000:
	set .Tierra_Timer, .Tierra_Timer+1;
	set .@event_timer, callfunc("bg_getarenainfo",1,"event_timer");
	if( .Tierra_Timer >= .@event_timer )
	{
		set $@TierraBG1, 2;
		if( $@TierraBG1_GuillScore > $@TierraBG1_CroixScore )
		{
			set $@TierraBG1_Victory, 1;
			mapannounce "bat_a01", "Marollo VII: O Exército de Guillaume destruíu mais depósitos de suprimentos, é o vencedor!",bc_map,"0x99CC00";
		}
		else if( $@TierraBG1_CroixScore > $@TierraBG1_GuillScore )
		{
			set $@TierraBG1_Victory, 2;
			mapannounce "bat_a01", "Marollo VII: O Exército de Croix destruíu mais depósitos de suprimentos, é o vencedor!",bc_map,"0x99CC00";
		}
		else {
			set $@TierraBG1_Victory, 3;
			mapannounce "bat_a01", "Marollo VII: Ambos Exércitos não cumpriram com seus objetivos, isso é uma empate!",bc_map,"0x99CC00";
		}
		
		// Computa Ranking e faz entrega de recompensas
		callfunc "bg_finish", 1, $@TierraBG1_id1, $@TierraBG1_id2, $@TierraBG1_Victory, $@TierraBG1_GuillScore, $@TierraBG1_CroixScore;
		
		donpcevent "Terapeuta da Batalha#a01_a::OnStop";
		donpcevent "Terapeuta da Batalha#a01_b::OnStop";
		donpcevent "countdown#bat_a01::OnStop";
		donpcevent "start#bat_a01_Out::OnBegin";
		enablenpc "Tenente#a01_a";
		enablenpc "Tenente#a01_b";
		bg_warp $@TierraBG1_id1,"bat_a01",50,374;
		bg_warp $@TierraBG1_id2,"bat_a01",42,16;
		stopnpctimer;
	}
	else
	{
		stopnpctimer;
		initnpctimer;
	}
	end;
}

-	script	start#bat_a01_Out	FAKE_NPC,{
	end;

OnBegin:
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_a01", "Por favor fale com o Tenente para sair do campo de batalha.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_a01", "Em 30 segundos, o Tenente irá dispersar o grupamento.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_a01", "Fale com o Tenente para sair do campo de batalha imediatamente.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_a01", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set $@TierraBG1_GuillScore,0;
	set $@TierraBG1_CroixScore,0;
	set $@TierraBG1_Victory, 0;
	bg_kick_team $@TierraBG1_id1;
	bg_kick_team $@TierraBG1_id2;
	disablenpc "Tenente#a01_a";
	disablenpc "Tenente#a01_b";
	mapwarp "bat_a01","bat_room",154,150;
	maprespawnguildid "bat_a01",0,3; // Just in case someone else
	bg_updatescore "bat_a01",0,0;
	end;
}

/*
bat_a01,351,75,3	script	Croix Camp Soldier#bat_a01_guide	4_M_RASWORD,{
	mes "Loading...";
	close;
}

bat_a01,356,326,3	script	Guillaume Camp Soldier#bat_a01_guide	4_M_RASWORD,{
	mes "Loading...";
	close;
}
*/

bat_a01,1,1,3	script	Libere Todos#a01	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Precisa da minha ajuda?";
		next;
		switch(select("Liberar todos.:Cancelar.")) {
		case 1:
			mes "Tchau.";
			close2;
			mapwarp "bat_a01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}

// Tenentes
// -----------------------------------------------------------------
bat_a01,53,377,3	script	Tenente#a01_a	4_M_KY_HEAD,{
	mes "[Axl Rose]";
	if( getcharid(4) == $@TierraBG1_id2 )
	{
		mes "O que?? Uns dos soldados do Exército de Croix está fazendo no acampamento de Guillaume!!";
		mes "Um soldado irá te conduzir ao seu grupamento.";
		close2;
		warp "bat_a01", 45, 19;
		end;
	}
	else if( getcharid(4) == $@TierraBG1_id1 )
	{
		if( $@TierraBG1_Victory == 0 )
		{
			mes "A Batalha ainda não foi definida soldado, vá agora mesmo ao seu posto, o Exército de Guillaume precisa de você!";
			close;
		}
		else if( $@TierraBG1_Victory == 1 )
		{
			mes "Abençoado o Exército de Guillaume!";
			mes strcharinfo(0) + ", vamos aproveitar nossa gloriosa vitória!";
		}
		else if( $@TierraBG1_Victory == 2 ) {
			mes "Você perdeu, mas se dedicou a esta batalha por Guillaume Marollo!";
			mes "Considere esta derrota como uma lição, e na próxima vez você sairemos vitoriosos.";
		}
		else {
			mes "Esta de fato foi uma grande Batalha!";
			mes "Nem todos podem sair vitoriosos, considere este empate como uma lição e na próxima vez sairemos vitoriosos.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "Um Cívil em nosso campo de batalha?";
		mes "Iremos te conduzir a um local seguro!";
		close2;
	}
	warp "prontera", 156, 176;
	end;

OnInit:
	disablenpc "Tenente#a01_a";
	end;
}

bat_a01,45,19,3	script	Tenente#a01_b	4_M_CRU_HEAD,{
	mes "[Swandery]";
	if( getcharid(4) == $@TierraBG1_id1 )
	{
		mes "O que?? Uns dos soldados do Exército de Guillaume está fazendo no acampamento de Croix!!";
		mes "Um soldado irá te conduzir ao seu grupamento.";
		close2;
		warp "bat_a01", 45, 19;
		end;
	}
	else if( getcharid(4) == $@TierraBG1_id2 )
	{
		if( $@TierraBG1_Victory == 0 )
		{
			mes "A Batalha ainda não foi definida soldado, vá agora mesmo ao seu posto, o Exército de Croix precisa de você!";
			close;
		}
		else if( $@TierraBG1_Victory == 1 ) {
			mes "Ah " + strcharinfo(0) + ", não fique triste.";
			mes "Mesmo que não ganhamos, fizemos o nosso melhor.";
			mes "Não esqueça desta batalha, ganharemos a próxima.";
		}
		else if( $@TierraBG1_Victory == 2 )
		{
			mes "Abençoado o Exército de Croix!";
			mes strcharinfo(0) + ", vamos aproveitar nossa gloriosa vitória!";
		}
		else {
			mes "Esta de fato foi uma grande Batalha!";
			mes "Nem todos podem sair vitoriosos, não se esqueça desta batalha!";
			mes "Ganharemos a próxima.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "Um Cívil em nosso campo de batalha?";
		mes "Iremos te conduzir a um local seguro!";
		close2;
	}
	warp "prontera", 156, 176;
	end;

OnInit:
	disablenpc "Tenente#a01_b";
	end;
}

// MapFlags
// -----------------------------------------------------------------
bat_a01	mapflag	nowarp
bat_a01	mapflag	nowarpto
bat_a01	mapflag	nomemo
bat_a01	mapflag	noteleport
bat_a01	mapflag	nosave
bat_a01	mapflag	noreturn
bat_a01	mapflag	nobranch
bat_a01	mapflag	nopenalty
bat_a01	mapflag	battleground	2