/*.-----------------------------------------------------------------.
  .    ____                          __                             .
  .   /\  _`\                       /\ \__  __                      .
  .   \ \ \/\_\  _ __    __     __  \ \ ,_\/\_\  __  __     __      .
  .    \ \ \/_/_/\`'__\/'__`\ /'__`\ \ \ \/\/\ \/\ \/\ \  /'__`\    .
  .     \ \ \s\ \ \ \//\  __//\ \d\.\_\ \ \_\ \ \ \ \_/ |/\  __/    .
  .      \ \____/\ \_\\ \____\ \__/.\_\\ \__\\ \_\ \___/ \ \____\   .
  .       \/___/  \/_/ \/____/\/__/\/_/ \/__/ \/_/\/__/   \/____/   .
  .                                                                 .
  .          2014~2017 © Creative Services and Development          .
  .                      www.creativesd.com.br                      .
  .-----------------------------------------------------------------.
  . Script:                                                         .
  .    Battleground Warfare (2ª Krieger von Midgard)                .
  .-----------------------------------------------------------------.
  . Autor: Romulo SM (sbk_)                          Versão: 1.1    .
  .                                                                 .
  . Compatibilidade:                                                .
  .    eAthena, rAthena, Hercules e derivações.                     .
  .-----------------------------------------------------------------.
  . Descrição:                                                      .
  .    Arena no qual os Exércitos se confrontam diretamente.        .                                                 .
  *-----------------------------------------------------------------*/

// Engine das Batalhas Campais de Krieger von Midgard
// -----------------------------------------------------------------
-	script	queue#bat_c02	FAKE_NPC,{
OnInit:
	// Criando Fila de Espera e Times especifico para Arena
	queue_create(6, "2ª Krieger von Midgard", 0, 0, "start#bat_c02::OnReadyCheck");
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;
	
OnEnter:
	set .new_player,1;
	end;
	
OnTimer60000:
	set .@min_player, callfunc("bg_getarenainfo", 6, "min_player");
	set .@max_player, callfunc("bg_getarenainfo", 6, "max_player");
	set .@a_name$, callfunc("bg_getarenainfo", 6, "arena_name");
	set .@group_id, callfunc("bg_getarenainfo", 6, "group_id");
	set .@group_status, callfunc("bg_getgroupinfo", .@group_id, "status");
	set .@min_player, (.@min_player*2);
	
	if( .@max_player > MAX_BG_MEMBERS )
	{
		bg_console "(BG_ID: 6) Maximo de jogadores em uma equipe ultrapassa o limite estabelecido, definindo o maximo de jogadores para " + MAX_BG_MEMBERS + ".";
		set .@max_player, MAX_BG_MEMBERS;
	}
	
	queue_get_players 6;
	set .@playlist_team1, bg_team_size($@KvM02BG_id1);
	set .@playlist_team2, bg_team_size($@KvM02BG_id2);
	if( .@queue_count <= 0 || .@group_id <= 0 || .@group_status <= 0 ) {
		set .new_player, 0;
		if( .@queue_count ) {
			for( set .@i, 0; .@i < .@queue_count; set .@i, .@i + 1 ) {
				queue_leave(0, .@queue_aid[.@i]);
				queue_leave_notify(6, 5, .@queue_aid[.@i]);
			}
		}
	}
	else if( $@KvM02BG && $@KvM02BG < 3 && .@queue_count && (.@playlist_team1 < .@max_player || .@playlist_team2 < .@max_player) )
	{
		set .@guill, 0;
		set .@croix, 0;
		set .@count_g, 0;
		set .@count_c, 0;
		
		// ---------------------------------------------------
		// -             Igualando os Exércitos              -
		// ---------------------------------------------------
		if( .@playlist_team1 < .@playlist_team2 )
		{
			set .@guill, .@playlist_team2-.@playlist_team1;
			for( set .@i,0; .@i < .@guill; set .@i, .@i + 1 )
			{
				bg_join_team $@KvM02BG_id1, 6, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_g, .@count_g + 1;
				set $@KvM02_Guillaume_Count, $@KvM02_Guillaume_Count+1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 6; // Reset
			set .@playlist_team1, bg_team_size($@KvM02BG_id1);
		}
		else if( .@playlist_team2 < .@playlist_team1 )
		{
			set .@croix, .@playlist_team1-.@playlist_team2;
			for( set .@i,0; .@i < .@croix; set .@i, .@i + 1 )
			{
				bg_join_team $@KvM02BG_id1, 6, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@count_c, .@count_c + 1;
				set $@KvM02_Croix_Count, $@KvM02_Croix_Count+1;
				message .@queue_player$[.@i], "Você ingressou no Exército de Croix em uma batalha em " + .@a_name$ + " que já está acontecendo, e será teleportado em 10 segundos.";
			}
			
			deletearray .@queue_cid;
			deletearray .@queue_aid;
			deletearray .@queue_player$;
			set .@queue_count, 0;
			queue_get_players 6; // Reset
			set .@playlist_team2, bg_team_size($@KvM02BG_id2);
		}
		// ---------------------------------------------------
		// -            Expandindo os Exércitos              -
		// ---------------------------------------------------
		if( .@queue_count && .@playlist_team1 == .@playlist_team2 && .@playlist_team1 < .@max_player && .@playlist_team2 < .@max_player )
		{
			set .@guill, .@max_player-.@playlist_team1;
			set .@croix, .@max_player-.@playlist_team2;
			
			if( .@queue_count > (.@guill+.@croix) )
				set .@queue_count, (.@guill+.@croix);
				
			if( .@queue_count%2 )
				set .@queue_count, .@queue_count-1;
			
			set .@size, .@queue_count/2;
			for( set .@i, 0; .@i < .@size; set .@i, .@i + 1 )
			{
				bg_join_team $@KvM02BG_id1, 6, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_g$[.@count_g], .@queue_player$[.@i];
				set .@count_g, .@count_g + 1;
				set $@KvM02_Guillaume_Count, $@KvM02_Guillaume_Count+1;
			}
			
			for( set .@i,.@size; .@i < .@queue_count; set .@i, .@i + 1 )
			{
				bg_join_team $@KvM02BG_id2, 6, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				set .@players_name_c$[.@count_c], .@queue_player$[.@i];
				set .@count_c, .@count_c + 1;
				set $@KvM02_Croix_Count, $@KvM02_Croix_Count+1;
			}
		}
		
		if( .@count_c || .@count_g )
		{
			bg_updatescore "bat_c02",$@KvM02_Guillaume_Count,$@KvM02_Croix_Count;
			announce "[Batalhas Campais]: " + (.@count_c+.@count_g) + " jogadores estão sendo enviados para uma batalha que está acontecendo em " + .@a_name$ + ", os Exércitos foram equilibrados e expandidos.", bc_all, "0xFFCE00";
			sleep 3000;
		}
		
		deletearray .@queue_cid;
		deletearray .@queue_aid;
		deletearray .@queue_player$;
		set .@queue_count, 0;
		queue_get_players 6; // Reset
		
		set .@queue_size, queue_size(6);
		set .@team1_size, bg_team_size($@KvM02BG_id1);
		set .@team2_size, bg_team_size($@KvM02BG_id2);
		
		if( .@team1_size < .@max_player || .@team2_size < .@max_player )
		{
			set .@size, (2*.@max_player)-(.@team1_size+.@team2_size)-.@queue_size;
			announce "[Batalhas Campais]: Ainda há " + (.@size) + " vagas para uma batalha que está acontecendo em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	else if( $@KvM02BG && !.global_announce && .@queue_count >= .@min_player )
	{
		set .new_player, 0;
		set .global_announce, 1;
		announce "[Batalhas Campais]: A batalha está completa, mas há uma batalha acontecendo em " + .@a_name$ + ", todos jogadores que estão na fila de espera devem aguardar.", bc_all, "0xFFCE00";
	}
	else if( .@queue_count < .@min_player ) {
		set .new_player, 0;
		if( .new_player )
			announce "[Batalhas Campais]: Falta " + (.@min_player-.@queue_count) + " jogadores na batalha em " + .@a_name$ + "!", bc_all, "0xFFCE00";
	
		// No Selected Arena, Remove Players from Queue.
		if( $@BG_MODE && $BG_ARENA != 6 ) {
			if( .@queue_count ) {
				for( set .@i, 0; .@i < .@queue_count; set .@i, .@i + 1 ) {
					queue_leave(0, .@queue_aid[.@i]);
					queue_leave_notify(6, 5, .@queue_aid[.@i]);
				}
			}
		}
	}
	else
	{
		// ---------------------------------------------------
		// -                Reiniciar Eventos                -
		// ---------------------------------------------------
		mapwarp "bat_d01","bat_room",154,150;
		bg_kick_team $@KvM02BG_id1;
		bg_kick_team $@KvM02BG_id2;
		set $@KvM02_Croix_Count, 0;
		set $@KvM02_Guillaume_Count, 0;
		disablenpc "Oficial KVM#KVM02A";
		disablenpc "Oficial KVM#KVM02B";
		donpcevent "BattleGround#queue_main::OnStopTimer";
		donpcevent "start#bat_c02_Out::OnStop";
		donpcevent "start#bat_c02::OnStopTimer";
		bg_updatescore "bat_c02",$@KvM02_Guillaume_Count,$@KvM02_Croix_Count;
		set .global_announce, 0;
		set .new_player, 0;
		set $@KvM02BG, 1;
		
		// ---------------------------------------------------
		// -                  Balanceamento                  -
		// ---------------------------------------------------
		if( .@queue_count > (.@max_player*2) )
			set .@queue_count, (.@max_player*2);
			
		if( .@queue_count%2 )
			set .@queue_count, .@queue_count-1;
			
		if( .@queue_count >= .@min_player )
		{
			for( set .@i,0; .@i < .@queue_count; set .@i, .@i + 2 )
			{
				// Ingressando no Exército de Guillaume
				bg_join_team $@KvM02BG_id1, 6, .@queue_aid[.@i];
				queue_leave 0, .@queue_aid[.@i];
				message .@queue_player$[.@i], "Você ingressou no Exército de Guillaume, será teleportado em 10 segundos.";
				set $@KvM02_Guillaume_Count, $@KvM02_Guillaume_Count+1;
				
				// Ingressando no Exército de Croix
				bg_join_team $@KvM02BG_id2, 6, .@queue_aid[.@i+1];
				queue_leave 0, .@queue_aid[.@i+1];
				message .@queue_player$[.@i+1], "Você ingressou no Exército de Croix, será teleportado em 10 segundos.";
				set $@KvM02_Croix_Count, $@KvM02_Croix_Count+1;
			}
		}
		
		bg_set_respawn $@KvM02BG_id1, 52, 129;
		bg_set_respawn $@KvM02BG_id2, 147, 55;
		bg_refresh_patent($@KvM02BG_id1);
		bg_refresh_patent($@KvM02BG_id2);
		announce "[Batalhas Campais]: Ambos os Exércitos, estão completo boa sorte nas batalhas em " + .@a_name$ + ".", bc_all, "0xFFCE00";
		sleep 3000;
		donpcevent "check#bat_c02::OnStartTimer";
		
		if( .@queue_count < (2*.@max_player) )
		{
			sleep 3000;
			set .@count, (2*.@max_player)-.@queue_count;
			announce "[Batalhas Campais]: Ainda há " + .@queue_count + " vagas nas batalhas em " + .@a_name$ + "!", bc_all, "0xFFCE00";
		}
	}
	stopnpctimer;
	initnpctimer;
	end;
}

-	script	start#bat_c02	FAKE_NPC,{
OnInit:
	// Removendo Jogadores da Arena
	mapwarp "bat_c02","bat_room",154,150;
	
	set $@KvM02BG_id1, bg_create_team(11, "bat_c02", 52, 129, 52, 129, 0, "start#bat_c02::OnGuillaumeQuit", "start#bat_c02::OnGuillaumeDie", "");
	set $@KvM02BG_id2, bg_create_team(12, "bat_c02", 147, 55, 147, 55, 1, "start#bat_c02::OnCroixQuit", "start#bat_c02::OnCroixDie", "");
	
	// Expulsando Jogadores
	bg_kick_team($@KvM02BG_id1, 0);
	bg_kick_team($@KvM02BG_id2, 0);
	
	// Definindo Barreiras
	setwall "bat_c02",54,122,6,7,0,"batc02wall_a";
	setwall "bat_c02",55,122,5,7,0,"batc02wall_b";
	setwall "bat_c02",140,56,6,7,0,"batc02wall_c";
	setwall "bat_c02",140,57,5,7,0,"batc02wall_d";
	
	// Seta NPC's de Bandeiras com o Simbolo do Exército.
	sleep 3000;
	donpcevent "::OnBatC02Emblem";
	end;

OnStartTimer:
	initnpctimer;
	end;

OnStopTimer:
	stopnpctimer;
	end;
	
OnGuillaumeQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 6, 0, 0, 1, 0, 0;
	if ($@KvM02BG == 2) {
		// Fix Die/Logout Check
		if( BAT_EV_FLAG <= 0 ) {
			set BAT_EV_FLAG,1;
			set $@KvM02_Guillaume_Count,$@KvM02_Guillaume_Count-1;
			bg_updatescore "bat_c02",$@KvM02_Guillaume_Count,$@KvM02_Croix_Count;
		}
		if( $@KvM02_Guillaume_Count < 1 )
			donpcevent "start#bat_c02::OnCroixWin";
		else {
			mapannounce "bat_c02", "O número de Soldados de Guillaume é "+$@KvM02_Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O número de Soldados de Croix é "+$@KvM02_Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	donpcevent "start#bat_c02::OnReadyCheck";
	end;
	
OnCroixQuit:
	callfunc "bg_clear_score", getcharid(0);
	callfunc "bg_ranking", getcharid(0), 6, 0, 1, 1, 0, 0;
	if ($@KvM02BG == 2) {
		// Fix Die/Logout Check
		if( BAT_EV_FLAG <= 0 ) {
			set BAT_EV_FLAG,1;
			set $@KvM02_Croix_Count,$@KvM02_Croix_Count-1;
			bg_updatescore "bat_c02",$@KvM02_Guillaume_Count,$@KvM02_Croix_Count;
		}
		if( $@KvM02_Croix_Count < 1 )
			donpcevent "start#bat_c02::OnGuillaumeWin";
		else {
			mapannounce "bat_c02", "O número de Soldados de Guillaume é "+$@KvM02_Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O número de Soldados de Croix é "+$@KvM02_Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	donpcevent "start#bat_c02::OnReadyCheck";
	end;
	
OnGuillaumeDie:
	if ($@KvM02BG == 2) {
		// Fix Die/Logout Check
		if( BAT_EV_FLAG <= 0 ) {
			set BAT_EV_FLAG,1;
			set $@KvM02_Guillaume_Count, $@KvM02_Guillaume_Count-1;
			bg_updatescore "bat_c02",$@KvM02_Guillaume_Count,$@KvM02_Croix_Count;
		}
		
		if( $@KvM02_Guillaume_Count < 1 )
			donpcevent "start#bat_c02::OnCroixWin";
		else {
			mapannounce "bat_c02", "O número de Soldados de Guillaume é "+$@KvM02_Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O número de Soldados de Croix é "+$@KvM02_Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;
	
OnCroixDie:
	if ($@KvM02BG == 2) {
		// Fix Die/Logout Check
		if( BAT_EV_FLAG <= 0 ) {
			set BAT_EV_FLAG,1;
			set $@KvM02_Croix_Count, $@KvM02_Croix_Count-1;
			bg_updatescore "bat_c02",$@KvM02_Guillaume_Count,$@KvM02_Croix_Count;
		}
		if( $@KvM02_Croix_Count < 1 )
			donpcevent "start#bat_c02::OnGuillaumeWin";
		else {
			mapannounce "bat_c02", "O número de Soldados de Guillaume é "+$@KvM02_Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O número de Soldados de Croix é "+$@KvM02_Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnGuillaumeWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 1;
	
	// Computa Ranking e faz entregua de recompensas
	callfunc "bg_finish", 6, $@KvM02BG_id1, $@KvM02BG_id2, $@KvM02BG_Victory, 0, 0;
	
	mapannounce "bat_c02", "Guillaume venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns aos membros do Exército de Guillaume.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Todos serão movidos para o ponto de início.",bc_map,"0x00ff00";
	donpcevent "start#bat_c02::OnStop";
	end;

OnCroixWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 2;
	
	// Computa Ranking e faz entregua de recompensas
	callfunc "bg_finish", 6, $@KvM02BG_id1, $@KvM02BG_id2, $@KvM02BG_Victory, 0, 0;
	
	mapannounce "bat_c02", "Croix venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns aos membros do Exército de Croix.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Todos serão movidos para o ponto de início.",bc_map,"0x00ff00";
	donpcevent "start#bat_c02::OnStop";
	end;

OnTimer1000:
	mapannounce "bat_c02", "Em 1 minuto o KVM irá começar.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	set .@event_timer, callfunc("bg_getarenainfo",6,"event_timer");
	mapannounce "bat_c02", "O tempo máximo para uma batalha KVM é de " + .@event_timer + " minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c02", "Por favor, prepare-se para a batalha KVM.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Você pode usar encatamentos em seus alidos.",bc_map,"0x00ff00";
	end;

OnTimer30000:
	mapannounce "bat_c02", "30 segundos restantes para começar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c02", "15 segundos restantes para começar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM02One";
	end;

OnTimer50000:
	mapannounce "bat_c02", "10 segundos restantes para começar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM02Two";
	end;

OnTimer55000:
	mapannounce "bat_c02", "5 segundos restantes para começar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM02One";
	end;

OnTimer59000:
	mapannounce "bat_c02", "KVM está começando agora.",bc_map,"0x00ff00";
	donpcevent "::OnKvM02Two";
	end;

OnTimer61000:
	set $@KvM02BG, 2;
	bg_warp $@KvM02BG_id1,"bat_c02",62,119;
	bg_warp $@KvM02BG_id2,"bat_c02",137,64;
	bg_set_respawn $@KvM02BG_id1,61,120;
	bg_set_respawn $@KvM02BG_id2,138,63;
	donpcevent "time_event#bat_c02::OnEnable";
	end;

OnStop:
	stopnpctimer;
	sleep 1000;
	// Warp Teams
	enablenpc "Oficial KVM#KVM02A";
	enablenpc "Oficial KVM#KVM02B";
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	donpcevent "start#bat_c02_Out::OnBegin";
	donpcevent "time_event#bat_c02::OnStop";
	end;
}

bat_room,197,211,0	warp	#kvm602	2,2,bat_room,154,150

// Starting Line
//============================================================
-	script	KVM02::CellEffect2	FAKE_NPC,{
	end;

OnKvM02One:
	specialeffect EF_CHANGECOLD;
	end;

OnKvM02Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c02,54,124,3	duplicate(CellEffect2)	#RedcellA1-2	HIDDEN_WARP_NPC
bat_c02,55,124,3	duplicate(CellEffect2)	#RedcellA2-2	HIDDEN_WARP_NPC
bat_c02,56,124,3	duplicate(CellEffect2)	#RedcellA3-2	HIDDEN_WARP_NPC
bat_c02,57,124,3	duplicate(CellEffect2)	#RedcellA4-2	HIDDEN_WARP_NPC
bat_c02,57,125,3	duplicate(CellEffect2)	#RedcellA5-2	HIDDEN_WARP_NPC
bat_c02,57,126,3	duplicate(CellEffect2)	#RedcellA6-2	HIDDEN_WARP_NPC
bat_c02,57,127,3	duplicate(CellEffect2)	#RedcellA7-2	HIDDEN_WARP_NPC

bat_c02,145,59,3	duplicate(CellEffect2)	#RedcellB1-2	HIDDEN_WARP_NPC
bat_c02,144,59,3	duplicate(CellEffect2)	#RedcellB2-2	HIDDEN_WARP_NPC
bat_c02,143,59,3	duplicate(CellEffect2)	#RedcellB3-2	HIDDEN_WARP_NPC
bat_c02,142,59,3	duplicate(CellEffect2)	#RedcellB4-2	HIDDEN_WARP_NPC
bat_c02,142,56,3	duplicate(CellEffect2)	#RedcellB5-2	HIDDEN_WARP_NPC
bat_c02,142,57,3	duplicate(CellEffect2)	#RedcellB6-2	HIDDEN_WARP_NPC
bat_c02,142,58,3	duplicate(CellEffect2)	#RedcellB7-2	HIDDEN_WARP_NPC

-	script	start#bat_c02_Out	FAKE_NPC,{
	end;

OnBegin:
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Por favor fale com o Oficial para sair do campo de batalha.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "Em 30 segundos, o Oficial irá dispersar o grupamento.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c02", "Fale com o Oficial para sair do campo de batalha imediatamente.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_c02", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set $@KvM02BG, 0;
	set $@KvM02_Croix_Count, 0;
	set $@KvM02_Guillaume_Count, 0;
	set $@KvM02BG_Victory, 0;
	bg_kick_team $@KvM02BG_id1;
	bg_kick_team $@KvM02BG_id2;
	disablenpc "Oficial KVM#KVM02A";
	disablenpc "Oficial KVM#KVM02B";
	mapwarp "bat_c02","bat_room",154,150;
	maprespawnguildid "bat_c02",0,3; // Just in case someone else
	bg_updatescore "bat_c02",0,0;
	if( $@BG_MODE )
		donpcevent "BattleGround#queue_main::OnReload";
	end;
}

-	script	time_event#bat_c02	FAKE_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	set .KvM_Timer, 0;
	stopnpctimer;
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;
	
OnTimer60000:
	set .KvM_Timer, .KvM_Timer+1;
	set .@event_timer, callfunc("bg_getarenainfo",6,"event_timer");
	set .@event_timer, .@event_timer-1;
	if( .KvM_Timer >= .@event_timer )
		mapannounce "bat_c02", "1 minuto restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	else
	{
		stopnpctimer; // Reset timertick...
		initnpctimer;
	}
	end;

OnTimer63000:
	mapannounce "bat_c02", "30 segundos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer64500:
	mapannounce "bat_c02", "15 segundos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer65000:
	mapannounce "bat_c02", "10 segundos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer65500:
	mapannounce "bat_c02", "5 segundos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer70000:
	mapannounce "bat_c02", "A batalha KVM terminou.",bc_map,"0x00ff00";
	if( $@KvM02_Croix_Count > $@KvM02_Guillaume_Count )
		donpcevent "start#bat_c02::OnCroixWin";
	else if( $@KvM02_Croix_Count < $@KvM02_Guillaume_Count )
		donpcevent "start#bat_c02::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM02BG, 3;
		set $@KvM02BG_Victory, 3;
		
		// Computa Ranking e faz entregua de recompensas
		callfunc "bg_finish", 6, $@KvM02BG1_id1, $@KvM02BG1_id2, $@KvM02BG1_Victory, 0, 0;
		
		mapannounce "bat_c02", "O número de Guillaume's é "+$@KvM02_Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "O número de Croix's é "+$@KvM02_Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "Esta batalha terminou em empate.",bc_map,"0x00ff00";
		donpcevent "start#bat_c02::OnStop";
	}
	stopnpctimer;
	end;
}

// Bandeiras de Guillaume
bat_c02,51,130,5	script	Exército de Guillaume#bat_c02_01	GUILD_FLAG,{
	end;
	
OnBatC02Emblem:
	bg_set_npc $@KvM02BG_id1;
	end;
}

bat_c02,64,130,4	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_02	GUILD_FLAG
bat_c02,72,130,4	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_03	GUILD_FLAG
bat_c02,80,130,4	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_04	GUILD_FLAG
bat_c02,88,130,4	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_05	GUILD_FLAG
bat_c02,96,130,4	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_06	GUILD_FLAG
bat_c02,104,130,4	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_07	GUILD_FLAG
bat_c02,112,130,4	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_08	GUILD_FLAG
bat_c02,51,119,6	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_09	GUILD_FLAG
bat_c02,51,112,6	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_10	GUILD_FLAG
bat_c02,51,104,6	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_11	GUILD_FLAG
bat_c02,51,96,6	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_12	GUILD_FLAG
bat_c02,51,88,6	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_13	GUILD_FLAG
bat_c02,76,106,2	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_14	GUILD_FLAG
bat_c02,82,113,0	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_15	GUILD_FLAG
bat_c02,89,107,6	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_16	GUILD_FLAG
bat_c02,82,100,4	duplicate(Exército de Guillaume#bat_c02_01)	Exército de Guillaume#bat_c02_17	GUILD_FLAG

// Bandeiras de Croix
bat_c02,148,53,1	script	Exército de Croix#bat_c02_01	GUILD_FLAG,{
	end;
	
OnBatC01Emblem:
	bg_set_npc $@KvM02BG_id2;
	end;
}

bat_c02,135,53,0	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_02	GUILD_FLAG
bat_c02,127,53,0	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_03	GUILD_FLAG
bat_c02,119,53,0	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_04	GUILD_FLAG
bat_c02,111,53,0	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_05	GUILD_FLAG
bat_c02,103,53,0	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_06	GUILD_FLAG
bat_c02,95,53,0	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_07	GUILD_FLAG
bat_c02,87,53,0	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_08	GUILD_FLAG
bat_c02,148,64,2	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_09	GUILD_FLAG
bat_c02,148,72,2	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_10	GUILD_FLAG
bat_c02,148,80,2	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_11	GUILD_FLAG
bat_c02,148,88,2	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_12	GUILD_FLAG
bat_c02,148,96,2	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_13	GUILD_FLAG
bat_c02,110,77,2	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_14	GUILD_FLAG
bat_c02,117,83,0	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_15	GUILD_FLAG
bat_c02,123,77,6	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_16	GUILD_FLAG
bat_c02,116,70,4	duplicate(Exército de Croix#bat_c02_01)	Exército de Croix#bat_c02_17	GUILD_FLAG

// Tenentes
// -----------------------------------------------------------------
bat_c02,56,130,3	script	Oficial KVM#KVM02A	4_M_KY_HEAD,{
	mes "[Tenente Axl Rose]";
	if( getcharid(4) == $@KvM02BG_id2 )
	{
		mes "O que?? Uns dos soldados do Exército de Croix está fazendo no acampamento de Guillaume!!";
		mes "Um soldado irá te conduzir ao seu grupamento.";
		close2;
		warp "bat_c02", 145, 56;
		end;
	}
	else if( getcharid(4) == $@KvM02BG_id1 )
	{
		if( $@KvM02BG < 3 )
		{
			mes "Aguarde um momento, a batalha ainda está acontecendo.";
			close;
		}
		else if( $@KvM02BG_Victory == 0 )
		{
			mes "A Batalha ainda não foi definida soldado, vá agora mesmo ao seu posto, o Exército de Guillaume precisa de você!";
			close;
		}
		else if( $@KvM02BG_Victory == 1 )
		{
			mes "Abençoado o Exército de Guillaume!";
			mes strcharinfo(0) + ", vamos aproveitar nossa gloriosa vitória!";
		}
		else if( $@KvM02BG_Victory == 2 ) {
			mes "Você perdeu, mas se dedicou a esta batalha por Guillaume Marollo!";
			mes "Considere esta derrota como uma lição, e na próxima vez você sairemos vitoriosos.";
		}
		else {
			mes "Esta de fato foi uma grande Batalha!";
			mes "Nem todos podem sair vitoriosos, considere este empate como uma lição e na próxima vez sairemos vitoriosos.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "Um Cívil em nosso campo de batalha?";
		mes "Iremos te conduzir a um local seguro!";
		close2;
	}
	warp "prontera", 156, 176;
	end;
	
OnInit:
	disablenpc "Oficial KVM#KVM02A";
	end;
}

bat_c02,143,53,7	script	Oficial KVM#KVM02B	4_M_CRU_HEAD,{
	mes "[Swandery]";
	if( getcharid(4) == $@KvM02BG_id1 )
	{
		mes "O que?? Uns dos soldados do Exército de Guillaume está fazendo no acampamento de Croix!!";
		mes "Um soldado irá te conduzir ao seu grupamento.";
		close2;
		warp "bat_c02", 54, 127;
		end;
	}
	else if( getcharid(4) == $@KvM02BG_id2 )
	{
		if( $@KvM02BG < 3 )
		{
			mes "Aguarde um momento, a batalha ainda está acontecendo.";
			close;
		}
		if( $@KvM02BG_Victory == 0 )
		{
			mes "A Batalha ainda não foi definida soldado, vá agora mesmo ao seu posto, o Exército de Croix precisa de você!";
			close;
		}
		else if( $@KvM02BG_Victory == 1 ) {
			mes "Ah " + strcharinfo(0) + ", não fique triste.";
			mes "Mesmo que não ganhamos, fizemos o nosso melhor.";
			mes "Não esqueça desta batalha, ganharemos a próxima.";
		}
		else if( $@KvM02BG_Victory == 2 )
		{
			mes "Abençoado o Exército de Croix!";
			mes strcharinfo(0) + ", vamos aproveitar nossa gloriosa vitória!";
		}
		else {
			mes "Esta de fato foi uma grande Batalha!";
			mes "Nem todos podem sair vitoriosos, não se esqueça desta batalha!";
			mes "Ganharemos a próxima.";
		}
		close2;
		bg_leave;
	}
	else {
		mes "Um Cívil em nosso campo de batalha?";
		mes "Iremos te conduzir a um local seguro!";
		close2;
	}
	warp "prontera", 156, 176;
	end;
	
OnInit:
	disablenpc "Oficial KVM#KVM02B";
	end;
}

// MapFlags
// -----------------------------------------------------------------
bat_c02	mapflag	nowarp
bat_c02	mapflag	nowarpto
bat_c02	mapflag	nomemo
bat_c02	mapflag	noteleport
bat_c02	mapflag	nosave
bat_c02	mapflag	noreturn
bat_c02	mapflag	nobranch
bat_c02	mapflag	nopenalty
bat_c02	mapflag	battleground	2
bat_c02	mapflag	nobgskillcall
bat_c02	mapflag	nobgrespawn