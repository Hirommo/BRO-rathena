prontera,120,126,4	script	Monster Wiki	497,{
Wiki:
	set .@GMLevel, 60;
	set .@n$,"[ ^a60536"+ strnpcinfo(1) +"^000000 ]";
	if (getgmlevel() < .@GMLevel) {
		if (.EventON){
			close2;
			message strcharinfo(0),"O evento está ativo no momento.";
			end;
		}
		mes .@n$;
		mes "Este evento é muito simples, no ^8B008BMonster Wiki^000000 você precisa adivinhar o nome do monstro que eu me transformo.";
		mes "São ^FF0000"+.Rounds+"^000000 rodadas e cada acerto você ganha um prêmio.";
		mes "^777777Prêmio do Evento:^000000";
		mes " ^FF0000»^000000 ^0000FF"+getitemname(.Prize)+".^000000";
		close;
	}
	mes .@n$;
	if (.EventON) {
		mes "O evento está: ^0000FF[Ligado]^000000";
		mes "Deseja desligar o evento?";
	} else {
		mes "O evento está: ^FF0000[Desligar]^000000";
		mes "Deseja ligar o evento?";
	}
	mes "^777777Selecione o que deseja fazer^000000.";
	next;
	if(select("[^a60536»^000000] Ligar/Desligar.:[^a60536»^000000] Sair.") == 2) close;
		if (.EventON) {
			set .EventON, 0; set .Timer, 0;
			setnpctimer 0; stopnpctimer;
			announce "[Monster Wiki]: O evento foi cancelado por um membro da staff.",bc_all|bc_blue;
			deletepset 1;
			setnpcdisplay strnpcinfo(0), 497;
			close;
		}
		set .EventON,1; set .Timer,1; setnpctimer 0; initnpctimer;
		set .ResetCounter,.ResetCounter + 1;
		announce "[Monster Wiki]: O evento começa em 3 minutos. Digite @evento para participar.", bc_all|bc_blue;
		close;
OnInit:
	set .EventON, 0;
	set .Wait, 0;
	set .Winner, 0;
	set .ResetCounter, 0;
	set .Rounds, 15;
	set .Prize, 12103;
	set .PrizeAmt, 1;
	set .Rule,1;
	setarray .MVP[0],1038,1039,1046,1059,1086,1087,1112,1115,1147,1150,1157,1159,1190,1251,1252,1272,1312,1373,
		1389,1399,1418,1492,1502,1511,1583,1623,1630,1646,1647,1648,1649,1650,1651,1658,1685,1688,1708,1719,
		1734,1751,1768,1779,1785,1802,1832,1871,1874,1885,1917,1980,2022,2068,2087,2131,2156,2165;

	set .BlackList$, "1003,1006,1017,1021,1022,1027,1043,1075,1136,1137,1168," + 
	    "1171,1172,1173,1181,1187,1210,1217,1218,1222,1223,1224,1225,1226,1227,1228," + 
	    "1233,1284,1407,1411,1414,1495,1501,1900,1996,2000,2001,2002,2003,2004," + 
	    "2005,2006,2007,2011,2012,2025,2028,2029,2030,2031,2032,2033,2034,2035," + 
	    "2036,2037,2038,2039,2040,2041,2042,2043,2044,2045,2046,2047,2048,2049," + 
	    "2050,2051,2052,2053,2054,2055,2056,2057,2058,2059,2060,2061,2062,2063," + 
	    "2064,2065,2066,2067,2075,2076,2077,2078,2079,2080,2081,2083,2084,2085," + 
	    "2086,2087,2088,2089,2090,2091,2092,2093,2094,2095,2096,2097,2098,2099," + 
	    "2100,2101,2012,2103,2104,2105,2106,2107,2108,2109,2110,2111,2112,2113," + 
	    "2114,2115,2116,2117,2118,2119,2120,2121,2123,2124,2125,1496,";
	end;

OnClock1400:
OnClock2200:
OnClock0400:
	set .ResetCounter,.ResetCounter + 1;
	set .EventON,1;
	set .Timer,1;
	set .Wait,1;
	announce "[Monster Wiki]: O evento começa em 3 minutos. Digite @evento para participar.", bc_all|bc_blue;
	setnpctimer 0;
	initnpctimer;
	end;
OnTimer10000:
	if (.Timer || .Change) end;
	set .Wait,0;
	goto iDisguise;
	end;
OnTimer30000:
	if (.Timer) end;
	set .Change,0;
	setnpcdisplay strnpcinfo(0), 497;
	npctalk "O nome certo era "+$MonsterName$+". Aguarde 10 segundos vou me transformar novamente.";
	specialeffect EF_DETECT2;
	set $MonsterName$,"";
	deletepset 1;
	stopnpctimer;
	setnpctimer 0;
	initnpctimer;
	end;
OnTimer60000:
	if (.Timer != 1) end;
	announce "[Monster Wiki]: O evento começa em 2 minutos. Venha até Prontera, 127, 123.", bc_all|bc_blue;
	end;
OnTimer120000:
	if (.Timer != 1) end;
	announce "[Monster Wiki]: O evento começa em 1 minuto. Venha até Prontera, 127, 123.", bc_all|bc_blue;
	end;
OnTimer180000:
	if (.Timer != 1) end;
	announce "[Monster Wiki]: O evento acaba de começar, corra pra participar!", bc_all|bc_blue;
	set .Timer,0; stopnpctimer;
	setnpctimer 0; initnpctimer;
iDisguise:
	if (.Rule == 1) {
		set .Winner,0;
		set .Monster,1000+rand(1,995);
		if( compare(","+ .BlackList$ +"," , ","+ .Monster +"," )) goto iDisguise;
		if( .Monster == .LastMonster) goto iDisguise;
		set .LastMonster, .Monster;
		set $MonsterName$, getmonsterinfo(.Monster,0);
	}
	if( .Rule == 2 ){
		set .Winner, 0;
		set .Monster, rand(49);
		set $MonsterName$, getmonsterinfo(.MVP[.Monster], 0);
	}
	deletepset 1;
	defpattern 1, "([^:]+):.*"+$MonsterName$+".*", "iCorrect";
	activatepset 1;
	if (.Rule==1) setnpcdisplay strnpcinfo(0), .Monster;
	if (.Rule==2) setnpcdisplay strnpcinfo(0), .MVP[.Monster];
	set .Change,1;
	setnpctimer 0;
	end;
iCorrect:
	if (.Winner) {
		dispbottom "[ Alguém já ganhou essa rodada ].";
		end;
	}
	set .Winner,1;
	set .RoundCount,.RoundCount+1;
	deletepset 1;
	activatepset 1;
	getitem .Prize, .PrizeAmt;
	query_sql( "INSERT INTO `rank_evento` SET `char_id`='"+ getcharid(0) +"',`name`='"+ strcharinfo(0) +"',`count`='1' ON DUPLICATE KEY UPDATE `count`=`count`+1" );
	mapannounce "prt_smart","[Monster Wiki]: » "+strcharinfo(0)+" « acertou, o disfarce era: ["+$MonsterName$+"].", bc_map|bc_blue;
	if (.RoundCount >= .Rounds) {
		setnpcdisplay strnpcinfo(0), 497;
		set .RoundCount, 0; 
		set .Change, 0; 
		set .EventON, 0;
		setnpctimer 0; stopnpctimer;
		npctalk "Obrigado a todos que participaram. Esta foi a última rodada, até logo!";
		end;
	}
	setnpcdisplay strnpcinfo(0), 497;
	set .Change, 0;
	setnpctimer 0;
}