-	script	DS#Evt	-1,{
OnInit:
	bindatcmd "descubrasenha", strnpcinfo(0) +"::OnStarPass", 60, 100;
	bindatcmd "descubrasenhaportal", strnpcinfo(0) +"::OnStarPortal", 60, 100;
	disablenpc "Descubra Senha#Evt";
	disablenpc "Portal#Evt";
	end;
OnStarPass:
	dispbottom "---------------------------------------------------------";
	dispbottom "[ Digite '@descubrasenha on' para ligar ].";
	dispbottom "[ Digite '@descubrasenha off' para desligar ].";
	dispbottom "---------------------------------------------------------";
	dispbottom "[ Digite '@descubrasenhaportal on' para ligar ].";
	dispbottom "[ Digite '@descubrasenhaportal off' para desligar ].";
	dispbottom "---------------------------------------------------------";
	if( compare( .@atcmd_parameters$, "on" ) ){
		mes "[ ^a60536Descubra Senha^000000 ]";
		mes "Antes de iniciar o evento, configure a senha.";
		mes "^777777Digite a senha que os jogadores devem descobrir:^000000";
		next;
		input .@lb_pass$;
		mes "[ ^a60536Descubra Senha^000000 ]";
		mes "A senha configurada é: ^ff0000[ "+ .@lb_pass$ +" ].^000000";
		set($lb_pass$, .@lb_pass$);
		next;
		if( select("[^EF8319»^000000] Ativar Evento.","[^EF8319»^000000] Sair.") == 2 ) close;
			close2;
			set $@evt_pass, 1;
			enablenpc "Descubra Senha#Evt";
			announce "[Descubra Senha]: O "+ strcharinfo(0) +" colocou uma nova senha. Descubra e pegue seu prêmio!", bc_all|bc_blue;
			end;
	}
	if( compare( .@atcmd_parameters$, "off" ) ){
		set $@evt_pass, 0;
		disablenpc "Descubra Senha#Evt";
		end;
	}
	end;
OnStarPortal:
	dispbottom "---------------------------------------------------------";
	dispbottom "[ Digite '@descubrasenhaportal on' para ligar ].";
	dispbottom "[ Digite '@descubrasenhaportal off' para desligar ].";
	dispbottom "---------------------------------------------------------";
	if( compare( .@atcmd_parameters$, "on" ) ){
		set $@evt_pass, 1;
		enablenpc "Portal#Evt";
		announce "[Descubra Senha]: O portal para o evento está em » Prontera, 125, 150 «.", bc_all|bc_blue;
		end;
	}
	if( compare( .@atcmd_parameters$, "off" ) ){
		set $@evt_pass, 0;
		disablenpc "Portal#Evt";
		end;
	}
	end;
}
guild_vs2-2,36,34,6	script	Saída#Evt	858,{
	warp "prontera", 156, 191;
	end;
}
prontera,123,146,6	script	Portal#Evt	723,{
	mes "[ ^a60536Descubra Senha^000000 ]";
	mes "Neste evento a Staff SmartRO colocará uma senha e dará dicas, o primeiro a acertar leva o prêmio!";
	mes "^777777Prêmio do Evento:^000000";
	mes " ^ff0000»^000000 ^0000ff"+ getitemname(21024) +"^000000";
	mes " ^ff0000»^000000 ^0000ff"+ getitemname(21039) +"^000000";
	mes " ^ff0000»^000000 ^0000ff"+ getitemname(21044) +"^000000";
	mes " ^ff0000»^000000 ^0000ff"+ getitemname(12210) +"^000000";
	mes " ^ff0000»^000000 ^0000ff"+ getitemname(12103) +"^000000";
	mes " ^ff0000»^000000 ^0000ff10,000.000x Zeny^000000";
	dispbottom "[ Chances aleatórias de prêmiação ].";
	if( !$@evt_pass ){
		close2;
		dispbottom "[ Evento desativado no momento ].";
		end;
	}
	next;
	if( select("[^ff0000»^000000] Entrar no Evento.:[^ff0000»^000000] Saír.") == 2 ) close;
		close2;
		announce "[Descubra Senha]: O jogador » "+strcharinfo(0)+" « entrou no evento!", bc_all|bc_blue;
		warp "guild_vs2-2", 50, 43;
		end;
}
guild_vs2-2,50,50,6	script	Descubra Senha#Evt	811,{
	specialeffect 263, AREA, strnpcinfo(0);
	specialeffect 403, AREA, strnpcinfo(0);
	specialeffect 800, AREA, strnpcinfo(0);
	emotion rand(2,30);
	if( !$@evt_pass ){
		dispbottom "[ Evento desativado no momento ].";
		end;
	}
	mes "[ ^a60536Descubra Senha^000000 ]";
	mes "Tem um palpite de senha? tente sua sorte!";
	mes "Digite a senha:";
	next;
	input .@lb_pass$;
	mes "[ ^a60536Descubra Senha^000000 ]";
	if( .@lb_pass$ != $lb_pass$  || $@evt_pass == 0 ){
		set .@erros, .@erros + 1;
		specialeffect2 800;
		specialeffect2 380;
		specialeffect2 507;
		message strcharinfo(0), "[ Erros "+ .@erros + " ].";
		mes "Você errou!";
		close;
	}
	set $@evt_pass, 0;
	disablenpc strnpcinfo(0);
	mes "Parabéns, ^0000ff"+ strcharinfo(0) +"^000000!";
	mes "Você acertou e seu prêmio será entregue agora mesmo.";
	specialeffect2 509;
	specialeffect2 709;
	query_sql( "INSERT INTO `rank_evento` SET `char_id`='"+ getcharid(0) +"',`name`='"+ strcharinfo(0) +"',`count`='1' ON DUPLICATE KEY UPDATE `count`=`count`+1" );
	switch( rand(1,10) ){
		case 1:
		case 2:
		case 3:
			getitem 21024, rand(1,3);
			dispbottom "[ Você ganhou "+ getitemname(21024) +" ].";
			break;
		case 4: 
			getitem 21039, rand(1,3);
			dispbottom "[ Você ganhou "+ getitemname(21039) +" ].";
			break;
		case 5:
			getitem 21044, rand(1,3);
			dispbottom "[ Você ganhou "+ getitemname(21044) +" ].";
			break;
		case 6:
		case 7:
			getitem 12210, rand(1,3);
			dispbottom "[ Você ganhou "+ getitemname(12210) +" ].";
			break;
		case 8:
		case 9:
			set Zeny, Zeny + 10000000;
			dispbottom "[ Você ganhou 10,000.000x Zeny ].";
			break;
		case 10:
			getitem 12103, rand(1,10);
			dispbottom "[ Você ganhou "+ getitemname(12103) +" ].";
			break;
	}
	close2;
	npctalk "[ A senha era: "+ .@lb_pass$ + " ].";
	announce "[Descubra Senha]: O jogador » "+strcharinfo(0)+" « descobriu a senha e foi prêmiado!!!", bc_all|bc_blue;
	end;
}